Boxy Cider Carp

High

# Inflating all calculations and forcing insolevency through sending unsanitized LayerZero messages to the `GlobalVariables` contract

### Summary

The `GlobalVariables` contract is expected to handle cross-chain messages from other Autonomint system contract, such as for when `oftOrCollateralReceiveFromOtherChains` is called:
```solidity
    /**
     * @dev Sends msg to destination chain, to transfer the OFT and Native tokens to get in this chain,
     * can only be called by cds or borrow liquidation contracts
     * @param functionToDo What function the destination chain contract needs to do,
     * E.g Token Transfer or updating data only like that
     * @param oftTransferData USDa amount to get from dst chain with address to receive in this chain and amount\
     * @param collateralTokenTransferData Collaterals which are oft to get from dst chain
     * @param refundAddress Address to refund the gas fee paid by msg.sender
     */
    function oftOrCollateralReceiveFromOtherChains(
        FunctionToDo functionToDo,
        USDaOftTransferData memory oftTransferData,
        CollateralTokenTransferData memory collateralTokenTransferData,
        CallingFunction callingFunction,
        address refundAddress
    ) external payable onlyCoreContracts returns (MessagingReceipt memory receipt)
    {
        // Define the payload to pass to _lzsend function with same order of data
        bytes memory _payload = abi.encode(
            functionToDo,
            callingFunction,
            callingFunction == CallingFunction.CDS_WITHDRAW ? oftTransferData.tokensToSend : 0,
            callingFunction == CallingFunction.BORROW_WITHDRAW ? oftTransferData.tokensToSend : 0,
            callingFunction == CallingFunction.BORROW_LIQ ? oftTransferData.tokensToSend : 0,
            CDSInterface.LiquidationInfo(0,0,0,0,IBorrowing.AssetName.DUMMY,0),
            0,
            oftTransferData,
            collateralTokenTransferData,
            omniChainData,
            IBorrowing.AssetName.DUMMY,
            s_collateralData[IBorrowing.AssetName.DUMMY]
        );
        // We need to send the gas fee required for transfer of oft and eth from dst to this chain
        // Fee required for this omnichain transaction
        MessagingFee memory _fee;
        // Fee required for USDa transfer, which we need to send to dst chain, so that dst chain don't need to pay gas fees
        MessagingFee memory feeForTokenTransfer;
        // Fee required for ETH,WeETH,RsETH transfer, which we need to send to dst chain, so that dst chain don't need to pay gas fees
        MessagingFee memory feeForCollateralTransfer;
        // Gas needs to send
        bytes memory _options;
        bytes memory options = OptionsBuilder.newOptions().addExecutorLzReceiveOption(60000, 0);
        // Define sendParam for OFT transfer
        SendParam memory _sendParam = SendParam(
            dstEid,
            bytes32(uint256(uint160(oftTransferData.recipient))),
            oftTransferData.tokensToSend * 10,
            oftTransferData.tokensToSend,
            options,
            "0x",
            "0x"
        );
        // Calculate the fee needed in dst chain to transfer oft from dst to this chain
        feeForTokenTransfer = usda.quoteSend(_sendParam, false);
        // DEfine options to get ETH from other chain with inbuilt function from layer zero //?addExecutorNativeDropOption
        options = OptionsBuilder.newOptions().addExecutorLzReceiveOption(280000, 0).addExecutorNativeDropOption(
                uint128(collateralTokenTransferData.ethToSend),
                bytes32(uint256(uint160(dstGlobalVariablesAddress)))
            );
        // Calculate the fee needed to transfer ETH from dst to this chain
        feeForCollateralTransfer = quote(
            FunctionToDo(1),
            IBorrowing.AssetName.DUMMY,
            options,
            false
        );
        //? Based on the functionToDo in dst chain, define options
        // If we need to get only the USDa from dst chain
        if (functionToDo == FunctionToDo.TOKEN_TRANSFER) {
            // Define options with native fee required in dst chain
            // to transfer this USDa using 'addExecutorNativeDropOption' with fee amount and this chain address where to get
            _options = OptionsBuilder.newOptions().addExecutorLzReceiveOption(500000, 0).addExecutorNativeDropOption(
                    uint128(feeForTokenTransfer.nativeFee),
                    bytes32(uint256(uint160(dstGlobalVariablesAddress)))
                );
            // If we need to get only the Collaterals from dst chain
        } else if (functionToDo == FunctionToDo.COLLATERAL_TRANSFER) {
            // Define options with native fee required in dst chain
            // to transfer this collaterals using 'addExecutorNativeDropOption' with fee amount and this chain address where to get
            _options = OptionsBuilder.newOptions().addExecutorLzReceiveOption(300000, 0).addExecutorNativeDropOption(
                    // Since, we have 3 collaterals, we are passing native fee required for 2 OFT transfers
                    // and 1 native transfer,so we are multiplying oft fee with 2.
                    // Since feeForCollateralTransfer includes the fee with native token needed, we are subtracting the native token
                    // needed to get the fee alone
                    uint128(2 * feeForTokenTransfer.nativeFee + (feeForCollateralTransfer.nativeFee - collateralTokenTransferData.ethToSend)),
                    bytes32(uint256(uint160(dstGlobalVariablesAddress)))
                );
            // If we need to get both the Collaterals and USDa from dst chain
        } else if (functionToDo == FunctionToDo.BOTH_TRANSFER) {
            _options = OptionsBuilder.newOptions().addExecutorLzReceiveOption(700000, 0).addExecutorNativeDropOption(
                    // Since, we have 3 collaterals, we are passing native fee required for 3 OFT transfers
                    // and 1 native transfer,so we are multiplying oft fee with 3.
                    // Since feeForCollateralTransfer includes the fee with native token needed, we are subtracting the native token
                    // needed to get the fee alone
                    uint128(5 * feeForTokenTransfer.nativeFee + (feeForCollateralTransfer.nativeFee - collateralTokenTransferData.ethToSend)),
                    bytes32(uint256(uint160(dstGlobalVariablesAddress)))
                );
        }
        // Calculate the fee for _lzSend function
        // Since in this function we don't need to store data, we are passing zero params and DUMMY enums.
        _fee = quoteInternal(
            dstEid,
            functionToDo,
            oftTransferData.tokensToSend,
            oftTransferData.tokensToSend,
            oftTransferData.tokensToSend,
            CDSInterface.LiquidationInfo(0,0,0,0,IBorrowing.AssetName.DUMMY,0),
            0,
            oftTransferData,
            collateralTokenTransferData,
            IBorrowing.AssetName.DUMMY,
            _options,
            false
        );

        // Calling layer zero send function to send to dst chain
        receipt = _lzSend(
            dstEid,
            _payload,
            _options,
            _fee,
            payable(refundAddress)
        );
    }
```

ALso, for when options are renewed:
```solidity
    /**
     * @dev renew the position by 30 days by paying usda.
     * The user will have 80% downside protection
     * @param index index of the position to renew
     */
    function renewOptions(uint64 index) external payable {
        if (
            !BorrowLib.renewOptions(
                Interfaces(
                    treasury,
                    globalVariables,
                    usda,
                    abond,
                    cds,
                    options
                ),
                index
            )
        ) revert Borrow_RenewFailed();

        // Get the deposit details
        ITreasury.GetBorrowingResult memory getBorrowingResult = treasury.getBorrowing(msg.sender, index);
        ITreasury.DepositDetails memory depositDetail = getBorrowingResult.depositDetails;
        depositDetail.optionsRenewedTimeStamp = block.timestamp;
        treasury.updateDepositDetails(msg.sender, index, depositDetail);

        // define options for lz send transaction with 400000 gas(found by testing)
        bytes memory _options = OptionsBuilder.newOptions().addExecutorLzReceiveOption(400000, 0);

        // calculting fee for lz send transaction
        MessagingFee memory fee = globalVariables.quote(
            IGlobalVariables.FunctionToDo(1),
            depositDetail.assetName,
            _options,
            false
        );

        // Calling Omnichain send function
        globalVariables.send{value: fee.nativeFee}(
            IGlobalVariables.FunctionToDo(1),
            depositDetail.assetName,
            fee,
            _options,
            msg.sender
        );

        emit Renewed(msg.sender, index, block.timestamp);
    }
```

... a cross-chain call is sent to notify the `GloablVariables` counterparty.

---
The `_lzReceive` function is the following:

```solidity
    /**
     * @dev function to receive data from src
     */
    function _lzReceive( // @audit-info: this can be exploited as it never validates the origin params
        Origin calldata /*_origin*/,
        bytes32 /*_guid*/,
        bytes calldata payload,
        address /*_executor*/,
        bytes calldata /*_extraData*/
    ) internal override {
        // Decoding the message from src
        OAppData memory oappData = abi.decode(payload, (OAppData));


        // Options for oft transfer
        bytes memory _options;
        // Native fee required
        MessagingFee memory _fee;
        //? based on functionToDo, do the lz transactions
        // If needs to transfer USDa only
        if (oappData.functionToDo == FunctionToDo.TOKEN_TRANSFER) {
            // getting options since,the src don't know the dst state
            _options = OptionsBuilder.newOptions().addExecutorLzReceiveOption(60000,0);
            // Define SendParam to send OFT
            SendParam memory _sendParam = SendParam(
                dstEid,
                bytes32(uint256(uint160(oappData.oftTransferData.recipient))),
                oappData.oftTransferData.tokensToSend,
                oappData.oftTransferData.tokensToSend,
                _options,
                "0x",
                "0x"
            );
            // Get the fee by calling quote function
            _fee = usda.quoteSend(_sendParam, false);
            // Since we need usda only, we have passed others zero
            treasury.transferFundsToGlobal([0, 0, 0, oappData.oftTransferData.tokensToSend]); // @audit-info: what does it do here?
            // Send oft to src chain
            usda.send{value: _fee.nativeFee}(_sendParam, _fee, address(this));
            // If need only collateral or collateral and USDa
        } else if (oappData.functionToDo == FunctionToDo.COLLATERAL_TRANSFER || oappData.functionToDo == FunctionToDo.BOTH_TRANSFER) {
            //Since we need to send ETH with OFt, we can do this by sending single transaction
            _options = OptionsBuilder.newOptions().addExecutorLzReceiveOption(280000, 0).addExecutorNativeDropOption(
                    // Pass the ETH amount needed
                    uint128(oappData.collateralTokenTransferData.ethToSend),
                    bytes32(uint256(uint160(oappData.collateralTokenTransferData.recipient)))
                );
            // Define the SendParam to send OFT
            SendParam memory _sendParam = SendParam(
                dstEid,
                bytes32(uint256(uint160(oappData.oftTransferData.recipient))),
                oappData.oftTransferData.tokensToSend,
                oappData.oftTransferData.tokensToSend,
                _options,
                "0x",
                "0x"
            );
            // Quote the native fee
            _fee = usda.quoteSend(_sendParam, false);
            // Get the funds from treasury, since the sender is global variables contract
            treasury.transferFundsToGlobal([
                    oappData.collateralTokenTransferData.ethToSend,
                    oappData.collateralTokenTransferData.weETHToSend,
                    oappData.collateralTokenTransferData.rsETHToSend,
                    oappData.oftTransferData.tokensToSend
                ]
            );
            // Send oft to src chain
            usda.send{value: _fee.nativeFee}(_sendParam, _fee, address(this));
            // Change the options since we dont need to send ETH
            _options = OptionsBuilder.newOptions().addExecutorLzReceiveOption(100000, 0);

            // Collateral tokens to send to src chain
            uint256[2] memory tokensToSend = [
                oappData.collateralTokenTransferData.weETHToSend,
                oappData.collateralTokenTransferData.rsETHToSend
            ];
            // Loop through the OFts to send tokens from this to src
            for (uint8 i = 0; i < tokensToSend.length; i++) {
                if (tokensToSend[i] > 0) {
                    _sendParam = SendParam(
                        dstEid,
                        bytes32(uint256(uint160(oappData.collateralTokenTransferData.recipient))),
                        tokensToSend[i],
                        tokensToSend[i],
                        _options,
                        "0x",
                        "0x"
                    );
                    _fee = usda.quoteSend(_sendParam, false);
                    address assetAddress;
                    if (i == 1) {
                        assetAddress = borrowInstance.assetAddress(IBorrowing.AssetName.rsETH);
                    } else {
                        assetAddress = borrowInstance.assetAddress(IBorrowing.AssetName(i + 2));
                    }
                    // Get the Collateal address from borrowing contract
                    IOFT(assetAddress).send{value: _fee.nativeFee}( _sendParam,_fee,address(this));
                }
            }
        }

        // Update the total cds deposited amount with options fees
        cds.updateTotalCdsDepositedAmountWithOptionFees(uint128(oappData.optionsFeesToRemove + oappData.cdsAmountToRemove + oappData.liqAmountToRemove));
        // Update the total cds deposited amount
        cds.updateTotalCdsDepositedAmount(uint128(oappData.cdsAmountToRemove + oappData.liqAmountToRemove));
        // Update the total available liquidation amount in CDS
        cds.updateTotalAvailableLiquidationAmount(oappData.liqAmountToRemove);
        // Update the liquidation info in CDS
        cds.updateLiquidationInfo(oappData.liqIndex, oappData.liquidationInfo);
        // Update the global omnichain data struct
        omniChainData = oappData.message;
        // Update the individual collateral data
        s_collateralData[oappData.assetName] = oappData.collateralData;
    }
 ```

- https://github.com/sherlock-audit/2024-11-autonomint/blob/main/Blockchain/Blockchian/contracts/Core_logic/GlobalVariables.sol

### Root Cause

The LayerZero system's integration suggests to sanitize the `origin` of the function that triggered the `_lzReceive` hook.

However, there's missing sanitization there:
```solidity
    /**
     * @dev function to receive data from src
     */
    function _lzReceive( // @audit-info: this can be exploited as it never validates the origin params
        Origin calldata /*_origin*/,
        bytes32 /*_guid*/,
        bytes calldata payload,
        address /*_executor*/,
        bytes calldata /*_extraData*/
    ) internal override {
        // Decoding the message from src
        OAppData memory oappData = abi.decode(payload, (OAppData));

```

You can see the NatSpec notation for the call from the LayerZero's docs:
```solidity
    /**
     * @dev Called when data is received from the protocol. It overrides the equivalent function in the parent contract.
     * Protocol messages are defined as packets, comprised of the following parameters.
     * @param _origin A struct containing information about where the packet came from.
     * @param _guid A global unique identifier for tracking the packet.
     * @param payload Encoded message.
     */
    function _lzReceive(
        Origin calldata _origin,
        bytes32 _guid,
        bytes calldata payload,
        address,  // Executor address as specified by the OApp.
        bytes calldata  // Any extra data or options to trigger on receipt.
    ) internal override {
        // Decode the payload to get the message
        // In this case, type is string, but depends on your encoding!
        data = abi.decode(payload, (string));
    }
}
```

### Internal pre-conditions

_No response_

### External pre-conditions

None.

### Attack Path

1. Any user can send a LayerZero message, pointing to the `GlobalVariables` contract on the **target** chain as the **destination**.
2. The constructed, encoded message payload will contain data to inflate the internal variables accounting globally, e.g. using the following template:
```solidity
    bytes memory _payload = abi.encode(_message); // Encodes message as bytes.
    _lzSend(
        _dstEid, // Destination chain's endpoint ID.
        _payload, // Encoded message payload being sent.
        _options, // Message execution options (e.g., gas to use on destination).
        MessagingFee(msg.value, 0), // Fee struct containing native gas and ZRO token.
        payable(msg.sender) // The refund address in case the send call reverts.
    );
```

**Please note that there's no validation in terms of acceptable `oappData.functionToDo` values, so for simplicity the attacker can set an arbitrary value that will *not* be covered by the following `if {} else if {}` blocks**:
```solidity
        if (oappData.functionToDo == FunctionToDo.TOKEN_TRANSFER) {
            // getting options since,the src don't know the dst state
            _options = OptionsBuilder.newOptions().addExecutorLzReceiveOption(60000,0);
            // Define SendParam to send OFT
            SendParam memory _sendParam = SendParam(
                dstEid,
                bytes32(uint256(uint160(oappData.oftTransferData.recipient))),
                oappData.oftTransferData.tokensToSend,
                oappData.oftTransferData.tokensToSend,
                _options,
                "0x",
                "0x"
            );
            // Get the fee by calling quote function
            _fee = usda.quoteSend(_sendParam, false);
            // Since we need usda only, we have passed others zero
            treasury.transferFundsToGlobal([0, 0, 0, oappData.oftTransferData.tokensToSend]); // @audit-info: what does it do here?
            // Send oft to src chain
            usda.send{value: _fee.nativeFee}(_sendParam, _fee, address(this));
            // If need only collateral or collateral and USDa
        } else if (oappData.functionToDo == FunctionToDo.COLLATERAL_TRANSFER || oappData.functionToDo == FunctionToDo.BOTH_TRANSFER) {
            //Since we need to send ETH with OFt, we can do this by sending single transaction
            _options = OptionsBuilder.newOptions().addExecutorLzReceiveOption(280000, 0).addExecutorNativeDropOption(
                    // Pass the ETH amount needed
                    uint128(oappData.collateralTokenTransferData.ethToSend),
                    bytes32(uint256(uint160(oappData.collateralTokenTransferData.recipient)))
                );
            // Define the SendParam to send OFT
            SendParam memory _sendParam = SendParam(
                dstEid,
                bytes32(uint256(uint160(oappData.oftTransferData.recipient))),
                oappData.oftTransferData.tokensToSend,
                oappData.oftTransferData.tokensToSend,
                _options,
                "0x",
                "0x"
            );
            // Quote the native fee
            _fee = usda.quoteSend(_sendParam, false);
            // Get the funds from treasury, since the sender is global variables contract
            treasury.transferFundsToGlobal([
                    oappData.collateralTokenTransferData.ethToSend,
                    oappData.collateralTokenTransferData.weETHToSend,
                    oappData.collateralTokenTransferData.rsETHToSend,
                    oappData.oftTransferData.tokensToSend
                ]
            );
            // Send oft to src chain
            usda.send{value: _fee.nativeFee}(_sendParam, _fee, address(this));
            // Change the options since we dont need to send ETH
            _options = OptionsBuilder.newOptions().addExecutorLzReceiveOption(100000, 0);

            // Collateral tokens to send to src chain
            uint256[2] memory tokensToSend = [
                oappData.collateralTokenTransferData.weETHToSend,
                oappData.collateralTokenTransferData.rsETHToSend
            ];
            // Loop through the OFts to send tokens from this to src
            for (uint8 i = 0; i < tokensToSend.length; i++) {
                if (tokensToSend[i] > 0) {
                    _sendParam = SendParam(
                        dstEid,
                        bytes32(uint256(uint160(oappData.collateralTokenTransferData.recipient))),
                        tokensToSend[i],
                        tokensToSend[i],
                        _options,
                        "0x",
                        "0x"
                    );
                    _fee = usda.quoteSend(_sendParam, false);
                    address assetAddress;
                    if (i == 1) {
                        assetAddress = borrowInstance.assetAddress(IBorrowing.AssetName.rsETH);
                    } else {
                        assetAddress = borrowInstance.assetAddress(IBorrowing.AssetName(i + 2));
                    }
                    // Get the Collateal address from borrowing contract
                    IOFT(assetAddress).send{value: _fee.nativeFee}( _sendParam,_fee,address(this));
                }
            }
        }
```

Hence, the constructed message's `payload` that is decoded to the `oappData` locally later can simply contain arbitrarily HIGH values for the following variables:
```solidity
        // Update the total cds deposited amount with options fees
        cds.updateTotalCdsDepositedAmountWithOptionFees(uint128(oappData.optionsFeesToRemove + oappData.cdsAmountToRemove + oappData.liqAmountToRemove));
        // Update the total cds deposited amount
        cds.updateTotalCdsDepositedAmount(uint128(oappData.cdsAmountToRemove + oappData.liqAmountToRemove));
        // Update the total available liquidation amount in CDS
        cds.updateTotalAvailableLiquidationAmount(oappData.liqAmountToRemove);
        // Update the liquidation info in CDS
        cds.updateLiquidationInfo(oappData.liqIndex, oappData.liquidationInfo);
        // Update the global omnichain data struct
        omniChainData = oappData.message;
        // Update the individual collateral data
        s_collateralData[oappData.assetName] = oappData.collateralData;
 ```

The attack allows to continuously and almost costlessly (despite paying the LZ fees, which are quite negligible) inflate the whole Autonomint's global accounting of the liquidation amounts and deposits, etc.

The attacker simply needs to encode high values of his choice as the `payload`, and send the message to the `GlobalVariables` contract as the `destination` of his LayerZero call.

### For instance, encoding malicious
- `oappData.cdsAmountToRemove`, 
- `oappData.liquidationInfo`, 
- `oappData.liqAmountToRemove`,
- `oappData.optionsFeesToRemove`.

This will lead to wrong accounting, potentially DoS due to underflows in many cases, and general insolvency of the Autonomint system, basically any contract and function that relies on these variables:

### Impact

```solidity
    /**
     * @dev update Total Available Liquidation Amount
     * @param amount Liquiation amount used for liquidation
     */
    function updateTotalAvailableLiquidationAmount(
        uint256 amount
    ) external onlyGlobalOrLiquidationContract {
        // If the totalAvailableLiquidationAmount is non zero
        if (totalAvailableLiquidationAmount != 0) {
            totalAvailableLiquidationAmount -= amount;
        }
    }

```

```solidity
    /**
     * @dev update the total cds deposited amount
     * @param _amount Liquiation amount used for liquidation
     */
    function updateTotalCdsDepositedAmount(
        uint128 _amount
    ) external onlyGlobalOrLiquidationContract {
        // If the totalCdsDepositedAmount is non zero
        if (totalCdsDepositedAmount != 0) {
            totalCdsDepositedAmount -= _amount;
        }
    }
```

Which will become inflated.

For instance, setting the `liqAmountToRemove` in the encoded sent malicious LayerZero message equal to the current `GlobalVariables`'s legitimately accounted `totalAvailableLiquidationAmount`, will decrement the accounted `totalAvailableLiquidationAmount` to `0` ultimately.
```solidity
cds.updateTotalAvailableLiquidationAmount(oappData.liqAmountToRemove);
```

Eventually, all liquidations will become blocked.

```solidity
    /**
     * @dev update Total Available Liquidation Amount
     * @param amount Liquiation amount used for liquidation
     */
    function updateTotalAvailableLiquidationAmount(
        uint256 amount
    ) external onlyGlobalOrLiquidationContract {
        // If the totalAvailableLiquidationAmount is non zero
        if (totalAvailableLiquidationAmount != 0) {
            totalAvailableLiquidationAmount -= amount;
        }
    }
```

will decrement `totalAvailableLiquidationAmount` to `0`, blocking liquidations:
```solidity
    uint256 public totalAvailableLiquidationAmount; // total deposited usda available for liquidation
```

The damage will be permanent.

```solidity
     */
    function liquidationType1(
        address user,
        uint64 index,
        uint64 currentEthPrice,
        uint256 lastCumulativeRate
    ) internal returns (CDSInterface.LiquidationInfo memory liquidationInfo) {
        // Get the borrower and deposit details
        ITreasury.GetBorrowingResult memory getBorrowingResult = treasury.getBorrowing(user, index);
        ITreasury.DepositDetails memory depositDetail = getBorrowingResult.depositDetails;
        // Check whether the position is already liquidated or not
        require(!depositDetail.liquidated, "Already Liquidated");

        // Get the exchange rate and ETH price
        (uint128 exchangeRate /*uint128 ethPrice*/, ) = borrowing.getUSDValue(borrowing.assetAddress(depositDetail.assetName));

        // Check whether the position is eligible for liquidation
        uint128 ratio = BorrowLib.calculateEthPriceRatio(depositDetail.ethPriceAtDeposit, currentEthPrice);
        require(ratio <= 8000,"You cannot liquidate, ratio is greater than 0.8");

        // Get global omnichain data
        IGlobalVariables.OmniChainData memory omniChainData = globalVariables.getOmniChainData();
        // Get the specific collateral global data
        IGlobalVariables.CollateralData memory collateralData = globalVariables.getOmniChainCollateralData(depositDetail.assetName);

        // Increment global number of liquidations
        ++omniChainData.noOfLiquidations;

        //Update the position to liquidated
        depositDetail.liquidated = true;

        // Calculate borrower's debt
        uint256 borrowerDebt = ((depositDetail.normalizedAmount * lastCumulativeRate) / BorrowLib.RATE_PRECISION);
        uint128 returnToTreasury = uint128(borrowerDebt);

        // 20% to abond usda pool
        uint128 returnToAbond = BorrowLib.calculateReturnToAbond(depositDetail.depositedAmountInETH, depositDetail.ethPriceAtDeposit, returnToTreasury);
        treasury.updateAbondUSDaPool(returnToAbond, true);
        // Calculate the CDS profits
        uint128 cdsProfits = (((depositDetail.depositedAmountInETH * depositDetail.ethPriceAtDeposit) / BorrowLib.USDA_PRECISION) / 100) - returnToTreasury - returnToAbond;
        // Liquidation amount in usda needed for liquidation
        uint128 liquidationAmountNeeded = returnToTreasury + returnToAbond;
        // Check whether the cds have enough usda for liquidation
        require(omniChainData.totalAvailableLiquidationAmount >= liquidationAmountNeeded, "Don't have enough USDa in CDS to liquidate");

```

### PoC

For instance, simply encoding the message with maliciously constructed `oappData`-type parameters of choice will work:
```solidity
    struct OAppData {
        FunctionToDo functionToDo;
        CallingFunction callingFunction;
        uint256 optionsFeesToRemove;
        uint256 cdsAmountToRemove;
        uint256 liqAmountToRemove;
        CDSInterface.LiquidationInfo liquidationInfo;
        uint128 liqIndex;
        USDaOftTransferData oftTransferData;
        CollateralTokenTransferData collateralTokenTransferData;
        OmniChainData message;
        IBorrowing.AssetName assetName;
        CollateralData collateralData;
    }
```

The message should be sent through the LayerZero directioning system, with the `destination` being the Autonomint's `GlobalVariables` contract.

The `functionToDo`'s value should be neither `TOKEN_TRANSFER`, nor `COLLATERAL_TRANSFER`, nor `BOTH_TRANSFER` to just avoid complexity --- simply setting malicious values to the variables that the contract accounts for will work.
```solidity
    enum FunctionToDo {
        DUMMY,
        UPDATE_GLOBAL,
        UPDATE_INDIVIDUAL,
        TOKEN_TRANSFER,
        COLLATERAL_TRANSFER,
        BOTH_TRANSFER
    }
```

Source: https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/Core_logic/GlobalVariables.sol#L531-L539

As you can see, this is the `OAppReceiver` base contract from which `GlobalVariables` indirectly inherits from:

https://github.com/LayerZero-Labs/devtools/blob/d4850c3c740e0de07352757b67ccff66931102ed/packages/oapp-evm/contracts/oapp/OAppReceiver.sol#L82-L110

```solidity
    /**
     * @dev Entry point for receiving messages or packets from the endpoint.
     * @param _origin The origin information containing the source endpoint and sender address.
     *  - srcEid: The source chain endpoint ID.
     *  - sender: The sender address on the src chain.
     *  - nonce: The nonce of the message.
     * @param _guid The unique identifier for the received LayerZero message.
     * @param _message The payload of the received message.
     * @param _executor The address of the executor for the received message.
     * @param _extraData Additional arbitrary data provided by the corresponding executor.
     *
     * @dev Entry point for receiving msg/packet from the LayerZero endpoint.
     */
    function lzReceive(
        Origin calldata _origin,
        bytes32 _guid,
        bytes calldata _message,
        address _executor,
        bytes calldata _extraData
    ) public payable virtual {
        // Ensures that only the endpoint can attempt to lzReceive() messages to this OApp.
        if (address(endpoint) != msg.sender) revert OnlyEndpoint(msg.sender);

        // Ensure that the sender matches the expected peer for the source endpoint.
        if (_getPeerOrRevert(_origin.srcEid) != _origin.sender) revert OnlyPeer(_origin.srcEid, _origin.sender);

        // Call the internal OApp implementation of lzReceive.
        _lzReceive(_origin, _guid, _message, _executor, _extraData);
    }

```
---

I'll provide a coded PoC on request, but the attack is obvious and conceptual.

### Mitigation

Sanitize and validate the LayerZero's received message `_origin`, just like the LayerZero's docs suggest as recommended.