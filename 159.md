Round Oily Tuna

High

# Wrong set of state variable in `ABONDToken`

### Summary

In `ABONDToken::transferFrom` before the transfer is executed there is a logic, which updates `userStates` of sender and reciever. The problem is that the state of sender is not updated correctly. 

```solidity
function transferFrom(
        address from,
        address to,
        uint256 value
    ) public override returns (bool) {
        // check the input params are non zero
        require(from != address(0) && to != address(0), "Invalid User");

        // get the sender and receiver state
        State memory fromState = userStates[from];
        State memory toState = userStates[to];

        // update receiver state
        toState = Colors._credit(fromState, toState, uint128(value));
        userStates[to] = toState;

        // update sender state
        fromState = Colors._debit(fromState, uint128(value));
        userStates[msg.sender] = fromState; // audit here should be from

        // transfer abond
        super.transferFrom(from, to, value);
        return true;
    }
```

### Root Cause

The root cause of this issue lies in the incorrect use of msg.sender instead of from when updating the sender's state after debiting the transfer amount.

https://github.com/sherlock-audit/2024-11-autonomint/blob/main/Blockchain/Blockchian/contracts/Token/Abond_Token.sol#L159-L165

### Internal pre-conditions

A valid sender (from) with a sufficient balance to cover the transfer.
A valid receiver (to) address.

### External pre-conditions

The transferFrom function is called by an approved spender or the token owner.
The value parameter does not exceed the from address's approved allowance or balance.

### Attack Path

_No response_

### Impact

The current logic is not updating the ABOND state of the sender of tokens, but updates the `msg.sender` state, leading to wrong state of the contract.

### PoC

_No response_

### Mitigation

Make the following change:
```diff
function transferFrom(
        address from,
        address to,
        uint256 value
    ) public override returns (bool) {
        // check the input params are non zero
        require(from != address(0) && to != address(0), "Invalid User");

        // get the sender and receiver state
        State memory fromState = userStates[from];
        State memory toState = userStates[to];

        // update receiver state
        toState = Colors._credit(fromState, toState, uint128(value));
        userStates[to] = toState;

        // update sender state
        fromState = Colors._debit(fromState, uint128(value));
-       userStates[msg.sender] = fromState; // audit here should be from
+       userStates[from] = fromState;

        // transfer abond
        super.transferFrom(from, to, value);
        return true;
    }
```