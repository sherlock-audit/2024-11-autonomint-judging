Shallow Coral Kookaburra

Medium

# `CDS::withdraw` will fail in some scenarios because `CDSLib:getOptionsFeesProportions` logic is incomplete

### Summary

During withdrawal in `CDS`, when there is enough liquidity in the other chain to pay its proportion of option fees but not in the chain the withdraw is occurring, the protocol increases the amount it needs from the other chain, not checking if that increase in the amount is actually available there. This leads to malfunctioning of the protocol.

### Root Cause

In `CDS::withdraw`,  the following calculation is done in order to know how much `optionFees` will be paid in the two chains:
[CDS.sol#L333-L338](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/Core_logic/CDS.sol#L333-L338)
```solidity
// Calculate return amount includes eth Price difference gain or loss option fees
uint256 optionFees = (
      (cdsDepositDetails.normalizedAmount * omniChainData.lastCumulativeRate)
		/ (CDSLib.PRECISION * CDSLib.NORM_PRECISION)
		) - cdsDepositDetails.depositedAmount;
		
 // Calculate the options fees to get from other chains
 uint256 optionsFeesToGetFromOtherChain = getOptionsFeesProportions(optionFees);
```

`CDSLib::getOptionsFeesProportions` (`CDS:getOptionsFeesProportions` internally calls the library function) fails to consider the case in which there is enough liquidity in the other chain to pay the option fees but not in the chain the option fees payment is occurring (see "internal pre-conditions"):
[CDSLib.sol#L62-L103](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/lib/CDSLib.sol#L62-L103)
```solidity
    function getOptionsFeesProportions(
        uint256 optionsFees,
        uint256 _totalCdsDepositedAmount,
        uint256 _totalGlobalCdsDepositedAmount,
        uint256 _totalCdsDepositedAmountWithOptionFees,
        uint256 _totalGlobalCdsDepositedAmountWithOptionFees
    ) public pure returns (uint256) {
        // calculate other chain cds amount
        uint256 otherChainCDSAmount = _totalGlobalCdsDepositedAmount - _totalCdsDepositedAmount;
        // calculate option fees in otherchain
        uint256 totalOptionFeesInOtherChain = _totalGlobalCdsDepositedAmountWithOptionFees - _totalCdsDepositedAmountWithOptionFees - otherChainCDSAmount;
        // calculate options fees in this chain
        uint256 totalOptionFeesInThisChain = _totalCdsDepositedAmountWithOptionFees - _totalCdsDepositedAmount;
        // calculate share of both chains
        uint256 share = (otherChainCDSAmount * 1e10) / _totalGlobalCdsDepositedAmount;
        // options fees to get from other chain
        uint256 optionsfeesToGet = (optionsFees * share) / 1e10;
        // options fees to get from this chain
        uint256 optionsFeesRemaining = optionsFees - optionsfeesToGet;
        
        // if the options fees in other chain is zero
        if (totalOptionFeesInOtherChain == 0) {
            // options fees to get from otherchain is zero
            optionsfeesToGet = 0;
        } else {
            // if the options fees in other chain is insufficient
            // take the remaining from this chain
            if (totalOptionFeesInOtherChain < optionsfeesToGet) {
                optionsfeesToGet = totalOptionFeesInOtherChain;
            } else {
                if (
                    totalOptionFeesInOtherChain > optionsfeesToGet &&
                    totalOptionFeesInThisChain < optionsFeesRemaining
                ) {
	                // @audit HERE
                    optionsfeesToGet += optionsFeesRemaining - totalOptionFeesInThisChain;
                } else {
                    optionsfeesToGet = optionsfeesToGet;
                }
            }
        }
        return optionsfeesToGet;
    }
```

You can see where the `@audit` tag is, the logic is insufficient to consider such case as it does in one of the previous `if` statements:
```solidity
// if the options fees in other chain is insufficient
// take the remaining from this chain
if (totalOptionFeesInOtherChain < optionsfeesToGet) {
    optionsfeesToGet = totalOptionFeesInOtherChain;
} 
```

Finally, when `GlobalVariables::oftOrCollateralReceiveFromOtherChains` is called in `CDSLib::withdrawUser`, the `optionsFeesToGetFromOtherChain` amount to get of `USDa` from the other chain will be a bigger amount than the maximum expected in the other chain:
[CDSLib.sol#L778-L795](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/lib/CDSLib.sol#L778-L795)
```solidity
                    // Get the assets from other chain
                    interfaces.globalVariables.oftOrCollateralReceiveFromOtherChains{
                        value: msg.value - params.fee
                    }(
                        IGlobalVariables.FunctionToDo(
                            // Call getLzFunctionToDo in cds library to get, which action needs to do in dst chain
                            getLzFunctionToDo(params.optionsFeesToGetFromOtherChain, collateralToGetFromOtherChain)
                        ),
                        IGlobalVariables.USDaOftTransferData(address(interfaces.treasury), params.optionsFeesToGetFromOtherChain),
                        IGlobalVariables.CollateralTokenTransferData(
                            address(interfaces.treasury),
                            ethAmountFromOtherChain,
                            weETHAmountFromOtherChain,
                            rsETHAmountFromOtherChain
                        ),
                        IGlobalVariables.CallingFunction.CDS_WITHDRAW,
                        msg.sender
                    );
```

In the other chain, in the `GlobalVariables::_lzReceive` function, the amount received to deduct from `CDS::totalCdsDepositedAmountWithOptionFees` is in the `OAppData` struct as `optionsFeesToRemove`. 

`optionsFeesToRemove` will be deducted from `totalCdsDepositedAmountWithOptionFees` even though `optionsFeesToRemove` is bigger than the actual option fees accrued in that chain:
[GlobalVariables.sol#L629-L630](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/Core_logic/GlobalVariables.sol#L629-L630)
```solidity
// Update the total cds deposited amount with options fees
cds.updateTotalCdsDepositedAmountWithOptionFees(uint128(oappData.optionsFeesToRemove + oappData.cdsAmountToRemove + oappData.liqAmountToRemove));
```

This scenario is possible because in the `CDSLib::getOptionsFeesProportions`, the share of each chain to do the payment, is calculated based on deposits proportions, which is not related or proportional to the amount of option fees earned in the different chains.

### Internal pre-conditions

A withdrawal has to occur when:
- There aren't enough funds to cover its share of option fees in the chain the withdrawal is occurring.
- The other chain has enough to cover its share of funds used for option fees payment, but not enough to cover the lack of funds in the chain the withdrawal is occurring.

### External pre-conditions

None

### Attack Path

None

### Impact

Withdrawal will occur but in the other chain, when the information is received by Layer Zero endpoint to transfer funds, the accounting will be broken because more funds than intended will be transferred. Funds that were not available to be used as option fees earnings, will be used for this payment; the limit to be used is `totalCdsDepositedAmountWithOptionFees - totalCdsDepositedAmount`. 

### PoC

None

### Mitigation

Add a check so you return `totalOptionFeesInOtherChain` as `optionsFeesToGet` in `CDSLib::getOptionsFeesProportions` instead of a bigger amount:
[CDSLib.sol#L91-L100](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/lib/CDSLib.sol#L91-L100)
```diff
        } else {
            if (
                totalOptionFeesInOtherChain > optionsfeesToGet &&
                totalOptionFeesInThisChain < optionsFeesRemaining
            ) {
                optionsfeesToGet += optionsFeesRemaining - totalOptionFeesInThisChain;
+               if(optionsFeesToGet > totalOptionFeesInOtherChain){
+                   optionsFeesToGet = totalOptionFeesInOtherChain;
+               }
            } else {
                optionsfeesToGet = optionsfeesToGet;
            }
        }
```