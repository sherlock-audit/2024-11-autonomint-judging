Powerful Honeysuckle Anteater

High

# LiquidationType2 does not mark the deposit as liquidated, which enables borrower to withdraw it after liquidation

### Summary
LiquidationType2 does not mark the liquidated borrower position as liquidated, which enables the borrower to still withdraw it.

### Root Cause
We don't update the status of the position to being liquidated, when using LiquidationType2 in `borrowLiquidation.sol`, reference:
```solidity
    function liquidationType2(address user, uint64 index, uint64 currentEthPrice) internal {
        // Get the borrower and deposit details
        ITreasury.GetBorrowingResult memory getBorrowingResult = treasury.getBorrowing(user, index);
        ITreasury.DepositDetails memory depositDetail = getBorrowingResult.depositDetails;
        require(!depositDetail.liquidated, "Already Liquidated");

        // Check whether the position is eligible for liquidation
        uint128 ratio = BorrowLib.calculateEthPriceRatio(depositDetail.ethPriceAtDeposit, currentEthPrice);
        require(ratio <= 8000, "You cannot liquidate, ratio is greater than 0.8");

        uint256 amount = BorrowLib.calculateHalfValue(depositDetail.depositedAmountInETH);

        // Convert the ETH into WETH
        weth.deposit{value: amount}();
        // Approve it, to mint sETH
        bool approved = weth.approve(address(wrapper), amount);

        if (!approved) revert BorrowLiq_ApproveFailed();

        // Mint sETH
        wrapper.mint(amount);

        // Exchange sETH with sUSD
        synthetix.exchange(
            0x7345544800000000000000000000000000000000000000000000000000000000,
            amount,
            0x7355534400000000000000000000000000000000000000000000000000000000
        );

        // Calculate the margin
        int256 margin = int256((amount * currentEthPrice) / 100);
        // Transfer the margin to synthetix
        synthetixPerpsV2.transferMargin(margin);

        // Submit an offchain delayed order in synthetix for short position with 1X leverage
        synthetixPerpsV2.submitOffchainDelayedOrder(
            -int256((uint256(margin * 1 ether * 1e16) / currentEthPrice)), currentEthPrice * 1e16
        );
    }
```

To the contrary, its updated in liquidationType1, here https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/Core_logic/borrowLiquidation.sol#L202
```solidity
        //Update the position to liquidated
        depositDetail.liquidated = true;
```

### Internal pre-conditions
Position is liquidated with the LiquidationType2 

### External pre-conditions

_No response_

### Attack Path
1. Position is liquidated with the LiquidationType2 
2. Price of ETH goes up again, so LTV is met and withdraw checks pass.
3. Borrower could withdraw its position, leaving the protocol insolvent.

### Impact
Borrower can withdraw its position after it being liquidated.

### Mitigation
Appropriately handle changes in the liquidation status, when a position is liquidated.