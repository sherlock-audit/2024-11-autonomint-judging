Atomic Lipstick Pangolin

Medium

# A Malicious user can grief other users by frontrunning withdrawals using signed  `odosAssembledData`

### Summary

The `odosAssembledData` used for withdrawal in `borrowing.sol::withdraw` along side the provided `signature` can be reused.

### Root Cause

> Are there any off-chain mechanisms involved in the protocol (e.g., keeper bots, arbitrage bots, etc.)? We assume these mechanisms will not misbehave, delay, or go offline unless otherwise specified.

> In borrowing.sol contract
we are getting eth volatility as one of the param in depositToken function.
In withdraw function we are getting odosAssembledData in the form of bytes.

For every withdrawal attempt, a user is supposed to interact with Autonomint backend to get their assembledData from ODOS api.
For more information on how ODOS swap work check [here](https://docs.odos.xyz/build/quickstart/sor). 

The protocol directly interact with ODOS api, and provide users an `odosAssembledData` and a `signature` to verify that the `odosAssembledData` is from their backend. 
This `odosAssembledData` is dynamic and may change over time. The issue here is that the current logic for verifying the `odosAssembledData` is flawed, and one signed data can be reused, either by the same user or other users. Let me explain further using the code snippets below.
https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/Core_logic/borrowing.sol#L281

```solidity 
    function withDraw(
        address toAddress,
        uint64 index,
@>>     bytes memory odosAssembledData,
@>>     bytes memory signature
    ) external payable nonReentrant whenNotPaused(IMultiSign.Functions(1)) {
        // check is _toAddress in not a zero address and isContract address
        if (toAddress == address(0) || isContract(toAddress)) revert Borrow_CantBeContractOrZeroAddress(toAddress);

@>>     if (!cds.verify(odosAssembledData, signature)) revert Borrow_NotSignedByEIP712Signer();
        // Get the depositor details
        ITreasury.GetBorrowingResult memory getBorrowingResult = treasury.getBorrowing(msg.sender, index);
        // Get the exchange rate for a collateral and eth price
        (uint128 exchangeRate, uint128 ethPrice) = getUSDValue(assetAddress[getBorrowingResult.depositDetails.assetName]);

        // check if borrowerIndex in BorrowerDetails of the msg.sender is greater than or equal to Index
        if (getBorrowingResult.totalIndex >= index) {
@>>         _withdraw(
                toAddress,
                index,
@>>             odosAssembledData,
                uint64(ethPrice),
                exchangeRate,
                uint64(block.timestamp)
            );
//Skip implementation
```
Here is `cds.sol::verify`
The issue with this code as highlighted is that the verify function is only checking if the signer was the authorized signer `Admin2`. Every valid pair of `odosExecutionData` and `signature` will always be valid.
```solidity
    function verify(
        bytes memory odosExecutionData,
        bytes memory signature
    ) external view onlyBorrowingContract returns (bool) {
        return
            _verify(
                FunctionName.BORROW_WITHDRAW,
                0,
                0,
                odosExecutionData,
                signature
            );
    }
    function _verify(
        FunctionName functionName,
        uint256 excessProfitCumulativeValue,
        uint256 nonce,
        bytes memory odosExecutionData,
        bytes memory signature
    ) private view returns (bool) {
        bytes32 digest;
        //skip implementation....
        } else if (functionName == FunctionName.BORROW_WITHDRAW) {
            digest = _hashTypedDataV4(
                keccak256(
                    abi.encode(
                        keccak256("OdosPermit(bytes odosExecutionData)"),
                        odosExecutionData
                    )
                )
            );
        }

        address signer = ECDSA.recover(digest, signature);
        bytes32 hashedSigner = keccak256(abi.encodePacked(signer));
@>>     if (hashedSigner == hashedAdminTwo) {
            return true;
```

Here is a portion `borrowing.sol::_withdraw` i want us to focus on.
Within this function `treasury::swapCollateralForUSDT` is called, taking depositDetail.assetName, amountToSwap and odosAssembledData as arguments.
```solidity
    function _withdraw(
        address toAddress,
        uint64 index,
        bytes memory odosAssembledData,
        uint64 ethPrice,
        uint128 exchangeRate,
        uint64 withdrawTime
    ) internal {
        // Get the deposit details
        ITreasury.DepositDetails memory depositDetail = getDepositDetails(msg.sender, index);

        // call Caluculate ratio function to update tha changes in cds and eth vaults
        _calculateRatio(0, ethPrice);

        BorrowWithdraw_Result memory result = BorrowLib.withdraw(
            depositDetail,
            BorrowWithdraw_Params(
                toAddress,
                index,
                ethPrice,
                exchangeRate,
                withdrawTime,
                lastCumulativeRate,
                totalNormalizedAmount,
                bondRatio,
                collateralRemainingInWithdraw,
                collateralValueRemainingInWithdraw
            ),
            Interfaces(treasury, globalVariables, usda, abond, cds, options)
        );
        // calculated upside collateral to swap
        uint256 amountToSwap = result.collateralRemainingInWithdraw -
            collateralRemainingInWithdraw;
        if (amountToSwap > 0) {
            // found the exact amount of collateral
            amountToSwap = (amountToSwap * 1 ether) / exchangeRate;
            // call the swapCollateralForUSDT in treasury
@>>         treasury.swapCollateralForUSDT(depositDetail.assetName, amountToSwap, odosAssembledData);
        }
        //Skip Implementation....
```
`Treasury::swapCollateralForUSDT` 
Here, noitce that the `swapAmount` will be approved to  
```solidity
    function swapCollateralForUSDT(
        IBorrowing.AssetName asset,
        uint256 swapAmount,
        bytes memory odosAssembledData
    ) external onlyCoreContracts returns (uint256) {
        //? Differs from actual value used in backend, so changing the final digit to 0,
        //? in both backend and here.
        swapAmount = (swapAmount / 10) * 10;
        // check the asset is eligible asset
        if (
            asset != IBorrowing.AssetName.ETH &&
            asset != IBorrowing.AssetName.WeETH &&
            asset != IBorrowing.AssetName.WrsETH
        ) revert Treasury_InvalidAsset(asset);
        // if the asset is other than native asset, approve it.
        if (
            asset == IBorrowing.AssetName.WeETH ||
            asset == IBorrowing.AssetName.WrsETH
        ) {
@>>         bool approved = IERC20(borrow.assetAddress(asset)).approve(odosRouterV2, swapAmount);
            // check whether the approve is successfull or not
            if (!approved) revert Treasury_ApproveFailed();
        }

        // paas the odos assembled data as msg.data to routerV2 contract of odos
        // if the asset is native, pass as msg.value
@>>     (bool success, bytes memory result) = odosRouterV2.call{value: asset == IBorrowing.AssetName.ETH ? swapAmount : 0}(odosAssembledData);

        // check the swap is successfull or not.
        if (!success) revert Treasury_SwapFailed();

        // return the usdt amount
        return abi.decode(result, (uint256));
    }
```

From the above informaton, it is evident that one signed data can be reused, hence allowing unauthorized transactions.
Also from this, we can deduce that frontrunning is possible.

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

_No response_

### Impact

1. Based on a private conversation with a staff of the odos team, i have understood that the `odosAssembleData` is tailored internally for a one time use. An attacker can always frontrun withdrawals, griefing users who want to make withdrawals
2. If the `odosAssembled` data is reusable without causing a revert, then users can carry out unauthorized transactions 

### PoC

_No response_

### Mitigation

Fix the verification logic to ensure that the `odosAssembledData` is not reusable