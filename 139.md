Boxy Cider Carp

High

# Almost infinitely doubling the aBondBalance by calling `ABondToken.transferFrom`, where `from` is the attacker's 1st account

### Summary

Due to a conceptual mistake of storing the ABond accounting state that was "debited" from the `from`'s balance as the new state of `msg.sender` (who is likely **distinct from** the `from` address), a malicious user can use 2 controlled accounts to double the `aBondToken` state, by populating his 2nd (`msg.sender`) account with roughly the balance of his 1st (`from`) account.

- https://github.com/sherlock-audit/2024-11-autonomint/blob/main/Blockchain/Blockchian/contracts/Token/Abond_Token.sol#L147-L170

### Root Cause

The root problem stems from here:
```solidity
    function transferFrom(
        address from,
        address to,
        uint256 value
    ) public override returns (bool) {
        // check the input params are non zero
        require(from != address(0) && to != address(0), "Invalid User");

        // get the sender and receiver state
        State memory fromState = userStates[from];
        State memory toState = userStates[to];

        // update receiver state
        toState = Colors._credit(fromState, toState, uint128(value));
        userStates[to] = toState;

        // update sender state
        fromState = Colors._debit(fromState, uint128(value));
        userStates[msg.sender] = fromState;

        // transfer abond
        super.transferFrom(from, to, value);
        return true;
    }
```

Where, as you can see, the method is responsible for approval-based transferring, and the `msg.sender` is not expected to incur any loss or profit, but is just entrusted to use `from`'s ERC20 and ABond state funds.

However, `userStates[msg.sender]` is updated with the `from`'s debited state.

The corresponding methods in the library are the following:
```solidity
// file: Colors.sol

    function _credit(
        State memory _fromState,
        State memory _toState,
        uint128 _amount
    ) internal pure returns(State memory){

        // find the average cumulatie rate
        _toState.cumulativeRate = _calculateCumulativeRate(_amount, _toState.aBondBalance, _fromState.cumulativeRate, _toState.cumulativeRate);
        // find the eth backed
        _toState.ethBacked = _calculateEthBacked(_amount, _toState.aBondBalance, _fromState.ethBacked, _toState.ethBacked);
        // increment abond balance
        _toState.aBondBalance += _amount;

        return _toState;
    }

    /**
     * @dev update the user abond data after debit
     * @param _fromState struct which contains, state of user abond before debit
     * @param _amount abond amount
     */
    function _debit(
        State memory _fromState,
        uint128 _amount
    ) internal pure returns(State memory) {

        uint128 balance = _fromState.aBondBalance;
        
        // check user has sufficient balance
        require(balance >= _amount,"InsufficientBalance");
 
        // update user abond balance
        _fromState.aBondBalance = balance - _amount;

        // after debit, if abond balance is zero, update cr and eth backed as 0
        if(_fromState.aBondBalance == 0){
            _fromState.cumulativeRate = 0;
            _fromState.ethBacked = 0;
        }
        return _fromState;
    }
```

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path
Assume a user Beatrice has 100e18 Abond tokens already.

Beatrice creates a 2nd EOA account or, for simplicity, assume she has an accomplice: Ninelle.

Beatrice approves Ninelle to use all her Abond tokens freely.

Ninelle calls `transferFrom(Beatrice, Beatrice, 100e18)`, and while the ERC20 tokens `balanceOf` Beatrice remains the same as before, Beatrice's `toState` gets additional `100e18` Abond tokens "credited" "in-state" (`aBondBalance`) (and correspondingly `ethBacked` and `cumulativeRate` also get updated with the **double-counted balance**):
```solidity
        // update sender state
        fromState = Colors._debit(fromState, uint128(value));
        userStates[msg.sender] = fromState;
```

Ninelle's account just gets a cleared `0`-values balance stored, whereas Beatrice's account's `ethBacked` and `cumulativeRate` effectively get double-counted as if she had `200e18` ABond tokens (at least when it comes to what the `userStates` mapping structure holds).


Because of the following code being executed with the Beatrice's FROM state, but stored for the accomplice's state (for Ninelle, which **has basically nothing to lose, as she doesn't hold any funds at all actually):
```solidity

    /**
     * @dev update the user abond data after debit
     * @param _fromState struct which contains, state of user abond before debit
     * @param _amount abond amount
     */
    function _debit(
        State memory _fromState,
        uint128 _amount
    ) internal pure returns(State memory) {

        uint128 balance = _fromState.aBondBalance;
        
        // check user has sufficient balance
        require(balance >= _amount,"InsufficientBalance");
 
        // update user abond balance
        _fromState.aBondBalance = balance - _amount;

        // after debit, if abond balance is zero, update cr and eth backed as 0
        if(_fromState.aBondBalance == 0){
            _fromState.cumulativeRate = 0;
            _fromState.ethBacked = 0;
        }
        return _fromState;
    }
```

This is executed for Beatrice's account though:
```solidity
    /**
     * @dev update the user abond data after credit
     * @param _fromState struct which contains, state of user abond before credit
     * @param _toState struct which contains, state of user abond after credit
     * @param _amount abond amount
     */
    function _credit(
        State memory _fromState,
        State memory _toState,
        uint128 _amount
    ) internal pure returns(State memory){

        // find the average cumulatie rate
        _toState.cumulativeRate = _calculateCumulativeRate(_amount, _toState.aBondBalance, _fromState.cumulativeRate, _toState.cumulativeRate);
        // find the eth backed
        _toState.ethBacked = _calculateEthBacked(_amount, _toState.aBondBalance, _fromState.ethBacked, _toState.ethBacked);
        // increment abond balance
        _toState.aBondBalance += _amount;

        return _toState;
    }
```



### Impact

Inflation of the `ethBacked` accounted values: this attack can be repeated infinitely, using a new `msg.sender` ("approvee") for every attack. This can be repeated multiple times to get a much larger `ethBacked` and `cumulativeRate` value that intended with no additional ABond token investments.


What is the incentive for this costless attack?


This will specifically work to get a much larger portion of ETH during redemption here:
```solidity
    /**
     * @dev redeem abond yields
     * @param user abond holder address
     * @param aBondAmount redeeming abond amount
     * @param abondAddress abond token address
     * @param treasuryAddress treasury address
     * @param usdaAddress usda token address
     */
    function redeemYields(
        address user,
        uint128 aBondAmount,
        address usdaAddress,
        address abondAddress,
        address treasuryAddress,
        address borrow
    ) public returns (uint256) {
        // check abond amount is non zewro
        if (aBondAmount == 0) revert IBorrowing.Borrow_NeedsMoreThanZero();
        IABONDToken abond = IABONDToken(abondAddress);
        // get user abond state
        State memory userState = abond.userStates(user);
        // check user have enough abond
        if (aBondAmount > userState.aBondBalance) revert IBorrowing.Borrow_InsufficientBalance();

        ITreasury treasury = ITreasury(treasuryAddress);
        // calculate abond usda ratio
        uint128 usdaToAbondRatio = uint128((treasury.abondUSDaPool() * RATE_PRECISION) / abond.totalSupply());
        uint256 usdaToBurn = (usdaToAbondRatio * aBondAmount) / RATE_PRECISION;
        // update abondUsdaPool in treasury
        treasury.updateAbondUSDaPool(usdaToBurn, false);

        // calculate abond usda ratio from liquidation
        uint128 usdaToAbondRatioLiq = uint128((treasury.usdaGainedFromLiquidation() * RATE_PRECISION) /abond.totalSupply());
        uint256 usdaToTransfer = (usdaToAbondRatioLiq * aBondAmount) / RATE_PRECISION;
        //update usdaGainedFromLiquidation in treasury
        treasury.updateUSDaGainedFromLiquidation(usdaToTransfer, false);

        //Burn the usda from treasury
        treasury.approveTokens(
            IBorrowing.AssetName.USDa,
            borrow,
            (usdaToBurn + usdaToTransfer)
        );

        IUSDa usda = IUSDa(usdaAddress);
        // burn the usda
        bool burned = usda.contractBurnFrom(address(treasury), usdaToBurn);
        if (!burned) revert IBorrowing.Borrow_BurnFailed();

        if (usdaToTransfer > 0) {
            // transfer usda to user
            bool transferred = usda.contractTransferFrom(
                address(treasury),
                user,
                usdaToTransfer
            );
            if (!transferred) revert IBorrowing.Borrow_TransferFailed();
        }
        // withdraw eth from ext protocol
        uint256 withdrawAmount = treasury.withdrawFromExternalProtocol(user,aBondAmount); // @audit-info: check this out for the ethBacked Colors variables data storage slots manipulation attack related information

        //Burn the abond from user
        bool success = abond.burnFromUser(msg.sender, aBondAmount);
        if (!success) revert IBorrowing.Borrow_BurnFailed();

        return withdrawAmount;
    }
```

```solidity
    /**
     * @dev Withdraws ETH from external protocol
     * @param user ABOND holder address
     * @param aBondAmount ABOND amount, the user is redeeming
     */
    function withdrawFromExternalProtocol(
        address user,
        uint128 aBondAmount
    ) external onlyCoreContracts returns (uint256) {
        if (user == address(0)) revert Treasury_ZeroAddress();

        // Withdraw from external protocols
        uint256 redeemAmount = withdrawFromIonicByUser(user, aBondAmount);
        // Send the ETH to user
        (bool sent, ) = payable(user).call{value: redeemAmount}("");
        // check the transfer is successfull or not
        require(sent, "Failed to send Ether");
        return redeemAmount;
    }
```

This is because `withdrawFromIonicByUser` references the `ethBacked` value for its calculation here:
```solidity
    /**
     * @dev Withdraw ETH from Ionic
     * @param user User address
     * @param aBondAmount ABOND amount to redeem
     */
    function withdrawFromIonicByUser(
        address user,
        uint128 aBondAmount
    ) internal nonReentrant returns (uint256) { // @audit-info: where is it called from?
        uint256 currentExchangeRate = ionic.exchangeRateCurrent();
        uint256 currentCumulativeRate = _calculateCumulativeRate(currentExchangeRate, Protocol.Ionic);

        State memory userState = abond.userStates(user);
        uint128 depositedAmount = (aBondAmount * userState.ethBacked) / PRECISION; // @audit-info: this value can be manipulated in a particular way
        uint256 normalizedAmount = (depositedAmount * CUMULATIVE_PRECISION) / userState.cumulativeRate;

        //withdraw amount
        uint256 amount = (currentCumulativeRate * normalizedAmount) / CUMULATIVE_PRECISION;
        // withdraw from ionic
        ionic.redeemUnderlying(amount);

        protocolDeposit[Protocol.Ionic].totalCreditedTokens = ionic.balanceOf(address(this));
        protocolDeposit[Protocol.Ionic].exchangeRate = currentExchangeRate;
        // convert weth to eth
        WETH.withdraw(amount);
        return amount;
    }
```

### PoC

Will provide on request.

### Mitigation
```diff
    /**
     * transfer abond from spender to, to address
     * @param from address of the abond owner
     * @param to address of the recepient
     * @param value abond amount to transfer
     */
    function transferFrom(
        address from,
        address to,
        uint256 value
    ) public override returns (bool) {
        // check the input params are non zero
        require(from != address(0) && to != address(0), "Invalid User");

        // get the sender and receiver state
        State memory fromState = userStates[from];
        State memory toState = userStates[to];

        // update receiver state
        toState = Colors._credit(fromState, toState, uint128(value));
        userStates[to] = toState;

        // update sender state
        fromState = Colors._debit(fromState, uint128(value));
-       userStates[msg.sender] = fromState;
+       userStates[from] = fromState; 

        // transfer abond
        super.transferFrom(from, to, value);
        return true;
    }
```