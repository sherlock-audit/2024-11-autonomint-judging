Sleepy Sepia Zebra

High

# The user can withdraw more collateral due to uncheck for `strikePrice`

## Summary

When users deposit collateral, they set `strikePrice` and `strikePercent`.  However, there is no check for `strikePrice` and as result, users will pay fewer option fees and withdraw more collateral.

## Root Cause

In the [borrowing.sol:226](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/Core_logic/borrowing.sol#L226) `depositTokens()` function, user set params - `strikePrice` and `strikePercent`.

However, there is no check for `strikePrice` and this makes possiblity for users to pay fewer option fees and withdraw more collateral.

```solidity
    function depositTokens(
        BorrowDepositParams memory depositParam
    ) external payable nonReentrant whenNotPaused(IMultiSign.Functions(0)) {
        ...
    }
```

In the [Options.sol](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/Core_logic/Options.sol#L104C1-L150C6) `calculateOptionPrice` function, calculates the option fees based on `strikePercent`.

When `strikePercent` increases, option fees decreases. Therefore, user can set `strikePercent` as `StrikePrice.TWENTY_FIVE` and pays fewer option fees.

```solidity
    function calculateOptionPrice(
        uint128 _ethPrice,
        uint256 _ethVolatility,
        uint256 _amount,
        StrikePrice _strikePrice
    ) public view returns (uint256) {
        uint256 a = _ethVolatility;
        uint256 ethPrice = _ethPrice; /*getLatestPrice();*/
        // Get global omnichain data
        IGlobalVariables.OmniChainData memory omniChainData = globalVariables.getOmniChainData();
        // Calculate the current ETH vault value
        uint256 E = omniChainData.totalVolumeOfBorrowersAmountinUSD + (_amount * _ethPrice);
        // Check the ETH vault is not zero
        require(E != 0, "No borrowers in protocol");
        uint256 cdsVault;
        // If the there is no borrowers in borrowing, then the cdsVault value is deposited value itself
        if (omniChainData.noOfBorrowers == 0) {
            cdsVault = omniChainData.totalCdsDepositedAmount * USDA_PRECISION;
        } else {
            // Else, get the cds vault current value from omnichain global data
            cdsVault = omniChainData.cdsPoolValue * USDA_PRECISION;
        }

        // Check cds vault is non zero
        require(cdsVault != 0, "CDS Vault is zero");
        // Calculate b, cdsvault to eth vault ratio
        uint256 b = (cdsVault * 1e2 * OPTION_PRICE_PRECISION) / E;
        uint256 baseOptionPrice = ((sqrt(10 * a * ethPrice)) * PRECISION) / OPTION_PRICE_PRECISION + ((3 * PRECISION * OPTION_PRICE_PRECISION) / b); // 1e18 is used to handle division precision

        uint256 optionPrice;
        // Calculate option fees based on strike price chose by user
        if (_strikePrice == StrikePrice.FIVE) {
            // constant has extra 1e3 and volatility have 1e8
            optionPrice = baseOptionPrice + (400 * OPTION_PRICE_PRECISION * baseOptionPrice) / (3 * a);
        } else if (_strikePrice == StrikePrice.TEN) {
            optionPrice = baseOptionPrice + (100 * OPTION_PRICE_PRECISION * baseOptionPrice) / (3 * a);
        } else if (_strikePrice == StrikePrice.FIFTEEN) {
            optionPrice = baseOptionPrice + (50 * OPTION_PRICE_PRECISION * baseOptionPrice) / (3 * a);
        } else if (_strikePrice == StrikePrice.TWENTY) {
            optionPrice = baseOptionPrice + (10 * OPTION_PRICE_PRECISION * baseOptionPrice) / (3 * a);
        } else if (_strikePrice == StrikePrice.TWENTY_FIVE) {
            optionPrice = baseOptionPrice + (5 * OPTION_PRICE_PRECISION * baseOptionPrice) / (3 * a);
        } else {
            revert("Incorrect Strike Price");
        }
        return ((optionPrice * _amount) / PRECISION) / USDA_PRECISION;
    }

```

In the [BorrowLib.sol:482](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/lib/BorrowLib.sol#L482) `calculateCollateralToReturn()` function, gets `collateralToReturn` from the [Option.sol:77](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/Core_logic/Options.sol#L77) `calculateStrikePriceGains()` function.

The `BorrowLib.calculateCollateralToReturn()` function calculates eth amount for `params.depositDetail.depositedAmountInETH` based on current eth price and eth amount of gains for strike price, when current ethPrice is more than `params.depositDetail.ethPriceAtDeposit`. Then send collateral for this amount of eth to user and remaining collateral is swaped to usdt(when collateral is not ETH).

User can set `strikePrice` as equal to ethPrice at deposit. Then he will withdraw full collateral(if collateral is ETH, will withdraw half amount of collateral), even though the price of eth increases than ethPriceDeposit.

As result, protocol won't get profits although the eth price is increasing and users will withdraw full amount of collateral.

```solidity
    function calculateCollateralToReturn(
        IBorrowing.CalculateCollateralToReturn_Param memory params
    ) public view returns (IBorrowing.CalculateCollateralToReturn_Result memory) {
        uint128 collateralToReturn;
        //Calculate current depositedAmount value
        uint128 depositedAmountvalue = (params.depositDetail.depositedAmountInETH * params.depositDetail.ethPriceAtDeposit) / params.ethPrice;
        uint128 collateralRemainingInWithdraw;
        // If the health is greater than 1
        if (params.borrowingHealth > 10000) {
            // If the ethPrice is higher than deposit ethPrice,call withdrawOption in options contract
482         collateralToReturn = (depositedAmountvalue + params.options.calculateStrikePriceGains(
                        params.depositDetail.depositedAmountInETH,
                        params.depositDetail.strikePrice,
                        params.ethPrice
                    )
                );
            ...
    
    }
```


```solidity
    function calculateStrikePriceGains(
        uint128 depositedAmount,
        uint128 strikePrice,
        uint64 ethPrice
    ) external view onlyBorrowingContract returns (uint128) {
        // Check the input params are non zero
        require(depositedAmount != 0 && strikePrice != 0 && ethPrice != 0,"Zero inputs in options");
        uint64 currentEthPrice = ethPrice;
        // Calculate current deposited ETH value in USD
        uint128 currentEthValue = depositedAmount * currentEthPrice;
        uint128 ethToReturn;
        // If the current value is greater, then user will get eth
        if (currentEthValue > strikePrice) {
            ethToReturn = (currentEthValue - strikePrice) / currentEthPrice;
        } else {
            ethToReturn = 0;
        }
        return ethToReturn;
    }
```


## Internal pre-conditions

## External pre-conditions

## Attack Path

## Impact

1. User pays fewer option fees.
2. Protocol won't get profit when eth price is increasing.

## Mitigation

Implement the check for `strikePrice` while depositing.