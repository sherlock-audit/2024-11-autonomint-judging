Vast Plum Chipmunk

High

# Incorrect application of `lastCumulativeRate` will increase borrower's debt or decrease borrower's repay amount unexpectedly.

### Summary

Applying `lastCumulativeRate` before updating it will cause an incorrect calculation of borrower's `normalizedAmount`, leading to an unexpected increase in borrower's debt or a decrease in borrower's repay amount.

### Root Cause

- In [`borrowing.sol#depositTokens()`](https://github.com/sherlock-audit/2024-11-autonomint/blob/main/Blockchain/Blockchian/contracts/Core_logic/borrowing.sol#L226-L268) function, `lastCumulativeRate` is applied before updating it.

```solidity
    function depositTokens(
        BorrowDepositParams memory depositParam
    ) external payable nonReentrant whenNotPaused(IMultiSign.Functions(0)) {
        // Assign options for lz contract, here the gas is hardcoded as 400000, we got this through testing by iteration
        bytes memory _options = OptionsBuilder.newOptions().addExecutorLzReceiveOption(400000, 0);
        // calculting fee
        MessagingFee memory fee = globalVariables.quote(
            IGlobalVariables.FunctionToDo(1),
            depositParam.assetName,
            _options,
            false
        );
        // Get the exchange rate for a collateral and eth price
        (uint128 exchangeRate, uint128 ethPrice) = getUSDValue(assetAddress[depositParam.assetName]);

        totalNormalizedAmount = BorrowLib.deposit(
            BorrowLibDeposit_Params(
                LTV,
                APR,
--->            lastCumulativeRate,
                totalNormalizedAmount,
                exchangeRate,
                ethPrice,
                lastEthprice
            ),
            depositParam,
            Interfaces(treasury, globalVariables, usda, abond, cds, options),
            assetAddress
        );

        //Call calculateCumulativeRate() to get currentCumulativeRate
--->    calculateCumulativeRate();
        lastEventTime = uint128(block.timestamp);

        // Calling Omnichain send function
        globalVariables.send{value: fee.nativeFee}(
            IGlobalVariables.FunctionToDo(1),
            depositParam.assetName,
            fee,
            _options,
            msg.sender
        );
    }
```

- In `BorrowLib.sol:L750`, `lastCumulativeRate` is applied to calculate `normalizedAmount`.

```solidity
        // Calculate normalizedAmount
        uint256 normalizedAmount = calculateNormAmount(tokensToMint, libParams.lastCumulativeRate);
```

### Internal pre-conditions

1. `lastCumulativeRate` needs to be applied before it is updated.
2. `calculateCumulativeRate()` needs to be called after `lastCumulativeRate` is applied.

### External pre-conditions

1. The borrower needs to deposit tokens.
2. The `currentCumulativeRate` needs to be different from `lastCumulativeRate`.

### Attack Path

1. The borrower deposits tokens.
2. `lastCumulativeRate` is applied before it is updated.
3. `calculateCumulativeRate()` is called after the deposit.

### Impact

The protocol suffers a loss as the borrower gets more `normalizedAmount` than normal, increasing their debt or repaying less than normal.

### PoC

1. In case of depositing in `borrowing.sol`.

- Assume `lastCumulativeRate` is `1.5e27`. After some time, `currentCumulativeRate` becomes `1.8e27` but `lastCumulativeRate` is not updated yet.
- A user deposits some `eth`, so `tokensToMint` which he borrows is `1000e6`. Then his `normalizedAmount` becomes `1000e6 * 1e27 / 1.5e27 = 666.67e6`.
- After the deposit, `lastCumulativeRate` is updated instantly to `1.8e27`. This means borrower's debt becomes `666.67e6 * 1.8e27 / 1e27 = 1200e6`.
- This is borrower's instant loss of 200e6 units.

2. In case of withdrawing in `borrowing.sol`.
   Similarly, the user repays less amount than normal due to the same rate discrepancy.

### Mitigation

`borrowing.sol#depositTokens()` function has to be modified as follows.

```solidity
    function depositTokens(
        BorrowDepositParams memory depositParam
    ) external payable nonReentrant whenNotPaused(IMultiSign.Functions(0)) {
        ...............

++      calculateCumulativeRate();
        totalNormalizedAmount = BorrowLib.deposit(
            BorrowLibDeposit_Params(
                LTV,
                APR,
                lastCumulativeRate,
                totalNormalizedAmount,
                exchangeRate,
                ethPrice,
                lastEthprice
            ),
            depositParam,
            Interfaces(treasury, globalVariables, usda, abond, cds, options),
            assetAddress
        );

        //Call calculateCumulativeRate() to get currentCumulativeRate
--      calculateCumulativeRate();
        lastEventTime = uint128(block.timestamp);

        .............
    }
```
