Shallow Coral Kookaburra

Medium

# `LiquidationType.TWO` incorrectly updates `liquidationInfo` as empty in `CDS` of the other chain in an index that was already used

### Summary

When liquidation type two is executed in a debt position of a user, `BorrowLiquidation::liquidateBorrowPosition` returns empty `LiquidationInfo` data to `Borrowing::liquidate`, which correctly updates the `noOfLiquidations` value (increasing it). However, that updated `noOfLiquidations` with the empty `LiquidationInfo` data is sent to the other chain, through `GlobalVariables::sendForLiquidation`, updating the `CDS` data by calling `CDS::updateLiquidationInfo`. The `noOfLiquidations` received value should be the `omniChainData.noOfLiquidations` instead of the `noOfLiquidations` in the chain where the liquidation happened.

### Root Cause

The `Borrowing` contracts has a `liquidate` function which is only called by the `admin`. That function is in charge of [updating the `noOfLiquidations` variable](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/Core_logic/borrowing.sol#L386-L387) and in charge of [calling `BorrowLiquidation:liquidateBorrowPosition`](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/Core_logic/borrowing.sol#L390-L397) with the liquidation data it will need to liquidate.

The first part of the issue is here: `BorrowLiquidation::liquidateBorrowPosition` returns an empty `LiquidationData` value when the liquidation type is two:
[borrowLiquidation.sol#L150-L165](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/Core_logic/borrowLiquidation.sol#L150-L165)
```solidity
    function liquidateBorrowPosition(
        address user,
        uint64 index,
        uint64 currentEthPrice,
        IBorrowing.LiquidationType liquidationType,
        uint256 lastCumulativeRate
    ) external payable onlyBorrowingContract returns (CDSInterface.LiquidationInfo memory liquidationInfo) {
        //? Based on liquidationType do the liquidation
        // Type 1: Liquidation through CDS
        if (liquidationType == IBorrowing.LiquidationType.ONE) {
            return liquidationType1(user, index, currentEthPrice, lastCumulativeRate);
            // Type 2: Liquidation by taking short position in synthetix with 1x leverage
        } else if (liquidationType == IBorrowing.LiquidationType.TWO) {
            liquidationType2(user, index, currentEthPrice); // HERE
        }
    }
```

The value returned by this function is received in `Borrowing::liquidate`:
[borrowing.sol#L386-L408](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/Core_logic/borrowing.sol#L386-L408) 
```solidity
		...
        // Increment number of liquidations
        ++noOfLiquidations;
        (, uint128 ethPrice) = getUSDValue(assetAddress[AssetName.ETH]);
        // Call the liquidateBorrowPosition function in borrowLiquidation contract
        CDSInterface.LiquidationInfo memory liquidationInfo = borrowLiquidation
            .liquidateBorrowPosition{value: msg.value - fee.nativeFee}(
            user,
            index,
            uint64(ethPrice),
            liquidationType,
            lastCumulativeRate
        );
        
        // Calling Omnichain send function
        globalVariables.sendForLiquidation{value: fee.nativeFee}(
            IGlobalVariables.FunctionToDo(2),
            noOfLiquidations,
            liquidationInfo,
            getBorrowingResult.depositDetails.assetName,
            fee,
            _options,
            msg.sender
        );
```

The empty `LiquidationInfo` with the updated `noOfLiquidations` is sent to the other chain through `GlobalVariables::sendForLiquidation`:
[GlobalVariables.sol#L503-L526](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/Core_logic/GlobalVariables.sol#L503-L526)
```solidity
    function sendForLiquidation(
        FunctionToDo functionToDo,
        uint128 liqIndex,
        CDSInterface.LiquidationInfo memory liquidationInfo,
        IBorrowing.AssetName assetName,
        MessagingFee memory fee,
        bytes memory options,
        address refundAddress
    ) external payable onlyCoreContracts returns (MessagingReceipt memory receipt) {
        // Call the sendInternal Function to send oapp msgs
        return sendInternal(
            dstEid,
            functionToDo,
            0,
            0,
            0,
            liquidationInfo,
            liqIndex,
            assetName,
            fee,
            options,
            refundAddress
        );
    }
```

Then, `GlobalVariables::sendInternal` uses the `_lzSend` inherited function from Layer Zero`OApp` contract, sending all the information received encoded so it can be received in the other chain.

The `GlobalVariables` contract in the other chain, receives that data and handles it in the `GlobalVariables::_lzReceive` function. Then, it updates all the received data in its chain:
[GlobalVariables.sol#L635-L636](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/Core_logic/GlobalVariables.sol#L635-L636)
```solidity
// Update the liquidation info in CDS
cds.updateLiquidationInfo(oappData.liqIndex, oappData.liquidationInfo);
```

The `liquidationInfo` received will overwrite other already stored information in the same `liqInfo`, because the `BorrowLiquidation::LiquidationType1` updates the liquidation info with `omniChainData.noOfLiquidations`:
[borrowLiquidation.sol#L240-L241](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/Core_logic/borrowLiquidation.sol#L240-L241)
```solidity
// Update the CDS data
cds.updateLiquidationInfo(omniChainData.noOfLiquidations, liquidationInfo);
```

The other part of the problem is that unlike `BorrowLiquidation::liquidationType1`, `BorrowLiquidation::liquidationType2` does not update the liquidation information in the `CDS` of its chain. This leads not only to overwritten information (explained above) but also to inconsistency of information between chains.

### Internal pre-conditions

- Liquidation type 2 is called

### External pre-conditions

None

### Attack Path

None

### Impact

`LiquidationInfo` in `CDS` across chains will be partially lost and inconsistent.
Part of liquidation earnings of `CDS` depositors who opted for liquidation will be lost.

### PoC

None

### Mitigation

Always use `omniChainData` to work with information that will be shared in other chains, and need to be consistent. However, with this issue that won't completely fix it. If there aren't intentions of saving `LiquidationInfo` from liquidation type two, don't save an empty `LiquidationInfo` in any index.