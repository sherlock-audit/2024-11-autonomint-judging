Shallow Coral Kookaburra

High

# Race condition when updating `GlobalVariables` data across chains

### Summary

The asynchronous nature of LayerZero and the way `GlobalVariables::_lzReceive` updates information create a classical race condition in distributed systems. Specifically, when `_lzSend` in `GlobalVariables` is called in Chain A, and before `GlobalVariables::_lzReceive` is processed in Chain B, a conflicting `_lzSend` is triggered on Chain B. This results in data being stored without proper synchronization, leading to potential corruption of the `omniChainData` data structure, and in case of the liquidation information, lost liquidation information when that happens with liquidation events.

### Root Cause

The issue arises due to:
- **Independent Execution**: Chain A and Chain B operate independently, each maintaining its own state without direct synchronization.

- **Asynchronous Updates**: The delay in processing `_lzReceive` on the destination chain means that both chains might independently decide to update the same information with outdated or incomplete data.

- **Shared Index Dependency in Liquidation Info**: Both chains rely on the same logical data structure and indexes, but without any mechanism to coordinate writes across chains, conflicts occur.

Every single function in the system that updates the state of the `omniChainData`, after the update, it sends the updated `omniChainData` structure to the other chain through Layer Zero so both chains share the crucial information that the system needs to operate. Information needs to be consistent.

To do so, in most of the functions (there are a couple of exceptions, but essentially they have the same issue) a call to `GlobalVariables::send` is done. Let's review the `CDS::deposit` flow which is a great example of this issue occurring:

1) Update of `omniChainData` information in the chain the deposit is occurring (chain A):
[CDSLib.sol#L594](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/lib/CDSLib.sol#L594)
```solidity
interfaces.globalVariables.setOmniChainData(omniChainData);
```
Before this update the data contained by `omniChainData` was modified (I don't include it in the report as I don't want to overwhelm the reader with information that won't explain the issue).

2)  Update of `omniChainData` in the other chain (chain B):
This is where the `GlobalVariables::send` function is called in `CDS::deposit`:
[CDS.sol#L251-L258](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/Core_logic/CDS.sol#L251-L258)
```solidity
// Calling Omnichain send function
globalVariables.send{value: fee.nativeFee}(
    IGlobalVariables.FunctionToDo(1),
    IBorrowing.AssetName(0),
    fee,
    _options,
    msg.sender
);
```

[GlobalVariables.sol#L470-L491](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/Core_logic/GlobalVariables.sol#L470-L491)
```solidity
function send(
    FunctionToDo functionToDo,
    IBorrowing.AssetName assetName,
    MessagingFee memory fee,
    bytes memory options,
    address refundAddress
) external payable onlyCoreContracts returns (MessagingReceipt memory receipt) {
    // Since we are updation global struct only we have passed zero params
    return sendInternal(
        dstEid,
        functionToDo,
        0,
        0,
        0,
        CDSInterface.LiquidationInfo(0,0,0,0,IBorrowing.AssetName.DUMMY,0),
        0,
        assetName,
        fee,
        options,
        refundAddress
    );
}
```

[GlobalVariables.sol#L345-L382](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/Core_logic/GlobalVariables.sol#L345-L382)
```solidity
    function sendInternal(
        uint32 _dstEid,
        FunctionToDo _functionToDo,
        uint256 _optionsFeesToGetFromOtherChain,
        uint256 _cdsAmountToGetFromOtherChain,
        uint256 _liqAmountToGetFromOtherChain,
        CDSInterface.LiquidationInfo memory _liquidationInfo,
        uint128 _liqIndex,
        IBorrowing.AssetName _assetName,
        MessagingFee memory _fee,
        bytes memory _options,
        address _refundAddress
    ) internal returns (MessagingReceipt memory receipt) {
        // encoding the message
        bytes memory _payload = abi.encode(OAppData(
                _functionToDo,
                CallingFunction.DUMMY,
                _optionsFeesToGetFromOtherChain,
                _cdsAmountToGetFromOtherChain,
                _liqAmountToGetFromOtherChain,
                _liquidationInfo,
                _liqIndex,
                USDaOftTransferData(address(0), 0),
                CollateralTokenTransferData(address(0), 0, 0, 0),
                omniChainData,
                _assetName,
                s_collateralData[_assetName]
            )
        );
        // Calling layer zero send function to send to dst chain
        receipt = _lzSend(
            _dstEid,
            _payload,
            _options,
            _fee,
            payable(_refundAddress)
        );
    }
```

 When `_lzSend` is called, it sends all the information needed to the Layer Zero end point so it sends the information passed in the `_payload` to the chain B.

3) Receiving in chain B the `_payload` sent form chain A:
This step is crucial. The `GlobalVariables` contract implements the `_lzReceive` function which indicates how the `_payload` data sent from chain A will be treated.
As the `GlobalVariables::_lzReceive` function handles the cases in which `OFT` transfer is needed, the code showed is just the needed for this issue: `IGlobalVariables::IFunctionToDo.DUMMY`.
[GlobalVariables.sol#L531-L641](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/Core_logic/GlobalVariables.sol#L531-L641)
```solidity
    function _lzReceive(
        Origin calldata /*_origin*/,
        bytes32 /*_guid*/,
        bytes calldata payload,
        address /*_executor*/,
        bytes calldata /*_extraData*/
    ) internal override {
        // Decoding the message from src
        OAppData memory oappData = abi.decode(payload, (OAppData));
        
        // Options for oft transfer
        bytes memory _options;
        // Native fee required
        MessagingFee memory _fee;
        //? based on functionToDo, do the lz transactions
        // If needs to transfer USDa only
        if (oappData.functionToDo == FunctionToDo.TOKEN_TRANSFER) {
            ...
        } else if (oappData.functionToDo == FunctionToDo.COLLATERAL_TRANSFER || oappData.functionToDo == FunctionToDo.BOTH_TRANSFER) {
            ...
        }
        
        // Update the total cds deposited amount with options fees
        cds.updateTotalCdsDepositedAmountWithOptionFees(uint128(oappData.optionsFeesToRemove + oappData.cdsAmountToRemove + oappData.liqAmountToRemove));
        // Update the total cds deposited amount
        cds.updateTotalCdsDepositedAmount(uint128(oappData.cdsAmountToRemove + oappData.liqAmountToRemove));
        // Update the total available liquidation amount in CDS
        cds.updateTotalAvailableLiquidationAmount(oappData.liqAmountToRemove);
        // Update the liquidation info in CDS
        cds.updateLiquidationInfo(oappData.liqIndex, oappData.liquidationInfo);
        // Update the global omnichain data struct
        omniChainData = oappData.message;
        // Update the individual collateral data
        s_collateralData[oappData.assetName] = oappData.collateralData;
    }
```

In this last part of the `GlobalVariables::_lzReceive` function, it can be seen that some updates to the chain B data are made. Specifically, I want to focus on the last three state updates that are made:
[GlobalVariables.sol#L635-L640](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/Core_logic/GlobalVariables.sol#L635-L640)
```solidity
// Update the liquidation info in CDS
cds.updateLiquidationInfo(oappData.liqIndex, oappData.liquidationInfo); // a
// Update the global omnichain data struct
omniChainData = oappData.message; // b
// Update the individual collateral data
s_collateralData[oappData.assetName] = oappData.collateralData; // c
```

a) `CDS::updateLiquidationInfo` stores the liquidation information in the liquidation index given which comes from the `omniChainData.noOfLiquidations` accumulator value:
[CDS.sol#L783-L788](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/Core_logic/CDS.sol#L783-L788)
```solidity
function updateLiquidationInfo(uint128 index, LiquidationInfo memory liquidationData)
		external
		onlyGlobalOrLiquidationContract
{
        omniChainCDSLiqIndexToInfo[index] = liquidationData;
}
```

b) The `omniChainData` is directly updated as the `omniChainData` of chain A (or source chain).

c) The `s_collateralData` of the asset passed in the `GlobalVariables::send` function is updated to match the collateral data in chain A (source)

The example of the `CDS::deposit` flow was great but the issue extends to every single function that updates the other chain information.

Due to the asynchronous nature of LayerZero  and the way those three updates of state (a, b, c) are done, a **race condition** presents when in chain A the `_lzSend` function is called with updated `omniChainData` and before the `_lzReceive` function is called in chain B, in chain B there is a call to `_lzSend`. This means that `omniChainData` from chain B updated before receiving the update from chain A. This will make chain A to have lost the updates that was sending in the first `_lzSend` call and update with chain B outdated state. Same thing will occur in chain B.

The exact same thing occurs when two liquidations occurs at the same time in the different chains. Both will try to update in the other chain using the same liquidation index and one of the liquidation info will be lost in the two chains:
[borrowing.sol#L399-L408](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/Core_logic/borrowing.sol#L399-L408)
```solidity
globalVariables.sendForLiquidation{value: fee.nativeFee}(
    IGlobalVariables.FunctionToDo(2),
    noOfLiquidations, //@audit this value should be gotten from omniChainData but that's other issue
    liquidationInfo,
    getBorrowingResult.depositDetails.assetName,
    fee,
    _options,
    msg.sender
);
```

This issue occurs because of an intention of having a strict consistency in a system that is asynchronous. 

### Internal pre-conditions

None

### External pre-conditions

`GlobalVariables::_lzSend` is called in the two chains before a `GlobalVariables::_lzReceive` is called on either chain. The occurrence of this problem increases when the chains are congested.

### Attack Path

None

### Impact

- Lost liquidation info in both chains (`CDS` depositors who opted for liquidation earnings will lose earnings)
- Omnichain data inconsistency. All calculations that involve omnichain data will be incorrect. 

### PoC

None

### Mitigation

Implement the **eventual consistency with delta-based updates** or **delta synchronization** approach to make cross-chain updates. This technique ensures cross-chain consistency by transmitting only the changes (deltas) rather than the entire state, which reduces the likelihood of conflicts and makes updates more efficient.

For example, in the case of liquidations, the best way to update the liquidation information in the destination chain to ensure data won't be lost or overwritten would be:
```diff
- function updateLiquidationInfo(uint128 index, LiquidationInfo memory liquidationData)
+ function updateLiquidationInfo(LiquidationInfo memory liquidationData)
      external
      onlyGlobalOrLiquidationContract
  {
-    omniChainCDSLiqIndexToInfo[index] = liquidationData;
+    omniChainCDSLiqIndexToInfo.push(liquidationData);
  }
```

Indexes can still be tracked, but as the strict consistency of the order is not relevant, this approach could be taken.

A correct **delta-based update** approach is taken in `CDS::updateTotalCdsDepositedAmount`, as example.