Jumpy Beige Pigeon

High

# Incorrect State Update in `ABONDToken.transferFrom` Function

### Summary

The `transferFrom` function in `ABONDToken` contract incorrectly updates the state of the `msg.sender` instead of the `from` address. This results in incorrect state management for token transfers, potentially leading to inconsistencies in user balances and states.

### Root Cause

The `transferFrom` function is intended to allow a spender to transfer tokens on behalf of the token owner (`from` address). However, the function mistakenly updates the state of the `msg.sender` instead of the `from` address.

```solidity
    function transferFrom(
        address from,
        address to,
        uint256 value
    ) public override returns (bool) {
        // check the input params are non zero
        require(from != address(0) && to != address(0), "Invalid User");

        // get the sender and receiver state
        State memory fromState = userStates[from];
        State memory toState = userStates[to];

        // update receiver state
        toState = Colors._credit(fromState, toState, uint128(value));
        userStates[to] = toState;

        // update sender state
        fromState = Colors._debit(fromState, uint128(value));
@>      userStates[msg.sender] = fromState;

        // transfer abond
        super.transferFrom(from, to, value);
        return true;
    }
```
https://github.com/sherlock-audit/2024-11-autonomint/blob/main/Blockchain/Blockchian/contracts/Token/Abond_Token.sol#L165

The line `userStates[msg.sender] = fromState;` is incorrect because it updates the state of the `msg.sender` instead of the `from` address. This results in the from address's state not being properly debited for the transferred amount.

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

1. Call the `transferFrom()` function with valid parameters.
2. Observe that the from address's state is not debited, while the spenders [`msg.sender`] state is incorrectly modified.

### Impact

1. The `from` address's state remains unchanged, leading to discrepancies between the recorded state and actual token balances.
2. The spenders state is overriden with the state of `from` address, so spender will not able to redeem his yields in [`Borrowings` contract](https://github.com/sherlock-audit/2024-11-autonomint/blob/main/Blockchain/Blockchian/contracts/lib/BorrowLib.sol#L990C1-L992C98)

### PoC

_No response_

### Mitigation

Update the transferFrom function to correctly debit the from address's state:

```diff
    function transferFrom(
        address from,
        address to,
        uint256 value
    ) public override returns (bool) {
        // check the input params are non zero
        require(from != address(0) && to != address(0), "Invalid User");

        // get the sender and receiver state
        State memory fromState = userStates[from];
        State memory toState = userStates[to];

        // update receiver state
        toState = Colors._credit(fromState, toState, uint128(value));
        userStates[to] = toState;

        // update sender state
        fromState = Colors._debit(fromState, uint128(value));
-       userStates[msg.sender] = fromState;
+       userStates[from] = fromState;

        // transfer abond
        super.transferFrom(from, to, value);
        return true;
    }
```