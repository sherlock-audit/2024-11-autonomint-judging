Dry Brown Cheetah

High

# Stale `lastEthprice` used in `borrowing::depositTokens` will artificially inflate/deflate the ratio

### Summary

The [`borrowing::depositTokens`](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/Core_logic/borrowing.sol#L226) is used by users to deposit collateral and get USDa tokens minted in return.
```solidity
    function depositTokens(
        BorrowDepositParams memory depositParam
    ) external payable nonReentrant whenNotPaused(IMultiSign.Functions(0)) {
```
Furthermore, the  [`BorrowLib::deposit`](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/lib/BorrowLib.sol#L644) uses the [`BorrowLib::calculateRatio`](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/lib/BorrowLib.sol#L156) function for updating net profit and loss based on the price difference between `currentEthPrice` and `lastEthPrice`.
```solidity
        // Calculate net P/L of CDS Pool
        // if the current eth price is high
        if (currentEthPrice > lastEthprice) {
            // profit, multiply the price difference with total collateral
            netPLCdsPool = (((currentEthPrice - lastEthprice) * totalCollateralInETH) / USDA_PRECISION) / 100;                     <@
        } else {
            // loss, multiply the price difference with total collateral
            netPLCdsPool = (((lastEthprice - currentEthPrice) * totalCollateralInETH) / USDA_PRECISION) / 100;                     <@
        }
```
And this `netPLCdsPool` is adjusted for the `currentCDSPoolValue`
```solidity
        // Check it is the first deposit
        if (noOfDeposits == 0) {
            // Calculate the ethVault value
            previousData.vaultValue = amount * currentEthPrice;
            // Set the currentEthVaultValue to lastEthVaultValue for next deposit
            currentVaultValue = previousData.vaultValue;

            // Get the total amount in CDS
            // lastTotalCDSPool = cds.totalCdsDepositedAmount();
            previousData.totalCDSPool = latestTotalCDSPool;

            // BAsed on the eth prices, add or sub, profit and loss respectively
            if (currentEthPrice >= lastEthprice) {
                currentCDSPoolValue = previousData.totalCDSPool + netPLCdsPool;                     <@
            } else {
                currentCDSPoolValue = previousData.totalCDSPool - netPLCdsPool;                     <@
            }

            // Set the currentCDSPoolValue to lastCDSPoolValue for next deposit
            previousData.cdsPoolValue = currentCDSPoolValue;
            currentCDSPoolValue = currentCDSPoolValue * USDA_PRECISION;                     <@
        } else {
            // find current vault value by adding current depositing amount
            currentVaultValue = previousData.vaultValue + (amount * currentEthPrice);
            previousData.vaultValue = currentVaultValue;

            // BAsed on the eth prices, add or sub, profit and loss respectively
            if (currentEthPrice >= lastEthprice) {
                previousData.cdsPoolValue += netPLCdsPool;                     <@
            } else {
                previousData.cdsPoolValue -= netPLCdsPool;                     <@
            }
            previousData.totalCDSPool = latestTotalCDSPool;
            currentCDSPoolValue = previousData.cdsPoolValue * USDA_PRECISION;                     <@
        }
```
And finally the ratio is calculated as per this `currentCDSPoolValue`
```solidity
        uint64 ratio = uint64((currentCDSPoolValue * CUMULATIVE_PRECISION) / currentVaultValue);
        return (ratio, previousData);
```
The problem here is that the `lastEthPrice` does not update after this [`BorrowLib::deposit`](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/lib/BorrowLib.sol#L644) finishes execution (which is done correctly in `borrowing::withDraw`), hence when a new depositor deposits token via same [`borrowing::depositTokens`](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/Core_logic/borrowing.sol#L226) function, will use the stale `lastEthPrice` for calculating ratio and updated cdsPoolValue.
This will lead to cascading effect of stale omnichain data as well as lead to artificially inflated / deflated ratio which is critical as the code and README suggests:
```text
New Borrows cannot mint new stablecoin if cds/borrow ratio is below 0.2 and cds users cannot withdraw. 
```
Technically, this invariant is broken. 
Also this stale data updation totally corrupts the data which if even synced at intervals does not accurately represent how it should have been.


### Root Cause

The [`borrowing::depositTokens`](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/Core_logic/borrowing.sol#L226) misses to update `lastEthPrice` before completing the execution flow

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

1. User1 deposits tokens using `borrowing::depositTokens`.
2. After some time, User3 deposits tokens using `borrowing::depositTokens`.
3. User2 after some time deposits tokens using `borrowing::depositTokens`, here the User2 has gets his ratio checked against a stale value as well as the option price is also calculated as per stale `cdsPoolValue`.

### Impact

1. Artificially large profits or losses are accounted for calculation of CDS Pool Value, which returns a ratio that has an invariant
```text
New Borrows cannot mint new stablecoin if cds/borrow ratio is below 0.2 and cds users cannot withdraw. 
```
This clearly gets broken.
2. The option price is calculated as per stale / incorrect `cdsPoolValue`, which leads to incorrect option fees for the user while depositing tokens, leading to loss of funds.
```solidity
    function calculateOptionPrice(
        uint128 _ethPrice,
        uint256 _ethVolatility,
        uint256 _amount,
        StrikePrice _strikePrice
    ) public view returns (uint256) {
       // Rest of code
        // If the there is no borrowers in borrowing, then the cdsVault value is deposited value itself
        if (omniChainData.noOfBorrowers == 0) {
            cdsVault = omniChainData.totalCdsDepositedAmount * USDA_PRECISION;
        } else {
            // Else, get the cds vault current value from omnichain global data
            cdsVault = omniChainData.cdsPoolValue * USDA_PRECISION;                <@
        }
// Rest of code
    }
```
3. Omnichain data is incorrectly updated with stale data.

### PoC

_No response_

### Mitigation

It is recommended to update `lastEthPrice` in `depositTokens`
```diff
    function depositTokens(
        BorrowDepositParams memory depositParam
    ) external payable nonReentrant whenNotPaused(IMultiSign.Functions(0)) {
        // Assign options for lz contract, here the gas is hardcoded as 400000, we got this through testing by iteration
        bytes memory _options = OptionsBuilder.newOptions().addExecutorLzReceiveOption(400000, 0);
        // calculting fee
        MessagingFee memory fee = globalVariables.quote(
            IGlobalVariables.FunctionToDo(1),
            depositParam.assetName,
            _options,
            false
        );
        // Get the exchange rate for a collateral and eth price
        (uint128 exchangeRate, uint128 ethPrice) = getUSDValue(assetAddress[depositParam.assetName]);

        totalNormalizedAmount = BorrowLib.deposit(
            BorrowLibDeposit_Params(
                LTV,
                APR,
                lastCumulativeRate,
                totalNormalizedAmount,
                exchangeRate,
                ethPrice,
                lastEthprice
            ),
            depositParam,
            Interfaces(treasury, globalVariables, usda, abond, cds, options),
            assetAddress
        );

        //Call calculateCumulativeRate() to get currentCumulativeRate
        calculateCumulativeRate();
        lastEventTime = uint128(block.timestamp);
+        (uint128 exchangeRate, uint128 ethPrice) = getUSDValue(assetAddress[depositParam.assetName]);
+      lastEthPrice = ethPrice;
        // Calling Omnichain send function
        globalVariables.send{value: fee.nativeFee}(
            IGlobalVariables.FunctionToDo(1),
            depositParam.assetName,
            fee,
            _options,
            msg.sender
        );
    }
```