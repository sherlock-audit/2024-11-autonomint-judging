Dry Brown Cheetah

High

# Incorrect `exchangeRate` for collaterals will result in significantly lower exchange rate

### Summary

The [`MasterPriceOracle ::_price`](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/oracles/MasterPriceOracle.sol#L53) function is used to calculate the `exchangeRate` which has expected 18 decimal precision and `ethPrice` which has expected 2 decimal precision.
We know that chainlink price feeds return value with decimal precision of `8`.
However, when the underlying token address is not native ETH, the exchange rate is incorrectly passed with a 8 decimal value instead.
```solidity
    function _price(
        address underlying
    ) internal view returns (uint128, uint128) {
          /// Rest of the code
        // get oracles based on chain
        if(block.chainid == 31337 || block.chainid == 34443){ //?  31337 is used for testing
          /// Rest of the code
        } else if (block.chainid == 10) {
            AggregatorV3Interface oracle = AggregatorV3Interface(oracles[underlying]);

            // Get the eth price
            (, int256 price_, , , ) = oracle.latestRoundData();
            // If the token is ETH
            if (underlying == assetAddress[IBorrowing.AssetName.ETH]) {
                // Return Exchange rate as 1 and ETH price with 2 decimals
                return (1 ether, uint128((uint256(price_) / 1e6)));
            } else {
                (, uint128 ethPrice) = _price(assetAddress[IBorrowing.AssetName.ETH]);
                // Return Exchange rate and ETH price with 2 decimals
                return (uint128(uint256(price_)), ethPrice);      <@ - // The first parameter here is price_ which is scaled at 8 decimals
            }
        } else {
            return (0, 0);
        }
    }
```
The `getUSDValue` is being used in both `borrowing` as well as `CDS` contracts in deposit and withdraw functions.
This would mean that everywhere exchange rate for this collateral is accessed, it will be significantly lower.
For example, a user depositing tokens would get an incorrect position
```solidity
    function deposit(
        IBorrowing.BorrowLibDeposit_Params memory libParams,
        IBorrowing.BorrowDepositParams memory params,
        IBorrowing.Interfaces memory interfaces,
        mapping(IBorrowing.AssetName => address assetAddress) storage assetAddress
    ) public returns (uint256) {
        uint256 depositingAmount = params.depositingAmount;
        // Check the deposting amount is non zero
        if (params.depositingAmount == 0) revert IBorrowing.Borrow_NeedsMoreThanZero();

        // Calculate the depsoting amount in ETH
        params.depositingAmount = (libParams.exchangeRate * params.depositingAmount) / 1 ether;  <@ - Too low amount
```
This would lead to loss of funds as UDSa will be minted too low.

### Root Cause

In [`MasterPriceOracle.sol:91`](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/oracles/MasterPriceOracle.sol#L91), exchange rate is incorrectly set leading to low exchange rate.

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

1. User simply calls depositTokens with any collateral other than native ETH.
2. User would have a very significantly less USDa.

### Impact

1. Renders the `getUSDValue` across the contract as useless for OP network.
2. Loss of funds for depositors with collateral other than native ETH.

### PoC

_No response_

### Mitigation

It is recommended to calculate correct scaled price in `_price`:
```diff
    function _price(
        address underlying
    ) internal view returns (uint128, uint128) {
          /// Rest of the code
        // get oracles based on chain
        if(block.chainid == 31337 || block.chainid == 34443){ //?  31337 is used for testing
          /// Rest of the code
        } else if (block.chainid == 10) {
            AggregatorV3Interface oracle = AggregatorV3Interface(oracles[underlying]);

            // Get the eth price
            (, int256 price_, , , ) = oracle.latestRoundData();
            // If the token is ETH
            if (underlying == assetAddress[IBorrowing.AssetName.ETH]) {
                // Return Exchange rate as 1 and ETH price with 2 decimals
                return (1 ether, uint128((uint256(price_) / 1e6)));
            } else {
                (, uint128 ethPrice) = _price(assetAddress[IBorrowing.AssetName.ETH]);
                // Return Exchange rate and ETH price with 2 decimals
+               return (uint128((price_ * 1e18) / ethPrice), ethPrice);
-                return (uint128(uint256(price_)), ethPrice);
            }
        } else {
            return (0, 0);
        }
    }
```