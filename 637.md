Shallow Coral Kookaburra

High

# `usdaGainedFromLiquidation` is not increased in the liquidation flow leading to stuck funds

### Summary

In the liquidation flow, profits for `ABOND` are taken in `USDa` as `returnToAbond`. However, those funds are neither added to the `OmniChainData::usdaGainedFromLiquidation` nor to the `Treasury::usdaGainedFromLiquidation` amount, so those funds are forever stuck in `Treasury` because the trace of funds is lost. The holders of `ABOND` do not receive adequate compensation, which endangers the stability of the price of `ABOND` in the market.

### Root Cause

In `BorrowLiquidation::liquidationType1`, although the `Treasury::abondUSDaPool` is increased with `returnToAbond`, `Treasury::usdaGainedFromLiquidation` is not:
[borrowLiquidation.sol#L174-L315](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/Core_logic/borrowLiquidation.sol#L174-L315)
```solidity
    function liquidationType1(
        address user,
        uint64 index,
        uint64 currentEthPrice,
        uint256 lastCumulativeRate
    ) internal returns (CDSInterface.LiquidationInfo memory liquidationInfo) {
        // Get the borrower and deposit details
        ITreasury.GetBorrowingResult memory getBorrowingResult = treasury.getBorrowing(user, index);
        ITreasury.DepositDetails memory depositDetail = getBorrowingResult.depositDetails;
        ...
        // 20% to abond usda pool
        uint128 returnToAbond = BorrowLib.calculateReturnToAbond(depositDetail.depositedAmountInETH, depositDetail.ethPriceAtDeposit, returnToTreasury);
        treasury.updateAbondUSDaPool(returnToAbond, true);
        ...
    }
```

The `Treasury::usdaGainedFromLiquidation` helps to back `ABOND` as it is used to compensate holders when they redeem yields in `Borrowing::redeemYields`:
[BorrowLib.sol#L978-L1036](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/lib/BorrowLib.sol#L978-L1036)
```solidity
    function redeemYields(
        address user,
        uint128 aBondAmount,
        address usdaAddress,
        address abondAddress,
        address treasuryAddress,
        address borrow
    ) public returns (uint256) {
        // check abond amount is non zewro
        if (aBondAmount == 0) revert IBorrowing.Borrow_NeedsMoreThanZero();
        IABONDToken abond = IABONDToken(abondAddress);
        // get user abond state
        State memory userState = abond.userStates(user);
        // check user have enough abond
        if (aBondAmount > userState.aBondBalance) revert IBorrowing.Borrow_InsufficientBalance();
        
        ITreasury treasury = ITreasury(treasuryAddress);
        // calculate abond usda ratio
        uint128 usdaToAbondRatio = uint128((treasury.abondUSDaPool() * RATE_PRECISION) / abond.totalSupply());
        uint256 usdaToBurn = (usdaToAbondRatio * aBondAmount) / RATE_PRECISION;
        // update abondUsdaPool in treasury
        treasury.updateAbondUSDaPool(usdaToBurn, false);
        
        // calculate abond usda ratio from liquidation
        uint128 usdaToAbondRatioLiq = uint128((treasury.usdaGainedFromLiquidation() * RATE_PRECISION) /abond.totalSupply());
        uint256 usdaToTransfer = (usdaToAbondRatioLiq * aBondAmount) / RATE_PRECISION;
        //update usdaGainedFromLiquidation in treasury
        treasury.updateUSDaGainedFromLiquidation(usdaToTransfer, false);
        
        //Burn the usda from treasury
        treasury.approveTokens(
            IBorrowing.AssetName.USDa,
            borrow,
            (usdaToBurn + usdaToTransfer)
        );
        
        IUSDa usda = IUSDa(usdaAddress);
        // burn the usda
        bool burned = usda.contractBurnFrom(address(treasury), usdaToBurn);
        if (!burned) revert IBorrowing.Borrow_BurnFailed();
        
        if (usdaToTransfer > 0) {
            // transfer usda to user
            bool transferred = usda.contractTransferFrom(
                address(treasury),
                user,
                usdaToTransfer
            );
            if (!transferred) revert IBorrowing.Borrow_TransferFailed();
        }
        // withdraw eth from ext protocol
        uint256 withdrawAmount = treasury.withdrawFromExternalProtocol(user,aBondAmount);
        
        //Burn the abond from user
        bool success = abond.burnFromUser(msg.sender, aBondAmount);
        if (!success) revert IBorrowing.Borrow_BurnFailed();
        
        return withdrawAmount;
    }
```

### Internal pre-conditions

None

### External pre-conditions

None

### Attack Path

None

### Impact

Loss of `USDa` earned for `ABOND`.

### PoC

None

### Mitigation

Update the `Treasury::usdaGainedFromLiquidation` amount and also the `OmniChainData::usdaGainedFromLiquidation` in `BorrowLiquidation::liquidationType1`:
```diff
    function liquidationType1(
        address user,
        uint64 index,
        uint64 currentEthPrice,
        uint256 lastCumulativeRate
    ) internal returns (CDSInterface.LiquidationInfo memory liquidationInfo) {
        // Get the borrower and deposit details
        ITreasury.GetBorrowingResult memory getBorrowingResult = treasury.getBorrowing(user, index);
        ITreasury.DepositDetails memory depositDetail = getBorrowingResult.depositDetails;
        ...
        // 20% to abond usda pool
        uint128 returnToAbond = BorrowLib.calculateReturnToAbond(depositDetail.depositedAmountInETH, depositDetail.ethPriceAtDeposit, returnToTreasury);
        treasury.updateAbondUSDaPool(returnToAbond, true);
+       treasury.updateUSDaGainedFromLiquidation(returnToAbond, true);
+       omniChainData.usdaGainedFromLiquidation += returnToAbond;
        ...
    }
```