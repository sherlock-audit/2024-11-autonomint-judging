Atomic Lipstick Pangolin

High

# `Abond_Token.sol::transferfrom` does not correctly update state

### Summary

_No response_

### Root Cause

[Link to the affected line of code](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/Token/Abond_Token.sol#L165)
The State struct is a struct that holds the current state of Abond token holders. This struct is updated as both debit and credit actions happen. Below is the struct 
```solidity
struct State {
    uint256 cumulativeRate;
    uint128 ethBacked;
    uint128 aBondBalance;
}
```
The struct above is updated for both the credited user and the debited user through the following functions 
```solidity
    function _credit(
        State memory _fromState,
        State memory _toState,
        uint128 _amount
    ) internal pure returns(State memory){

        // find the average cumulatie rate
@>>     _toState.cumulativeRate = _calculateCumulativeRate(_amount, _toState.aBondBalance, _fromState.cumulativeRate, _toState.cumulativeRate);
        // find the eth backed
@>>     _toState.ethBacked = _calculateEthBacked(_amount, _toState.aBondBalance, _fromState.ethBacked, _toState.ethBacked);
        // increment abond balance
@>>     _toState.aBondBalance += _amount;

        return _toState;
    }
```

```solidity
    function _debit(
        State memory _fromState,
        uint128 _amount
    ) internal pure returns(State memory) {

        uint128 balance = _fromState.aBondBalance;
        
        // check user has sufficient balance
        require(balance >= _amount,"InsufficientBalance");
 
        // update user abond balance
        _fromState.aBondBalance = balance - _amount;

        // after debit, if abond balance is zero, update cr and eth backed as 0
        if(_fromState.aBondBalance == 0){
            _fromState.cumulativeRate = 0;
            _fromState.ethBacked = 0;
        }
        return _fromState;
    }
```
 
```solidity
    function transferFrom(
        address from,
        address to,
        uint256 value
    ) public override returns (bool) {
        // check the input params are non zero
        require(from != address(0) && to != address(0), "Invalid User");

        // get the sender and receiver state
@>>     State memory fromState = userStates[from];
        State memory toState = userStates[to];

        // update receiver state
        toState = Colors._credit(fromState, toState, uint128(value));
        userStates[to] = toState;

        // update sender state
@>>     fromState = Colors._debit(fromState, uint128(value));
@>>     userStates[msg.sender] = fromState;

        // transfer abond
        super.transferFrom(from, to, value);
        return true;
    }
```

The `transferFrom` function erroneosly assigns the new state of the debited account, to the `msg.sender`, instead of `from`.

The implication of this issue is that the state of the msg.sender is overwritten which can be fatal in certain cases.
Take the scenario below for an example. We have two actors; 
`Dj chicken enu po` and `Oba solomoni`
Suppose `Dj chicken enu po` approves `Oba solomoni` of their entire Abond balance, and  `Oba solomoni` exhausts the funds. The logic should set `Dj chicken enu po`  `state.aBondBalance = 0` but will wrongly set `Oba solomoni` `state.aBondBalance = 0` instead, overwriting `Oba solomoni` real balance. This introduces a permanent DOS as `Oba solomoni` previous balance is permanently lost.
`Oba solomoni` cannot redeem their tokens anymore, because of the check in `BorrowLib.sol::redeemYields`
```solidity
if (aBondAmount > userState.aBondBalance) revert IBorrowing.Borrow_InsufficientBalance();
```
`Oba solomoni` will forever be in severe pains ðŸ¤£ðŸ’€
### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

_No response_

### Impact

1. Internal accounting errors
2. Permanent fund loss
3. Overinflated balance  supposing `from` state balance is > `msg.sender` state balance 

### PoC

_No response_

### Mitigation
```diff
- userStates[msg.sender] = fromState;
+ userStates[from] = fromState;
```