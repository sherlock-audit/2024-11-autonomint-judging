Spare Plum Tardigrade

High

# Self transfer `Abond_Token` causes DoS, user cannot perform `redeemYield`.

### Summary

A self-transfer can cause a DoS, disabling the user from utilizing their tokens.
When a user transfers their `Abond` tokens to themselves, their `aBondBalance` becomes `0`.

### Root Cause
In [Abond_Token.sol::L114-L170](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/Token/Abond_Token.sol#L114-L170)
```solidity
 function transfer(
        address to,
        uint256 value
    ) public override returns (bool) {

        // get the sender and receiver state
        State memory fromState = userStates[msg.sender];
        State memory toState = userStates[to];

        // check sender has enough abond to transfer
        require(fromState.aBondBalance >= value, "Insufficient aBond balance");

        // update receiver state
        toState = Colors._credit(fromState, toState, uint128(value));
        userStates[to] = toState;

        // update sender state
        fromState = Colors._debit(fromState, uint128(value));
        userStates[msg.sender] = fromState;

}

   function transferFrom(
        address from,
        address to,
        uint256 value
    ) public override returns (bool) {
  
        State memory fromState = userStates[from];
        State memory toState = userStates[to];

        // update receiver state
        toState = Colors._credit(fromState, toState, uint128(value));
        userStates[to] = toState;

        // update sender state
        fromState = Colors._debit(fromState, uint128(value));
        userStates[msg.sender] = fromState;
   
```
The transfer method in `Abond_Token` uses the State structure and the Colors library to process token transfers. When the transfer method is triggered, it processes `Colors._credit` followed by `Colors._debit`:
```solidity
# Abond_Token.sol
 # Transfer method
 State memory fromState = userStates[msg.sender];
        State memory toState = userStates[to];

 // update receiver state
        toState = Colors._credit(fromState, toState, uint128(value));
        userStates[to] = toState;

        // update sender state
        fromState = Colors._debit(fromState, uint128(value));
        userStates[msg.sender] = fromState;

# Colors.sol
function _credit(
        State memory _fromState,
        State memory _toState,
        uint128 _amount
    ) internal pure returns(State memory){

        // find the average cumulatie rate
        _toState.cumulativeRate = _calculateCumulativeRate(_amount, _toState.aBondBalance, _fromState.cumulativeRate, _toState.cumulativeRate);
        // find the eth backed
        _toState.ethBacked = _calculateEthBacked(_amount, _toState.aBondBalance, _fromState.ethBacked, _toState.ethBacked);
        // increment abond balance
=>        _toState.aBondBalance += _amount;

        return _toState;
    }

    function _debit(
        State memory _fromState,
        uint128 _amount
    ) internal pure returns(State memory) {

        uint128 balance = _fromState.aBondBalance;
        
        // check user has sufficient balance
        require(balance >= _amount,"InsufficientBalance");
 
        // update user abond balance
 =>       _fromState.aBondBalance = balance - _amount;

        // after debit, if abond balance is zero, update cr and eth backed as 0
        if(_fromState.aBondBalance == 0){
            _fromState.cumulativeRate = 0;
            _fromState.ethBacked = 0;
        }
        return _fromState;
    } 
```
The transfer method follows this flow:

1. `_credit` is triggered first, adding the transfer amount to the recipient's balance.  
2. `_debit` is triggered afterward, reducing the sender's balance by the transfer amount.  

However, due to the way states are managed:  
```solidity
State memory fromState = userStates[msg.sender];
State memory toState = userStates[to];
```
`fromState` and `toState` are treated as separate entities. During a self-transfer, this causes an issue resulting in the loss of the user's `aBondBalance`. This happens because when `_debit` is executed, it does not take into account the updated balance after `_credit`. Instead, it reduces the `aBondBalance` based on the original `fromState`.

**Example:**
- Andi has `2000e18` `Abond_Token` and transfers the full amount to himself.  
- `_credit` updates Andi's `aBondBalance` to `4000e18`, but this update is ignored.  
- `_debit` reduces the `aBondBalance` by `2000e18` based on the initial `fromState`.  
- As a result, Andi's `aBondBalance` becomes `0`, since `_debit` is the final step in the state process.  

This causes all subsequent operations, such as transfers or burns, to fail, since Andi's `aBondBalance` is now zero.

Contracts that rely on the user's `aBondBalance` for validation (e.g., `redeemYield`) will also fail.

### Internal pre-conditions

_No response_

### External pre-conditions

1. The user performs a self-transfer of Abond_Token.
2. As a result, their token becomes permanently unusable (DoS).

### Attack Path

_No response_ 

### Impact
A self-transfer of `Abond_Token` causes a permanent DoS for the user, making their tokens non-transferable and non-burnable.

### PoC

```solidity
pragma solidity 0.8.22;

import {Test, StdUtils, console} from "forge-std/Test.sol";
import {State, IABONDToken, TestABONDToken} from "../../contracts/TestContracts/Copy_Abond_Token.sol";

contract PoCTest is Test {
    TestABONDToken abondToken;
    address andi;
    address jhon;

    function setUp() public {
        andi = makeAddr("andi");
        jhon = makeAddr("jhon");
        abondToken = new TestABONDToken();
        abondToken.initialize();
        abondToken.setBorrowingContract(address(this));
        abondToken.mint(andi, 1, 2000e18);
    }

    function testTransfer() public {
        vm.startPrank(andi);
        assertEq(abondToken.balanceOf(andi), 2000e18);

        IABONDToken abond = IABONDToken(address(abondToken));

        State memory userStatebefore = abond.userStates(andi);
        uint128 userStateBalbefore = userStatebefore.aBondBalance;
        console.log("userState Balance Before:", userStateBalbefore);

        abondToken.transfer(andi, 2000e18);
        assertEq(abondToken.balanceOf(andi), 2000e18);
        
        State memory userStateAfter = abond.userStates(andi);
        uint128 userStateBalAfter = userStateAfter.aBondBalance;
        assertEq(userStateBalAfter, 0);
        console.log("userState Balance After:", userStateBalAfter);
        vm.expectRevert();
        abondToken.transfer(jhon, 2000e18);


    }
}

```

### Mitigation
Disable self-transfers.