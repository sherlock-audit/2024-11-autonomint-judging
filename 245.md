Restless Orange Iguana

High

# Incorrect `userStates` update in ABONDToken::transferFrom function

### Summary

In the `ABONDToken::transferFrom` function, the `userStates` mapping is incorrectly updated for `msg.sender` instead of `from`. This leads to the `userStates[from]` remaining unchanged while `userStates[msg.sender]` is incorrectly modified.

[ABONDToken::transferFrom](https://github.com/sherlock-audit/2024-11-autonomint/blob/main/Blockchain/Blockchian/contracts/Token/Abond_Token.sol#L147) function:
```javascript
function transferFrom(
    address from,
    address to,
    uint256 value
) public override returns (bool) {
    require(from != address(0) && to != address(0), "Invalid User");

    // get the sender and receiver state
    State memory fromState = userStates[from];
    State memory toState = userStates[to];

    // update receiver state
    toState = Colors._credit(fromState, toState, uint128(value));
    userStates[to] = toState;

    // update sender state
    fromState = Colors._debit(fromState, uint128(value));
=>  userStates[msg.sender] = fromState;

    // transfer abond
    super.transferFrom(from, to, value);
    return true;
}
```

### Root Cause

The `ABONDToken::transferFrom` function mistakenly updates `userStates[msg.sender]` instead of `userStates[from]`.

### Impact

The `userStates[from]` does not reflect the transfer, and the `userStates[msg.sender]` is improperly altered, leading to incorrect state representation.

### Mitigation

Correct the logic in the `ABONDToken::transferFrom` function to ensure updates apply to `userStates[from]`.

```diff
function transferFrom(
    address from,
    address to,
    uint256 value
) public override returns (bool) {
    require(from != address(0) && to != address(0), "Invalid User");

    // get the sender and receiver state
    State memory fromState = userStates[from];
    State memory toState = userStates[to];

    // update receiver state
    toState = Colors._credit(fromState, toState, uint128(value));
    userStates[to] = toState;

    // update sender state
    fromState = Colors._debit(fromState, uint128(value));
-   userStates[msg.sender] = fromState;
+   userStates[from] = fromState;

    // transfer abond
    super.transferFrom(from, to, value);
    return true;
}
```