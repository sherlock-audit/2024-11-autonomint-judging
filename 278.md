Shallow Coral Kookaburra

Medium

# In `Borrowing`, `LiquidationType.One` will fail in some scenarios because `BorrowLib::getLiquidationAmountProportions` logic is incomplete

### Summary

During liquidation type one, when there is enough liquidity in the other chain to liquidate but not in the chain the liquidation is occurring, the protocol increases the amount it needs from the other chain, not checking if that increase in the amount is actually available there. This leads to malfunctioning of the protocol.

### Root Cause

The `Borrowing::liquidate` function calls the `BorrowLiquidation::liquidateBorrowPosition` function:
[borrowing.sol#L363-L409](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/Core_logic/borrowing.sol#L363-L409)
```solidity
    function liquidate(
        address user,
        uint64 index,
        IBorrowing.LiquidationType liquidationType
    ) external payable whenNotPaused(IMultiSign.Functions(2)) onlyAdmin {
    
        ...
        
        (, uint128 ethPrice) = getUSDValue(assetAddress[AssetName.ETH]);
        // Call the liquidateBorrowPosition function in borrowLiquidation contract
        CDSInterface.LiquidationInfo memory liquidationInfo = borrowLiquidation
            .liquidateBorrowPosition{value: msg.value - fee.nativeFee}(
            user,
            index,
            uint64(ethPrice),
            liquidationType,
            lastCumulativeRate
        );
        
        ...
    }
```

Then, `BorrowLiquidation::liquidateBorrowPosition` checks the liquidation type the admin wants and executes its function. The issue is present in the liquidation type one.

As liquidations are paid proportionally between the two chains (according to CDS deposits), there is a function used inside `BorrowLiquidation::liquidationType1` to know how much funds will be used from one chain and from the other:
[borrowLiquidation.sol#L227-L234](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/Core_logic/borrowLiquidation.sol#L227-L234)
```solidity
// calculate the amount of usda to get from Other chains
uint256 liqAmountToGetFromOtherChain = BorrowLib.getLiquidationAmountProportions(
        liquidationAmountNeeded,
        cds.getTotalCdsDepositedAmount(),
        omniChainData.totalCdsDepositedAmount - omniChainData.downsideProtected,
        cds.totalAvailableLiquidationAmount(),
        omniChainData.totalAvailableLiquidationAmount
    );
```

`BorrowLib::getLiquidationAmountProportions` fails to consider the case in which there is enough liquidity in the other chain to liquidate but not in the chain the liquidation is occurring (see "internal pre-conditions"):
[BorrowLib.sol#L375-L414](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/lib/BorrowLib.sol#L375-L414)
```solidity
    function getLiquidationAmountProportions(
        uint256 _liqAmount,
        uint256 _totalCdsDepositedAmount,
        uint256 _totalGlobalCdsDepositedAmount,
        uint256 _totalAvailableLiqAmount,
        uint256 _totalGlobalAvailableLiqAmountAmount
    ) public pure returns (uint256) {
        // Calculate other chain cds deposited amount
        uint256 otherChainCDSAmount = _totalGlobalCdsDepositedAmount - _totalCdsDepositedAmount;

        // calculate other chain available liq amount in cds
        uint256 totalAvailableLiqAmountInOtherChain = _totalGlobalAvailableLiqAmountAmount - _totalAvailableLiqAmount;

        // find the share of each chain
        uint256 share = (otherChainCDSAmount * LIQ_AMOUNT_PRECISION) / _totalGlobalCdsDepositedAmount;
        // amount to get from other chain
        uint256 liqAmountToGet = (_liqAmount * share) / LIQ_AMOUNT_PRECISION;
        // amount to get from this chain
        uint256 liqAmountRemaining = _liqAmount - liqAmountToGet;

        // if tha other chain dont have any available liquidation amount
        if (totalAvailableLiqAmountInOtherChain == 0) {
            liqAmountToGet = 0;
        } else {
            // if the other chain dont have sufficient liq amount to get, get the remaining from thsi chain itself
            if (totalAvailableLiqAmountInOtherChain < liqAmountToGet) {
                liqAmountToGet = totalAvailableLiqAmountInOtherChain;
            } else {
                if (
                    totalAvailableLiqAmountInOtherChain > liqAmountToGet
                        && _totalAvailableLiqAmount < liqAmountRemaining
                ) {
                    liqAmountToGet += liqAmountRemaining - _totalAvailableLiqAmount;
                    // @audit you have to check that liqAmountToGet now is not greater than totalAvailableLiqAmountInOtherChain
                } else {
                    // @audit-info useless else statement
                    liqAmountToGet = liqAmountToGet;
                }
            }
        }
        return liqAmountToGet;
    }
```

You can see where the `@audit` tag is, the logic is insufficient to consider such case as it does in one of the previous `if` statements:
```solidity
if (totalAvailableLiqAmountInOtherChain < liqAmountToGet) {
        liqAmountToGet = totalAvailableLiqAmountInOtherChain;
}
```

Then, in `GlobalVariables::_lzReceive` in the other chain, an underflow will occur when trying to update total available liquidation amount:
[GlobalVariables.sol#L633-L634](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/Core_logic/GlobalVariables.sol#L633-L634)
```solidity
// Update the total available liquidation amount in CDS
cds.updateTotalAvailableLiquidationAmount(oappData.liqAmountToRemove);
```
[CDS.sol#L794-L801](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/Core_logic/CDS.sol#L794-L801)
```solidity
function updateTotalAvailableLiquidationAmount(
    uint256 amount
) external onlyGlobalOrLiquidationContract {
    // If the totalAvailableLiquidationAmount is non zero
    if (totalAvailableLiquidationAmount != 0) {
        totalAvailableLiquidationAmount -= amount;
    }
}
```


### Internal pre-conditions

A liquidation type one has to occur when:
- There aren't enough funds to cover its share of liquidation in the chain the liquidation is occurring.
- The other chain has enough to cover its share of funds used for liquidation, but not enough to cover the lack of funds in the chain the liquidation is occurring.

### External pre-conditions

None

### Attack Path

None

### Impact

Liquidation will occur but in the other chain, when the information is received by Layer Zero endpoint to transfer `USDa`, the accounting will be broken because more funds than intended will be transferred. Funds that were not available to be used for liquidation, will be tried to be used for this one; the limit to be used is `CDS::totalAvailableLiquidationAmount`.  An underflow will occur in `GlobalVariables::_lzReceive` when trying to deduct more than `totalAvailableLiquidationAmount`.

### PoC

None

### Mitigation

Add a check so you return `totalAvailableLiqAmountInOtherChain` as `liqAmountToGet` in `BorrowLib::getLiquidationAmountProportions` instead of a bigger amount:
```diff
		// if tha other chain dont have any available liquidation amount
        if (totalAvailableLiqAmountInOtherChain == 0) {
            liqAmountToGet = 0;
        } else {
            // if the other chain dont have sufficient liq amount to get, get the remaining from thsi chain itself
            if (totalAvailableLiqAmountInOtherChain < liqAmountToGet) {
                liqAmountToGet = totalAvailableLiqAmountInOtherChain;
            } else {
                if (
                    totalAvailableLiqAmountInOtherChain > liqAmountToGet
                        && _totalAvailableLiqAmount < liqAmountRemaining
                ) {
                    liqAmountToGet += liqAmountRemaining - _totalAvailableLiqAmount;
+                   if(liqAmountToGet > totalAvailableLiqAmountInOtherChain) {
+                         liqAmountToGet = totalAvailableLiqAmountInOtherChain;
+                   }
                } else {
                    // @audit-info useless else statement
                    liqAmountToGet = liqAmountToGet;
                }
            }
        }
        return liqAmountToGet;
```