Boxy Cider Carp

Medium

# Options can be renewed for already liquidated or withdrawn borrowing positions

### Summary

Due to usage of wrong conditional operators usage in determining **when** to revert in the `if` blocks:
```solidity

    /**
     * @dev gets the options fees, the borrower needs to pay to renew
     * @param index the index of the position
     */
    function getOptionFeesToPay(
        ITreasury treasury,
        uint64 index
    ) public view returns (uint256) {
        
        // Get the deposit details
        ITreasury.GetBorrowingResult memory getBorrowingResult = treasury.getBorrowing(msg.sender, index);
        ITreasury.DepositDetails memory depositDetail = getBorrowingResult.depositDetails;

        // check if borrowerIndex in BorrowerDetails of the msg.sender is greater than or equal to Index
        if (getBorrowingResult.totalIndex >= index) {
            // check the position is not withdrew or liquidated
            if (depositDetail.withdrawed && depositDetail.liquidated) // @audit-info: shouldn't it use the OR || p
                revert IBorrowing.Borrow_AlreadyWithdrewOrLiquidated();

```

The `renewOptions` function is intended for:
> Renew(ing)(s) the 20% downside protection of the borrower position by 30 days by providing USDa tokens.



### Root Cause

```solidity
    /**
     * @dev renew the position by 30 days
     * @param index index of the position to renew
     */
    function renewOptions(
        IBorrowing.Interfaces memory interfaces,
        uint64 index
    ) external returns (bool) {
        // calculate options fees needs to pay to renew
        uint256 optionsFeesNeedsToPay = getOptionFeesToPay(interfaces.treasury,index);

        // check whether the user has enough options fees to pay
        if (interfaces.usda.balanceOf(msg.sender) < optionsFeesNeedsToPay) revert IBorrowing.Borrow_InsufficientBalance();

        // transfer the options fees from user to treasury
        bool sent = interfaces.usda.transferFrom(msg.sender, address(interfaces.treasury), optionsFeesNeedsToPay);

        if (!sent) revert IBorrowing.Borrow_USDaTransferFailed();

        // getting omnichain global data
        IGlobalVariables.OmniChainData memory omniChainData = interfaces.globalVariables.getOmniChainData();

        // updating last cumulative rate
        omniChainData.lastCumulativeRate = interfaces.cds.calculateCumulativeRate(uint128(optionsFeesNeedsToPay));
        omniChainData.totalCdsDepositedAmountWithOptionFees += optionsFeesNeedsToPay; // @audit-info: these are purely the options fees on this particular chain!

        // updating omnichain data
        interfaces.globalVariables.setOmniChainData(omniChainData);

        return true;
    }
```

The above is the internal `renewOptions` implementation to be called from the `borrowing` module.

The users should never be allowed to renew options for the borrowing positions of theirs which have either already been liquidated, or withdrawn.

```solidity
    /**
     * @dev renew the position by 30 days by paying usda.
     * The user will have 80% downside protection
     * @param index index of the position to renew
     */
    function renewOptions(uint64 index) external payable {
        if (
            !BorrowLib.renewOptions(
                Interfaces(
                    treasury,
                    globalVariables,
                    usda,
                    abond,
                    cds,
                    options
                ),
                index
            )
        ) revert Borrow_RenewFailed();

        // Get the deposit details
        ITreasury.GetBorrowingResult memory getBorrowingResult = treasury.getBorrowing(msg.sender, index);
        ITreasury.DepositDetails memory depositDetail = getBorrowingResult.depositDetails;
        depositDetail.optionsRenewedTimeStamp = block.timestamp;
        treasury.updateDepositDetails(msg.sender, index, depositDetail);

        // define options for lz send transaction with 400000 gas(found by testing)
        bytes memory _options = OptionsBuilder.newOptions().addExecutorLzReceiveOption(400000, 0);

        // calculting fee for lz send transaction
        MessagingFee memory fee = globalVariables.quote(
            IGlobalVariables.FunctionToDo(1),
            depositDetail.assetName,
            _options,
            false
        );

        // Calling Omnichain send function
        globalVariables.send{value: fee.nativeFee}(
            IGlobalVariables.FunctionToDo(1),
            depositDetail.assetName,
            fee,
            _options,
            msg.sender
        ); // @audit-info: check this out! delayed call before updates!

        emit Renewed(msg.sender, index, block.timestamp);
    }

```



### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

The user can simply `renewOptions` for the borrowing positions of his own which have already been either liquidated or withdrawn, extending his eligibility for the downside protected funds pool.

### Impact

Due to the usage of the flawed `if` condition, the options can still be renewed for the positions that were already withdrawn or liquidated.

### PoC

_No response_

### Mitigation

```diff
        // check if borrowerIndex in BorrowerDetails of the msg.sender is greater than or equal to Index
        if (getBorrowingResult.totalIndex >= index) {
            // check the position is not withdrew or liquidated
-           if (depositDetail.withdrawed && depositDetail.liquidated) // @audit-info: shouldn't it use the OR || operator here
+           if (depositDetail.withdrawed || depositDetail.liquidated) // @audit-info: shouldn't it use the OR || operator here
                revert IBorrowing.Borrow_AlreadyWithdrewOrLiquidated();
```

https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/lib/BorrowLib.sol#L442-L443