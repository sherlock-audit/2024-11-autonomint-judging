Powerful Honeysuckle Anteater

High

# Liquidation amounts are not correctly deducted from the totalCDS value available, when doing a dCDS withdraw

### Summary
Liquidation amounts are not correctly deducted from the `totalCdsDepositedAmount` omnichain variable when a `dCDS` user performs a withdrawal.

### Root Cause

This case occurs in this part of [CDSLib.sol#L726-L736](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/lib/CDSLib.sol#L726-L736)
```solidity
                } else {
                    // update totalCdsDepositedAmount
                    totalCdsDepositedAmount -= (params.cdsDepositDetails.depositedAmount - params.cdsDepositDetails.liquidationAmount);
                    params.omniChainData.totalCdsDepositedAmount -= (params.cdsDepositDetails.depositedAmount - params.cdsDepositDetails.liquidationAmount);
                    // update totalCdsDepositedAmountWithOptionFees
                    totalCdsDepositedAmountWithOptionFees -= (params.cdsDepositDetails.depositedAmount - params.cdsDepositDetails.liquidationAmount + params.optionsFeesToGetFromOtherChain);
                    params.omniChainData.totalCdsDepositedAmountWithOptionFees -= (
                        params.cdsDepositDetails.depositedAmount -
                        params.cdsDepositDetails.liquidationAmount +
                        params.optionFees);
                }
```
The issue arises because the deposited amounts are reduced by `params.cdsDepositDetails.liquidationAmount`, resulting in incorrect global variable updates.

The `params.cdsDepositDetails.liquidationAmount` is previously updated during the calculation of the liquidation amounts owed to this `dCDS` depositor.

```solidity
                for (uint128 i = (liquidationIndexAtDeposit + 1); i <= params.omniChainData.noOfLiquidations; i++) {
                    uint128 liquidationAmount = params.cdsDepositDetails.liquidationAmount;
                    // If the user available liquidation is non zero
                    if (liquidationAmount > 0) {
                        CDSInterface.LiquidationInfo memory liquidationData = omniChainCDSLiqIndexToInfo[i];
                        // Calculate the share by taking ratio between
                        // User's available liquidation amount and total available liquidation amount
                        uint128 share = (liquidationAmount * 1e10) / uint128(liquidationData.availableLiquidationAmount);
                        // Update users available liquidation amount
@>>                     params.cdsDepositDetails.liquidationAmount -= getUserShare(liquidationData.liquidationAmount, share);
                        // Based on the collateral type calculate the liquidated collateral to give to user
                        if (liquidationData.assetName == IBorrowing.AssetName.ETH) {
                            // increment eth amount
                            params.ethAmount += getUserShare(liquidationData.collateralAmount, share);
                        } else if (liquidationData.assetName == IBorrowing.AssetName.WeETH) {
                            // increment weeth amount and weth amount value
                            weETHAmount += getUserShare(liquidationData.collateralAmount, share);
                            weETHAmountInETHValue += getUserShare(liquidationData.collateralAmountInETHValue, share);
                        } else if (liquidationData.assetName == IBorrowing.AssetName.WrsETH) {
                            // increment rseth amount and rseth amount value
                            rsETHAmount += getUserShare(liquidationData.collateralAmount, share);
                            rsETHAmountInETHValue += getUserShare(liquidationData.collateralAmountInETHValue, share);
                        }
                    }
                }
@>>             uint256 returnAmountWithGains = params.returnAmount + params.cdsDepositDetails.liquidationAmount;
@>>             returnAmountWithGains -= params.cdsDepositDetails.initialLiquidationAmount;
```
The way this works is that for every liquidation that has occurred since the user's deposit, the corresponding amount to be returned is deducted from the user's maximum liquidation amount, which they specified when depositing. This maximum amount is represented by `params.cdsDepositDetails.liquidationAmount`, and it is reduced during the calculation process as described above.

Later, we use `returnAmountWithGains` to return the corresponding amount in USDA, while the remaining amount is returned in ETH collateral.  
`returnAmountWithGains` essentially represents `(initial USDA deposited)` - `(liquidation amount to give)`.

After the loop, `params.cdsDepositDetails.liquidationAmount` represents `initialLiquidationAmount` - `(liquidation amount to give)`.

It is incorrect to reduce the global deposited amount fields, such as `params.omniChainData.totalCdsDepositedAmount`, by `(depositedAmount - the amount left undistributed)`. Instead, we should remove the USDA amount given to the user from `totalCdsDepositedAmount`, which should be calculated as `(depositedAmount - liquidation amount to give)`, since the `(liquidation amount to give)` is what is provided via ETH collateral.

### Internal Preconditions

_No response_

### External Preconditions

_No response_

### Attack Path

1. A user deposits 1000 USDA and sets the liquidation amount to 500 USDA.
2. The liquidated amount to be given in ETH is 300 USDA.
3. The USDA that should be removed from `totalCdsDepositedAmount` is `1000 USDA - 300 USDA`.
4. The issue is that the current logic removes `1000 USDA - 200 USDA`, where 200 USDA is the undistributed amount (`500 USDA - 300 USDA`), which is incorrect.

### Impact
This results in incorrect global variables, negatively affecting the protocol's calculations, and the accurate tracking of the available totalCdsDepositedAmount. That could be catastrophic and lead to loss of funds.

### Mitigation

As suggested you may use:
```diff
                } else {
                    // update totalCdsDepositedAmount
                    totalCdsDepositedAmount -= (params.cdsDepositDetails.depositedAmount - params.cdsDepositDetails.liquidationAmount);
-                    params.omniChainData.totalCdsDepositedAmount -= (params.cdsDepositDetails.depositedAmount - params.cdsDepositDetails.liquidationAmount);
+                    params.omniChainData.totalCdsDepositedAmount -= (params.cdsDepositDetails.depositedAmount -  (params.cdsDepositDetails.initialLiquidationAmount - params.params.cdsDepositDetails.liquidationAmount);
                    // update totalCdsDepositedAmountWithOptionFees
                    totalCdsDepositedAmountWithOptionFees -= (params.cdsDepositDetails.depositedAmount - params.cdsDepositDetails.liquidationAmount + params.optionsFeesToGetFromOtherChain);
                    params.omniChainData.totalCdsDepositedAmountWithOptionFees -= (
                        params.cdsDepositDetails.depositedAmount -
                        params.cdsDepositDetails.liquidationAmount +
                        params.optionFees);
                }
```
Note: also change them for the other fields.