Cuddly Leather Pigeon

Medium

# Owner will be unable to withdraw interest from treasury under certain conditions

### Summary

The `withdrawInterest` function in the Treasury contract can fail to withdraw interest under certain conditions due to improper handling of `totalInterest` and `totalInterestFromLiquidation`.

### Root Cause

In `Treasury.sol:622`, the `withdrawInterest` function only deducts the withdrawn amount from `totalInterest` without considering `totalInterestFromLiquidation`. This can lead to an underflow error if `totalInterest` is zero or a failed transaction if the withdrawal amount exceeds `totalInterest` but is still within the combined total of `totalInterest` and `totalInterestFromLiquidation`.

### Internal pre-conditions

1. `totalInterest` needs to be 0, or
2. input `amount` needs to be greater than `totalInterest` but less than or equal to `totalInterest + totalInterestFromLiquidation`.

### External pre-conditions

None

### Attack Path

**Vulnerability Path:** 

1. The owner calls `withdrawInterest` with a valid `toAddress` and an `amount` that exceeds `totalInterest` but is less than or equal to the combined available funds (`totalInterest + totalInterestFromLiquidation`).  
2. The function fails during the operation `totalInterest -= amount` due to an underflow error.  
3. The requested withdrawal is reverted, even though sufficient funds exist in `totalInterestFromLiquidation`.

### Impact

The owner cannot withdraw interest from the treasury under the conditions mentioned above, hindering the functionality of the `withdrawInterest`

### PoC

The issue arises from these lines of code [Treasury::L616-L625](https://github.com/sherlock-audit/2024-11-autonomint/blob/main/Blockchain/Blockchian/contracts/Core_logic/Treasury.sol#L616-L625):

```solidity
function withdrawInterest(
    address toAddress,
    uint256 amount
) external onlyOwner {
    require(toAddress != address(0) && amount != 0,"Input address or amount is invalid");
    require(amount <= (totalInterest + totalInterestFromLiquidation),"Treasury don't have enough interest");
    totalInterest -= amount;
    bool sent = usda.transfer(toAddress, amount);
    require(sent, "Failed to send Ether");
}
```

The function only deducts the withdrawn amount from `totalInterest`, leading to two critical issues:

1. **Underflow Error When `totalInterest` Is Exhausted:**
   - If the `totalInterest` is fully depleted (i.e., `totalInterest == 0`), even though `totalInterestFromLiquidation` may still have funds, the subtraction `totalInterest -= amount` will cause an underflow and revert the transaction. Thus `totalInterestFromLiquidation` can never be withdrawn.

2. **Improper Handling of Requests Greater Than `totalInterest`:**
   - If the withdrawal amount is valid (`amount <= totalInterest + totalInterestFromLiquidation`) but exceeds `totalInterest`, the function will revert due to the subtraction logic. For example:
     - `totalInterest = 100`, `totalInterestFromLiquidation = 50`
     - Requested `amount = 120` (valid because `120 <= 150`).
     - The function attempts `totalInterest -= 120`, causing a revert.

### Mitigation

Modify the `withdrawInterest` function to correctly handle withdrawals from both `totalInterest` and `totalInterestFromLiquidation`:

```solidity
function withdrawInterest(
    address toAddress,
    uint256 amount
) external onlyOwner {
    require(toAddress != address(0) && amount != 0, "Input address or amount is invalid");
    require(amount <= (totalInterest + totalInterestFromLiquidation), "Treasury doesn't have enough interest");
    
    if (amount <= totalInterest) {
        totalInterest -= amount;
    } else {
        uint256 remainingAmount = amount - totalInterest;
        totalInterest = 0; 
        totalInterestFromLiquidation -= remainingAmount; 
    }

    bool sent = usda.transfer(toAddress, amount);
    require(sent, "Failed to send funds");
}
```