Boxy Cider Carp

High

# Inflating the behavior of `_getDownsideFromCds` and general cumulative interest rate calculations

### Summary
- https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/Core_logic/CDS.sol#L829-L831

`CDS:updateDownsideProtected` lacks access control:
```solidity
    function updateDownsideProtected(uint128 downsideProtectedAmount) external {
        downsideProtected += downsideProtectedAmount; // @audit-info
    }

    function _updateCurrentTotalCdsDepositedAmount() private {
        if (downsideProtected > 0) {
            totalCdsDepositedAmount -= downsideProtected;
            totalCdsDepositedAmountWithOptionFees -= downsideProtected;
            downsideProtected = 0;
        }
    }
 ```
 
 Whereas it should only be calleable by the Autonomint's system contracts, such as, for instance, `borrowing`, where it is currently called:
```solidity
    ) internal {
        if (cds.getTotalCdsDepositedAmount() < downsideProtected) {
            // Call the oftOrCollateralReceiveFromOtherChains function in global variables
            globalVariables.oftOrCollateralReceiveFromOtherChains{value: feeForOFT}(
                IGlobalVariables.FunctionToDo(3),
                IGlobalVariables.USDaOftTransferData(address(treasury),downsideProtected - cds.getTotalCdsDepositedAmount()),
                // Since we don't need ETH, we have passed zero params
                IGlobalVariables.CollateralTokenTransferData(address(0),0,0,0),
                IGlobalVariables.CallingFunction.BORROW_WITHDRAW,
                msg.sender
            );
        } else {
            // updating downside protected from this chain in CDS
            cds.updateDownsideProtected(downsideProtected);
        }
        // Burn the borrow amount
        treasury.approveTokens(IBorrowing.AssetName.USDa, address(this), downsideProtected);
        bool success = usda.contractBurnFrom(address(treasury), downsideProtected);
        if (!success) revert Borrow_BurnFailed();
    }
```

### Root Cause

- Lack of `onlyGlobalOrLiquidationContract` modifier in the `CDS`'s `updateDownsideProtected` function:
```solidity
    function updateDownsideProtected(uint128 downsideProtectedAmount) external {
        downsideProtected += downsideProtectedAmount;
    }
```

### Internal pre-conditions

None.

### External pre-conditions

None.

### Attack Path

1. Any user can simply call `updateDownsideProtected` to inflate the accounting of the `downsideProtected` funds through infinitely increasing it at no costs.

### Impact

There's a variety of impacts here:

I have separated them into distinct submissions, because they have different impact and are not related to each other.

2. Renewing options may become permanently DoS'ed, breaking the core functionality:


This is because there're multiple view-only `CDS`'s functions that the `renewOptions` function uses, and solely the usage of them will cause reverts, and hence DoS:

```solidity

    /**
     * @dev renew the position by 30 days by paying usda.
     * The user will have 80% downside protection
     * @param index index of the position to renew
     */
    function renewOptions(uint64 index) external payable {
        if (
            !BorrowLib.renewOptions(
                Interfaces(
                    treasury,
                    globalVariables,
                    usda,
                    abond,
                    cds,
                    options
                ),
                index
            )
        ) revert Borrow_RenewFailed();

        // Get the deposit details
        ITreasury.GetBorrowingResult memory getBorrowingResult = treasury.getBorrowing(msg.sender, index);
        ITreasury.DepositDetails memory depositDetail = getBorrowingResult.depositDetails;
        depositDetail.optionsRenewedTimeStamp = block.timestamp;
        treasury.updateDepositDetails(msg.sender, index, depositDetail);

        // define options for lz send transaction with 400000 gas(found by testing)
        bytes memory _options = OptionsBuilder.newOptions().addExecutorLzReceiveOption(400000, 0);

        // calculting fee for lz send transaction
        MessagingFee memory fee = globalVariables.quote(
            IGlobalVariables.FunctionToDo(1),
            depositDetail.assetName,
            _options,
            false
        );

        // Calling Omnichain send function
        globalVariables.send{value: fee.nativeFee}(
            IGlobalVariables.FunctionToDo(1),
            depositDetail.assetName,
            fee,
            _options,
            msg.sender
        );

        emit Renewed(msg.sender, index, block.timestamp);
    }
```

This function calls `BorrowLib`'s `renewOptions` function:
```solidity
    /**
     * @dev renew the position by 30 days
     * @param index index of the position to renew
     */
    function renewOptions(
        IBorrowing.Interfaces memory interfaces,
        uint64 index
    ) external returns (bool) {
        // calculate options fees needs to pay to renew
        uint256 optionsFeesNeedsToPay = getOptionFeesToPay(interfaces.treasury,index);

        // check whether the user has enough options fees to pay
        if (interfaces.usda.balanceOf(msg.sender) < optionsFeesNeedsToPay) revert IBorrowing.Borrow_InsufficientBalance();

        // transfer the options fees from user to treasury
        bool sent = interfaces.usda.transferFrom(msg.sender, address(interfaces.treasury), optionsFeesNeedsToPay);

        if (!sent) revert IBorrowing.Borrow_USDaTransferFailed();

        // getting omnichain global data
        IGlobalVariables.OmniChainData memory omniChainData = interfaces.globalVariables.getOmniChainData();

        // updating last cumulative rate
        omniChainData.lastCumulativeRate = interfaces.cds.calculateCumulativeRate(uint128(optionsFeesNeedsToPay)); // @audit-info, this may become DoS'ed
        omniChainData.totalCdsDepositedAmountWithOptionFees += optionsFeesNeedsToPay;

        // updating omnichain data
        interfaces.globalVariables.setOmniChainData(omniChainData);

        return true;
    }
    
```


As you can see, this `BorrowLib`'s `renewOptions` function calls `CDS`'s `calculateCumulativeRate`:
```solidity
        // updating last cumulative rate
        omniChainData.lastCumulativeRate = interfaces.cds.calculateCumulativeRate(uint128(optionsFeesNeedsToPay)); // @audit-info, this may become DoS'ed
```

This function uses the 2 following view-only functions for its computations:
```solidity
// file: CDS.sol

    /**
     * @dev calculate cumulative rate
     * @param fees fees to split
     */
    function calculateCumulativeRate(
        uint128 fees
    ) external onlyBorrowingContract returns (uint128) {
        // get omnichain data
        IGlobalVariables.OmniChainData memory omniChainData = globalVariables.getOmniChainData();
        // call calculate cumulative rate in cds library
        (
            totalCdsDepositedAmountWithOptionFees,
            omniChainData.totalCdsDepositedAmountWithOptionFees,
            omniChainData.lastCumulativeRate
        ) = CDSLib.calculateCumulativeRate(
            fees,
            getTotalCdsDepositedAmount(),
            getTotalCdsDepositedAmountWithOptionFees(), // @audit-info
            omniChainData.totalCdsDepositedAmountWithOptionFees - omniChainData.downsideProtected,
            omniChainData.lastCumulativeRate,
            omniChainData.noOfBorrowers
        );

        return omniChainData.lastCumulativeRate;
    }
```

```solidity
    function getTotalCdsDepositedAmount() public view returns (uint256) {
        return totalCdsDepositedAmount - downsideProtected; // @audit-info
    }

    function getTotalCdsDepositedAmountWithOptionFees() public view returns (uint256) {
        return totalCdsDepositedAmountWithOptionFees - downsideProtected; // @audit info
    }
```

The impact here is that, if `downsideProtected` is set too high, the calls will automatically revert due to subtraction underflow.

3. Moreover, if the attacker doesn't want to simply impose DoS, but, for instance, just wants to inflate the cumulative interest rate computation for his profit, he can increase `downsideProtected` for it to be, for instance, `== totalCdsDepositedAmount`, or in some other way (there're multiple combinations in which the attack can be performed), because these values are later used in the computations, he can just inflate them, gaining profit himself in some rare cases, but prevalently just causing a drastic loss for the users:
```solidity
    function calculateCumulativeRate(
        uint128 _fees,
        uint256 _totalCdsDepositedAmount,
        uint256 _totalCdsDepositedAmountWithOptionFees,
        uint256 _totalGlobalCdsDepositedAmountWithOptionFees,
        uint128 _lastCumulativeRate,
        uint128 _noOfBorrowers
    ) public pure returns (uint256, uint256, uint128) {
        // check the fees is non zero
        require(_fees != 0, "Fees should not be zero");
        // if there is some deposits in cds then only increment fees
        if (_totalCdsDepositedAmount > 0) {
            _totalCdsDepositedAmountWithOptionFees += _fees;
        }
        uint128 netCDSPoolValue = uint128(
            _totalGlobalCdsDepositedAmountWithOptionFees
        );
        _totalGlobalCdsDepositedAmountWithOptionFees += _fees;
        // Calculate percentage change
        uint128 percentageChange = (_fees * PRECISION) / netCDSPoolValue;
        uint128 currentCumulativeRate;
        // If there is no borrowers
        if (_noOfBorrowers == 0 && _lastCumulativeRate == PRECISION) {
            currentCumulativeRate = PRECISION + percentageChange;
            _lastCumulativeRate = currentCumulativeRate;
        } else {
            currentCumulativeRate = _lastCumulativeRate * (PRECISION + percentageChange);
            _lastCumulativeRate = (currentCumulativeRate / PRECISION);
        }

        return (
            _totalCdsDepositedAmountWithOptionFees,
            _totalGlobalCdsDepositedAmountWithOptionFees,
            _lastCumulativeRate
        );
    }
```

As the `lastCumulativeRate` is stored in the `borrowing` contract after the computations are returned.

It is later used for determining the `normalizedAmount` during `deposit`'s, as well as it's also accounted for determining the current effective current cumulative interest rate:
```solidity
    /**
     * @dev calculates cumulative rate
     * @param noOfBorrowers total number of borrowers in the protocol
     * @param ratePerSec interest rate per second
     * @param lastEventTime last event timestamp
     * @param lastCumulativeRate previous cumulative rate
     */
    function calculateCumulativeRate(
        uint128 noOfBorrowers,
        uint256 ratePerSec,
        uint128 lastEventTime,
        uint256 lastCumulativeRate
    ) public view returns (uint256) {
        uint256 currentCumulativeRate;

        // If there is no borrowers in the protocol
        if (noOfBorrowers == 0) {
            // current cumulative rate is same as ratePeSec
            currentCumulativeRate = ratePerSec;
        } else {
            // Find time interval between last event and now
            uint256 timeInterval = uint128(block.timestamp) - lastEventTime;
            //calculate cumulative rate
            currentCumulativeRate = lastCumulativeRate * _rpow(ratePerSec, timeInterval, RATE_PRECISION);
            currentCumulativeRate = currentCumulativeRate / RATE_PRECISION;
        }
        return currentCumulativeRate;
    }
```


```solidity
        depositDetail.depositedAmount = uint128(depositingAmount);
        depositDetail.borrowedAmount = uint128(tokensToMint);
        depositDetail.optionFees = uint128(optionFees);
        depositDetail.APR = libParams.APR;
        depositDetail.exchangeRateAtDeposit = libParams.exchangeRate;

        //Update variables in treasury
        interfaces.treasury.updateHasBorrowed(msg.sender, true);
        interfaces.treasury.updateTotalBorrowedAmount(msg.sender, tokensToMint);

        // Calculate normalizedAmount
        uint256 normalizedAmount = calculateNormAmount(tokensToMint, libParams.lastCumulativeRate);

        // Update the borrower details for this index
        depositDetail.normalizedAmount = uint128(normalizedAmount);
        depositDetail.strikePrice = params.strikePrice * uint128(params.depositingAmount);

        //Update the deposit details
        interfaces.treasury.updateDepositDetails(
```

### PoC

Will be provided on request.

### Mitigation
Update the `CDS`'s `updateDownsideProtected` function in such a way that only allows Autonomint's system contracts to call it, e.g.:
```diff
-   function updateDownsideProtected(uint128 downsideProtectedAmount) external {
+   function updateDownsideProtected(uint128 downsideProtectedAmount) external onlyGlobalOrLiquidationContract {
        downsideProtected += downsideProtectedAmount;
    }
```