Boxy Cider Carp

High

# `CDS::updateDownsideProtected` lacks access control, and hence allows for DoS'ing `_updateCurrentTotalCdsDepositedAmount` and therefore `deposit`

### Summary
- https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/Core_logic/CDS.sol#L829-L831

`CDS:updateDownsideProtected` lacks access control:
```solidity
    function updateDownsideProtected(uint128 downsideProtectedAmount) external {
        downsideProtected += downsideProtectedAmount; // @audit-info
    }

    function _updateCurrentTotalCdsDepositedAmount() private {
        if (downsideProtected > 0) {
            totalCdsDepositedAmount -= downsideProtected;
            totalCdsDepositedAmountWithOptionFees -= downsideProtected;
            downsideProtected = 0;
        }
    }
 ```
 
 Whereas it should only be calleable by the Autonomint's system contracts, such as, for instance, `borrowing`, where it is currently called:
```solidity
    ) internal {
        if (cds.getTotalCdsDepositedAmount() < downsideProtected) {
            // Call the oftOrCollateralReceiveFromOtherChains function in global variables
            globalVariables.oftOrCollateralReceiveFromOtherChains{value: feeForOFT}(
                IGlobalVariables.FunctionToDo(3),
                IGlobalVariables.USDaOftTransferData(address(treasury),downsideProtected - cds.getTotalCdsDepositedAmount()),
                // Since we don't need ETH, we have passed zero params
                IGlobalVariables.CollateralTokenTransferData(address(0),0,0,0),
                IGlobalVariables.CallingFunction.BORROW_WITHDRAW,
                msg.sender
            );
        } else {
            // updating downside protected from this chain in CDS
            cds.updateDownsideProtected(downsideProtected);
        }
        // Burn the borrow amount
        treasury.approveTokens(IBorrowing.AssetName.USDa, address(this), downsideProtected);
        bool success = usda.contractBurnFrom(address(treasury), downsideProtected);
        if (!success) revert Borrow_BurnFailed();
    }
```

### Root Cause

- Lack of `onlyGlobalOrLiquidationContract` modifier in the `CDS`'s `updateDownsideProtected` function:
```solidity
    function updateDownsideProtected(uint128 downsideProtectedAmount) external {
        downsideProtected += downsideProtectedAmount;
    }
```

### Internal pre-conditions

None.

### External pre-conditions

None.

### Attack Path

1. Any user can simply call `updateDownsideProtected` to inflate the accounting of the `downsideProtected` funds through infinitely increasing it at no costs.

### Impact

There's a variety of impacts here:
1. The first impact is DoS'ing numerous functions:

As you can see, the `_updateCurrentTotalCdsDepositedAmount` function decrements `totalCdsDepositedAmount` and `totalCdsDepositedAmountWithOptionFees` by `downsideProtected`:
```solidity
    function _updateCurrentTotalCdsDepositedAmount() private {
        if (downsideProtected > 0) {
            totalCdsDepositedAmount -= downsideProtected;
            totalCdsDepositedAmountWithOptionFees -= downsideProtected;
            downsideProtected = 0;
        }
    }
 ```

Hence, any function calling this `_updateCurrentTotalCdsDepositedAmount` function internally will become DoS'ed after the attack (1):

- The function is called during CDS withdrawals in `CDS::withdraw`:
```solidity
    /**
     * @dev withdraw the deposited amount in usda with options fees,
     * upsides and liquidation gains if opted for liquidation gains during deposit.
     * @param index index of the deposit to withdraw
     */
    function withdraw(
        uint64 index,
        uint256 excessProfitCumulativeValue,
        uint256 nonce,
        bytes memory signature
    ) external payable nonReentrant whenNotPaused(IMultiSign.Functions(5)) {
        if (!_verify(FunctionName.CDS_WITHDRAW, excessProfitCumulativeValue, nonce, "0x", signature)) revert CDS_NotAnAdminTwo();
        // Check whether the entered index is present or not
        if (cdsDetails[msg.sender].index < index) revert CDS_InvalidIndex();

        CdsAccountDetails memory cdsDepositDetails = cdsDetails[msg.sender].cdsAccountDetails[index];
        if (cdsDepositDetails.withdrawed) revert CDS_AlreadyWithdrew();

        // Check whether the withdraw time limit is reached or not
        if (cdsDepositDetails.depositedTime + withdrawTimeLimit > uint64(block.timestamp)) revert CDS_WithdrawTimeNotYetReached();

        cdsDepositDetails.withdrawed = true;

        if (cdsDetails[msg.sender].index == 1 && index == 1) {
            --cdsCount;
        }

        // Get the exchange rate and eth price for all collaterals
        (uint128 weETH_ExchangeRate, uint128 ethPrice) = borrowing.getUSDValue(borrowing.assetAddress(IBorrowing.AssetName.WeETH));
        (uint128 rsETH_ExchangeRate, ) = borrowing.getUSDValue(borrowing.assetAddress(IBorrowing.AssetName.WrsETH));

        if (ethPrice == 0) revert CDS_ETH_PriceFeed_Failed();
        // Get the global omnichain data
        IGlobalVariables.OmniChainData memory omniChainData = globalVariables.getOmniChainData();

        // Calculate current value
        CalculateValueResult memory result = _calculateCumulativeValue(
            omniChainData.totalVolumeOfBorrowersAmountinWei,
            omniChainData.totalCdsDepositedAmount,
            ethPrice
        );

        // Set the cumulative vaue
        (omniChainData.cumulativeValue, omniChainData.cumulativeValueSign) = getCumulativeValue(
            omniChainData,
            result.currentValue,
            result.gains
        );

        // updating totalCdsDepositedAmount by considering downsideProtected
   @    _updateCurrentTotalCdsDepositedAmount();

        // updating global data
        if (omniChainData.downsideProtected > 0) {
            omniChainData.totalCdsDepositedAmount -= omniChainData.downsideProtected;
            omniChainData.totalCdsDepositedAmountWithOptionFees -= omniChainData.downsideProtected;
            omniChainData.downsideProtected = 0;
        }
```

- As well as during deposits, in `CDS::deposit`:
```solidity
    function deposit(
        uint128 usdtAmount,
        uint128 usdaAmount,
        bool liquidate,
        uint128 liquidationAmount,
        uint128 lockingPeriod
    ) public payable nonReentrant whenNotPaused(IMultiSign.Functions(4)) {
        // Get eth price
        uint128 ethPrice = getLatestData();

        // Check the eth price is non zero
        if (ethPrice == 0) revert CDS_ETH_PriceFeed_Failed();

        DepositResult memory result = CDSLib.deposit(
            DepositUserParams(
                usdtAmount,
                usdaAmount,
                liquidate,
                liquidationAmount,
                ethPrice,
                lastEthPrice,
                usdaLimit,
                usdtLimit,
                usdtAmountDepositedTillNow,
                totalCdsDepositedAmount,
                totalCdsDepositedAmountWithOptionFees,
                totalAvailableLiquidationAmount,
                cdsCount,
                lockingPeriod
            ),
            cdsDetails,
            Interfaces(
                treasury,
                globalVariables,
                usda,
                usdt,
                borrowing,
                CDSInterface(address(this))
            )
        );

        usdtAmountDepositedTillNow = result.usdtAmountDepositedTillNow;
        totalCdsDepositedAmount = result.totalCdsDepositedAmount;
        totalCdsDepositedAmountWithOptionFees = result.totalCdsDepositedAmountWithOptionFees;
        totalAvailableLiquidationAmount = result.totalAvailableLiquidationAmount;
        cdsCount = result.cdsCount;

        // updating totalCdsDepositedAmount by considering downsideProtected
        _updateCurrentTotalCdsDepositedAmount();

        if (ethPrice != lastEthPrice) {
    @       updateLastEthPrice(ethPrice);
        }

        // getting options since,the src don't know the dst state
        bytes memory _options = OptionsBuilder.newOptions().addExecutorLzReceiveOption(400000, 0);

        // calculting fee
        MessagingFee memory fee = globalVariables.quote(
            IGlobalVariables.FunctionToDo(1),
            IBorrowing.AssetName(0),
            _options,
            false
        );

        // Calling Omnichain send function
        globalVariables.send{value: fee.nativeFee}(
            IGlobalVariables.FunctionToDo(1),
            IBorrowing.AssetName(0),
            fee,
            _options,
            msg.sender
        );

        // emit Deposit event
        emit Deposit(
            msg.sender,
            cdsDetails[msg.sender].index,
            usdaAmount,
            usdtAmount,
            block.timestamp,
            ethPrice,
            60,
            liquidationAmount,
            liquidate
        );
    }
```

### PoC

Will be provided on request.

### Mitigation
Update the `CDS`'s `updateDownsideProtected` function in such a way that only allows Autonomint's system contracts to call it, e.g.:
```diff
-   function updateDownsideProtected(uint128 downsideProtectedAmount) external {
+   function updateDownsideProtected(uint128 downsideProtectedAmount) external onlyGlobalOrLiquidationContract {
        downsideProtected += downsideProtectedAmount;
    }
```