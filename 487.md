Vast Plum Chipmunk

High

# An attacker can steal funds from the treasury.


### Summary

The incorrect implementation of `borrowing.redeemYields()` will cause a loss of ETH for the protocol as an attacker will redeem more yields by burning `aBond` with low `ethBacked`.

### Root Cause

In [`BorrowLib.sol:L1032`](https://github.com/sherlock-audit/2024-11-autonomint/blob/main/Blockchain/Blockchian/contracts/lib/BorrowLib.sol#L1032), the protocol burns `aBond` of the wrong address, `msg.sender` instead of the user.

```solidity
    function redeemYields(
        address user,
        uint128 aBondAmount,
        address usdaAddress,
        address abondAddress,
        address treasuryAddress,
        address borrow
    ) public returns (uint256) {
        // check abond amount is non zero
        if (aBondAmount == 0) revert IBorrowing.Borrow_NeedsMoreThanZero();
        IABONDToken abond = IABONDToken(abondAddress);
        // get user abond state
        State memory userState = abond.userStates(user);
        // check user have enough abond
L992--->    if (aBondAmount > userState.aBondBalance) revert IBorrowing.Borrow_InsufficientBalance();

        ITreasury treasury = ITreasury(treasuryAddress);
        // calculate abond usda ratio
        uint128 usdaToAbondRatio = uint128((treasury.abondUSDaPool() * RATE_PRECISION) / abond.totalSupply());
        uint256 usdaToBurn = (usdaToAbondRatio * aBondAmount) / RATE_PRECISION;
        // update abondUsdaPool in treasury
        treasury.updateAbondUSDaPool(usdaToBurn, false);

        // calculate abond usda ratio from liquidation
        uint128 usdaToAbondRatioLiq = uint128((treasury.usdaGainedFromLiquidation() * RATE_PRECISION) /abond.totalSupply());
        uint256 usdaToTransfer = (usdaToAbondRatioLiq * aBondAmount) / RATE_PRECISION;
        //update usdaGainedFromLiquidation in treasury
        treasury.updateUSDaGainedFromLiquidation(usdaToTransfer, false);

        //Burn the usda from treasury
        treasury.approveTokens(
            IBorrowing.AssetName.USDa,
            borrow,
            (usdaToBurn + usdaToTransfer)
        );

        IUSDa usda = IUSDa(usdaAddress);
        // burn the usda
        bool burned = usda.contractBurnFrom(address(treasury), usdaToBurn);
        if (!burned) revert IBorrowing.Borrow_BurnFailed();

        if (usdaToTransfer > 0) {
            // transfer usda to user
            bool transferred = usda.contractTransferFrom(
                address(treasury),
                user,
                usdaToTransfer
            );
            if (!transferred) revert IBorrowing.Borrow_TransferFailed();
        }
        // withdraw eth from ext protocol
L1029--->    uint256 withdrawAmount = treasury.withdrawFromExternalProtocol(user,aBondAmount);

L1031--->    //Burn the abond from user
L1032--->    bool success = abond.burnFromUser(msg.sender, aBondAmount);      (Audit >> THIS IS INCORRECT)
        if (!success) revert IBorrowing.Borrow_BurnFailed();

        return withdrawAmount;
    }

```

From the code analysis:

1. `L992` verifies the aBond balance of `user`
2. `L1029` calculates withdrawal based on `user`'s state
3. However, `L1032` incorrectly burns aBond from `msg.sender`

This mismatch between balance verification, withdrawal calculation, and token burning creates the vulnerability.

Focus on `Treasury.sol#withdrawFromExternalProtocol`

```solidity
    function withdrawFromExternalProtocol(
        address user,
        uint128 aBondAmount
    ) external onlyCoreContracts returns (uint256) {
        if (user == address(0)) revert Treasury_ZeroAddress();

        // Withdraw from external protocols
--->    uint256 redeemAmount = withdrawFromIonicByUser(user, aBondAmount);
        // Send the ETH to user
--->    (bool sent, ) = payable(user).call{value: redeemAmount}("");
        // check the transfer is successfull or not
        require(sent, "Failed to send Ether");
        return redeemAmount;
    }
```

And focus on `Treasury.sol#withdrawFromIonicByUser`

```solidity
    function withdrawFromIonicByUser(
        address user,
        uint128 aBondAmount
    ) internal nonReentrant returns (uint256) {
        uint256 currentExchangeRate = ionic.exchangeRateCurrent();
        uint256 currentCumulativeRate = _calculateCumulativeRate(currentExchangeRate, Protocol.Ionic);

        State memory userState = abond.userStates(user);
--->    uint128 depositedAmount = (aBondAmount * userState.ethBacked) / PRECISION;
        uint256 normalizedAmount = (depositedAmount * CUMULATIVE_PRECISION) / userState.cumulativeRate;

        //withdraw amount
        uint256 amount = (currentCumulativeRate * normalizedAmount) / CUMULATIVE_PRECISION;
        // withdraw from ionic
--->    ionic.redeemUnderlying(amount);

        protocolDeposit[Protocol.Ionic].totalCreditedTokens = ionic.balanceOf(address(this));
        protocolDeposit[Protocol.Ionic].exchangeRate = currentExchangeRate;
        // convert weth to eth
        WETH.withdraw(amount);
        return amount;
    }
```

### Internal pre-conditions

1. The user must have `aBond` in their account.
2. The `aBond` balance must be greater than or equal to the amount being redeemed.

### External pre-conditions

1. The attacker must control two addresses with `aBond`.
2. The `ethBacked` value for the first address must be higher than the second address.

### Attack Path

1. The attacker uses two addresses with `aBond`. Each has `1e18` of `aBond`. The first has `ethBacked = 1.5e18`, and the second has `ethBacked = 1.0e18`.
2. The attacker calls `borrowing.sol#redeemYields` with `user = first address` and `msg.sender = second address`.
3. ETH is sent to the attacker with a higher `ethBacked` than normal, resulting in a gain for the attacker and a loss for the protocol.

### Impact

The protocol loses ETH as an attacker exploits the vulnerability in `borrowing.sol#redeemYields()`.

### Mitigation

The `BorrowLib.sol#redeemYields()` function should be modified to ensure the correct address is used for burning `aBond`.

```solidity
    function redeemYields(
        address user,
        uint128 aBondAmount,
        address usdaAddress,
        address abondAddress,
        address treasuryAddress,
        address borrow
    ) public returns (uint256) {
++      require(user == msg.sender);
        // check abond amount is non zewro
        if (aBondAmount == 0) revert IBorrowing.Borrow_NeedsMoreThanZero();
        IABONDToken abond = IABONDToken(abondAddress);
        .............
    }
```
