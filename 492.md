Vast Plum Chipmunk

Medium

# Incorrect ETH Amount Calculation Will Lead to User Loss in CDS System


### Summary

Incorrect calculation of liquidated ETH amount in [`CDS.sol#calculateLiquidatedETHTogiveToUser()`](https://github.com/sherlock-audit/2024-11-autonomint/blob/main/Blockchain/Blockchian/contracts/Core_logic/CDS.sol#L730-L776) will cause financial loss for users as the function only considers the last liquidation instead of accumulating across all liquidation indexes.

### Root Cause

In `CDS.sol:766` there is incorrect ETH amount calculation that fails to accumulate values across liquidation iterations:

```solidity
            if (currentLiquidations >= liquidationIndexAtDeposit) {
                // Loop through the liquidations that were done after user enters
                for (uint128 i = (liquidationIndexAtDeposit + 1); i <= currentLiquidations; i++) {
                    uint128 liquidationAmount = cdsDepositData.liquidationAmount;
                    if (liquidationAmount > 0) {
                        CDSInterface.LiquidationInfo memory liquidationData = omniChainCDSLiqIndexToInfo[i];

                        uint128 share = (liquidationAmount * 1e10) / uint128(liquidationData.availableLiquidationAmount);

                        profit += (liquidationData.profits * share) / 1e10;
                        cdsDepositData.liquidationAmount -= ((liquidationData.liquidationAmount * share) / 1e10);
766 --->                ethAmount = (liquidationData.collateralAmount * share) / 1e10;
                    }
                }
            }
```

This contrasts with the correct implementation in `CDSLib.sol#withdrawUser():639~663`:

```solidity
                        if (liquidationData.assetName == IBorrowing.AssetName.ETH) {
                            // increment eth amount
--->                        params.ethAmount += getUserShare(liquidationData.collateralAmount, share);
                        }
```

### Internal pre-conditions

1. User needs to have deposits in CDS system that are eligible for liquidation
2. Multiple liquidations must occur after user's deposit

### Impact

Users suffer partial loss of their entitled ETH amounts from liquidations as only the last liquidation's ETH amount is considered instead of the sum of all applicable liquidations.

### Mitigation

Modify `CDS.sol#calculateLiquidatedETHTogiveToUser()` to accumulate ETH amounts across all liquidations:

```solidity
    function calculateLiquidatedETHTogiveToUser(
        address user,
        uint64 index
    ) external view returns (uint256, uint256, uint128, uint256) {
        ...
--      ethAmount = (liquidationData.collateralAmount * share) / 1e10;
++      ethAmount += (liquidationData.collateralAmount * share) / 1e10;
        ...
    }
```
