Eager Syrup Starfish

Medium

# Legitimate user can be not able to withdraw from CDS because of underflow related to `excessProfitCumulativeValue`.

### Summary

Missing underflow check in calculation of `returnAmount` related to `excessProfitCumulativeValue` will cause `revert` of withdrawing so the user's legitimate call can be reverted.

### Root Cause

`CDS.sol#withdraw()` function is as follows.
```solidity
    function withdraw(
        uint64 index,
        uint256 excessProfitCumulativeValue,
        uint256 nonce,
        bytes memory signature
    ) external payable nonReentrant whenNotPaused(IMultiSign.Functions(5)) {
        ...
343     uint256 currentValue = cdsAmountToReturn(
            msg.sender,
            index,
            omniChainData.cumulativeValue,
            omniChainData.cumulativeValueSign,
@>          excessProfitCumulativeValue
        ) - 1; //? subtracted extra 1 wei
        ...
    }
```
And `CDS.sol#cdsAmountToReturn()` function is as follows.
```solidity
    function cdsAmountToReturn(
        address _user,
        uint64 index,
        uint128 cumulativeValue,
        bool cumulativeValueSign,
        uint256 excessProfitCumulativeValue
    ) private view returns (uint256) {
        uint256 depositedAmount = cdsDetails[_user].cdsAccountDetails[index].depositedAmount;
        uint128 cumulativeValueAtDeposit = cdsDetails[msg.sender].cdsAccountDetails[index].depositValue;
        // Get the cumulative value sign at the time of deposit
        bool cumulativeValueSignAtDeposit = cdsDetails[msg.sender].cdsAccountDetails[index].depositValueSign;
        uint128 valDiff;
        uint128 cumulativeValueAtWithdraw = cumulativeValue;

        // If the depositVal and cumulativeValue both are in same sign
        if (cumulativeValueSignAtDeposit == cumulativeValueSign) {
            // Calculate the value difference
            if (cumulativeValueAtDeposit > cumulativeValueAtWithdraw) {
                valDiff = cumulativeValueAtDeposit - cumulativeValueAtWithdraw;
            } else {
                valDiff = cumulativeValueAtWithdraw - cumulativeValueAtDeposit;
            }
            // If cumulative value sign at the time of deposit is positive
            if (cumulativeValueSignAtDeposit) {
                if (cumulativeValueAtDeposit > cumulativeValueAtWithdraw) {
                    // Its loss since cumulative val is low
                    uint256 loss = (depositedAmount * valDiff) / 1e11;
                    return (depositedAmount - loss);
                } else {
                    // Its gain since cumulative val is high
@>                  uint256 profit = (depositedAmount * (valDiff - excessProfitCumulativeValue)) / 1e11;
                    return (depositedAmount + profit);
                }
            } else {
                if (cumulativeValueAtDeposit > cumulativeValueAtWithdraw) {
                    // Its gain since cumulative val is high
@>                  uint256 profit = (depositedAmount * (valDiff - excessProfitCumulativeValue)) / 1e11;
                    return (depositedAmount + profit);
                } else {
                    // Its loss since cumulative val is low
                    uint256 loss = (depositedAmount * valDiff) / 1e11;
                    return (depositedAmount - loss);
                }
            }
        } else {
            valDiff = cumulativeValueAtDeposit + cumulativeValueAtWithdraw;
            if (cumulativeValueSignAtDeposit) {
                // Its loss since cumulative val at deposit is positive
                uint256 loss = (depositedAmount * valDiff) / 1e11;
                return (depositedAmount - loss);
            } else {
                // Its loss since cumulative val at deposit is negative
@>              uint256 profit = (depositedAmount * (valDiff - excessProfitCumulativeValue)) / 1e11;
                return (depositedAmount + profit);
            }
        }
    }
```
As you can see above, if `valDiff < excessProfitCumulativeValue`, underflow is caused.

https://github.com/sherlock-audit/2024-11-autonomint/blob/main/Blockchain/Blockchian/contracts/Core_logic/CDS.sol#L472

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

_No response_

### Impact

Missing check of underflow in `cdsAmountToReturn()` will cause revert of legitimate withdrawing even if `valDiff` is slightly less than `excessProfitCumulativeValue`.   
Because of this, user suffers from regenerating signature from protocol.

### PoC

_No response_

### Mitigation

`CDS.sol#cdsAmountToReturn()` function has to be modified as follows.
```solidity
    function cdsAmountToReturn(
        address _user,
        uint64 index,
        uint128 cumulativeValue,
        bool cumulativeValueSign,
        uint256 excessProfitCumulativeValue
    ) private view returns (uint256) {
        uint256 depositedAmount = cdsDetails[_user].cdsAccountDetails[index].depositedAmount;
        uint128 cumulativeValueAtDeposit = cdsDetails[msg.sender].cdsAccountDetails[index].depositValue;
        // Get the cumulative value sign at the time of deposit
        bool cumulativeValueSignAtDeposit = cdsDetails[msg.sender].cdsAccountDetails[index].depositValueSign;
        uint128 valDiff;
        uint128 cumulativeValueAtWithdraw = cumulativeValue;

        // If the depositVal and cumulativeValue both are in same sign
        if (cumulativeValueSignAtDeposit == cumulativeValueSign) {
            // Calculate the value difference
            if (cumulativeValueAtDeposit > cumulativeValueAtWithdraw) {
                valDiff = cumulativeValueAtDeposit - cumulativeValueAtWithdraw;
            } else {
                valDiff = cumulativeValueAtWithdraw - cumulativeValueAtDeposit;
            }
            // If cumulative value sign at the time of deposit is positive
            if (cumulativeValueSignAtDeposit) {
                if (cumulativeValueAtDeposit > cumulativeValueAtWithdraw) {
                    // Its loss since cumulative val is low
                    uint256 loss = (depositedAmount * valDiff) / 1e11;
                    return (depositedAmount - loss);
                } else {
                    // Its gain since cumulative val is high
--                  uint256 profit = (depositedAmount * (valDiff - excessProfitCumulativeValue)) / 1e11;
++                  uint256 profit = (depositedAmount * (valDiff >= excessProfitCumulativeValue ? (valDiff - excessProfitCumulativeValue) : 0) ) / 1e11;
                    return (depositedAmount + profit);
                }
            } else {
                if (cumulativeValueAtDeposit > cumulativeValueAtWithdraw) {
                    // Its gain since cumulative val is high
--                  uint256 profit = (depositedAmount * (valDiff - excessProfitCumulativeValue)) / 1e11;
++                  uint256 profit = (depositedAmount * (valDiff >= excessProfitCumulativeValue ? (valDiff - excessProfitCumulativeValue) : 0) ) / 1e11;
                    return (depositedAmount + profit);
                } else {
                    // Its loss since cumulative val is low
                    uint256 loss = (depositedAmount * valDiff) / 1e11;
                    return (depositedAmount - loss);
                }
            }
        } else {
            valDiff = cumulativeValueAtDeposit + cumulativeValueAtWithdraw;
            if (cumulativeValueSignAtDeposit) {
                // Its loss since cumulative val at deposit is positive
                uint256 loss = (depositedAmount * valDiff) / 1e11;
                return (depositedAmount - loss);
            } else {
                // Its loss since cumulative val at deposit is negative
--              uint256 profit = (depositedAmount * (valDiff - excessProfitCumulativeValue)) / 1e11;
++              uint256 profit = (depositedAmount * (valDiff >= excessProfitCumulativeValue ? (valDiff - excessProfitCumulativeValue) : 0) ) / 1e11;
                return (depositedAmount + profit);
            }
        }
    }
```