Shallow Coral Kookaburra

Medium

# `Borrowing::redeemYields` does not update `omniChainData`, so `ABOND` redeemers are paid extra

### Summary

In the `Borrowing::redeemYields` flow the `omniChainData` is not updated neither in the chain where the redemption occurs nor in the other chain. This leads to overpayment to redeemers as a bigger amount than the available is distributed. Also, it breaks accountability of `Treasury`.

### Root Cause

The `Borrowing::redeemYields` function does not update `omniChainData` neither in the chain where the redemption occurs nor in the other chain:
[borrowing.sol#L318-L333](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/Core_logic/borrowing.sol#L318-L333)
```solidity
    function redeemYields(
        address user,
        uint128 aBondAmount
    ) public returns (uint256) {
        // Call redeemYields function in Borrow Library
        return (
            BorrowLib.redeemYields(
                user,
                aBondAmount,
                address(usda),
                address(abond),
                address(treasury),
                address(this)
            )
        );
    }
```
[BorrowLib.sol#L978-L1036](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/lib/BorrowLib.sol#L978-L1036)
```solidity
    function redeemYields(
        address user,
        uint128 aBondAmount,
        address usdaAddress,
        address abondAddress,
        address treasuryAddress,
        address borrow
    ) public returns (uint256) {
        // check abond amount is non zewro
        if (aBondAmount == 0) revert IBorrowing.Borrow_NeedsMoreThanZero();
        IABONDToken abond = IABONDToken(abondAddress);
        // get user abond state
        State memory userState = abond.userStates(user);
        // check user have enough abond
        if (aBondAmount > userState.aBondBalance) revert IBorrowing.Borrow_InsufficientBalance();
        
        ITreasury treasury = ITreasury(treasuryAddress);
        // calculate abond usda ratio
        uint128 usdaToAbondRatio = uint128((treasury.abondUSDaPool() * RATE_PRECISION) / abond.totalSupply());
        uint256 usdaToBurn = (usdaToAbondRatio * aBondAmount) / RATE_PRECISION;
        // update abondUsdaPool in treasury
        treasury.updateAbondUSDaPool(usdaToBurn, false);
        
        // calculate abond usda ratio from liquidation
        uint128 usdaToAbondRatioLiq = uint128((treasury.usdaGainedFromLiquidation() * RATE_PRECISION) /abond.totalSupply());
        uint256 usdaToTransfer = (usdaToAbondRatioLiq * aBondAmount) / RATE_PRECISION;
        //update usdaGainedFromLiquidation in treasury
        treasury.updateUSDaGainedFromLiquidation(usdaToTransfer, false);
        
        //Burn the usda from treasury
        treasury.approveTokens(
            IBorrowing.AssetName.USDa,
            borrow,
            (usdaToBurn + usdaToTransfer)
        );
        
        IUSDa usda = IUSDa(usdaAddress);
        // burn the usda
        bool burned = usda.contractBurnFrom(address(treasury), usdaToBurn);
        if (!burned) revert IBorrowing.Borrow_BurnFailed();
        
        if (usdaToTransfer > 0) {
            // transfer usda to user
            bool transferred = usda.contractTransferFrom(
                address(treasury),
                user,
                usdaToTransfer
            );
            if (!transferred) revert IBorrowing.Borrow_TransferFailed();
        }
        // withdraw eth from ext protocol
        uint256 withdrawAmount = treasury.withdrawFromExternalProtocol(user,aBondAmount);
        
        //Burn the abond from user
        bool success = abond.burnFromUser(msg.sender, aBondAmount);
        if (!success) revert IBorrowing.Borrow_BurnFailed();
        
        return withdrawAmount;
    }
```

It can be seen the deduction done in `Treasury::updateUSDaGainedFromLiquidation` is no updated in the `omniChainData`. 

As `omniChainData::usdaGainedFromLiquidation` is bigger than the actual available amount, the `USDa` sent to the redeemer and the burned will be bigger.

Here is the `OmniChainData` structure:
[IGlobalVariables.sol#L10-L40](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/interface/IGlobalVariables.sol#L10-L40)
```solidity
    struct OmniChainData {
        ...
        uint256 usdaGainedFromLiquidation;
		...
    }
```

### Internal pre-conditions

None

### External pre-conditions

None

### Attack Path

None

### Impact

- Broken `Treasury` accountability
- Overpayment to `ABOND` redeemers

### PoC

None

### Mitigation

As in other functions that `omniChainData` state is updated, update `omniChainData` locally and use `GlobalVariables::send` to update the state of both chains correctly.