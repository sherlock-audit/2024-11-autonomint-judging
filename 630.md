Boxy Cider Carp

Medium

# Contrary to the intended schedule, the users can renew options as often as they wish

### Summary

There's a wrong logical expression here:
```solidity
    /**
     * @dev gets the options fees, the borrower needs to pay to renew
     * @param index the index of the position
     */
    function getOptionFeesToPay(
        ITreasury treasury,
        uint64 index
    ) public view returns (uint256) {
        // Get the deposit details
        ITreasury.GetBorrowingResult memory getBorrowingResult = treasury.getBorrowing(msg.sender, index);
        ITreasury.DepositDetails memory depositDetail = getBorrowingResult.depositDetails;

        // check if borrowerIndex in BorrowerDetails of the msg.sender is greater than or equal to Index
        if (getBorrowingResult.totalIndex >= index) {
            // check the position is not withdrew or liquidated
            if (depositDetail.withdrawed && depositDetail.liquidated) // @audit-info: shouldn't it use the OR || operator here
                revert IBorrowing.Borrow_AlreadyWithdrewOrLiquidated();

            // check the user is eligible to renew position
            if (
                block.timestamp <
                depositDetail.optionsRenewedTimeStamp + 15 days &&
                block.timestamp >
                depositDetail.optionsRenewedTimeStamp + 30 days // @audit-info: what is the logic behind these weird expressions lines?
            ) revert IBorrowing.Borrow_DeadlinePassed();

```

The intent (as was clarified after I consulted with the sponsor) was to limit the `renewOptions` function's calls to the following schedule: `renewOptions` for a specific borrowing index of the given user should be called:

1. when the time elapsed since the last options renewal is less than 30 days
2. And when the time elapsed since the last options renewal is more than 15 days.

I.e. during the 15th day - 30th day period.

### Root Cause

_No response_

### Internal pre-conditions

_No response_

### External pre-conditions

The problem is with restricting this schedule and timing, so it arises when the user calls `renewOptions` in the "> 30 days elapsed" OR the "< 15 days elapsed since the last options renewal" period.

In a case like that, the function will not revert, in spite of what the schedule attempted to dictate.


![image](https://github.com/user-attachments/assets/d0665105-16a4-45ac-8071-a2d899cc57ce)

### Attack Path

- https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/lib/BorrowLib.sol#L445-L465

### Impact

Users can renew options at the times when they are not eligible for that.

First of all, as the options fees precision is not that high, it means that oftentimes, when renewing the options at times when not intended by the schedule, users can renew at MUCH MUCH lowered rates.

Moreover, users can renew the options even if they missed the previous deadlines, and violated the schedule's terms. Because it will never be the right case for the `Borrow_DeadlinePassed` error to be thrown, as the logic in the aforementioned `if` checks is completely flawed.


```solidity
    function updateRatePerSecByUSDaPrice(uint32 usdaPrice) public onlyAdmin {
        // Check the usda price is non zero
        if (usdaPrice <= 0) revert Borrow_NeedsMoreThanZero();
        // Get the new apr and rate per sec to update from library
        (ratePerSec, APR) = BorrowLib.calculateNewAPRToUpdate(usdaPrice);
    }

    /**
     * @dev renew the position by 30 days by paying usda.
     * The user will have 80% downside protection
     * @param index index of the position to renew
     */
    function renewOptions(uint64 index) external payable {
        if (
            !BorrowLib.renewOptions(
                Interfaces(
                    treasury,
                    globalVariables,
                    usda,
                    abond,
                    cds,
                    options
                ),
                index
            )
        ) revert Borrow_RenewFailed();

        // Get the deposit details
        ITreasury.GetBorrowingResult memory getBorrowingResult = treasury.getBorrowing(msg.sender, index);
        ITreasury.DepositDetails memory depositDetail = getBorrowingResult.depositDetails;
        depositDetail.optionsRenewedTimeStamp = block.timestamp;
        treasury.updateDepositDetails(msg.sender, index, depositDetail);

        // define options for lz send transaction with 400000 gas(found by testing)
        bytes memory _options = OptionsBuilder.newOptions().addExecutorLzReceiveOption(400000, 0);

        // calculting fee for lz send transaction
        MessagingFee memory fee = globalVariables.quote(
            IGlobalVariables.FunctionToDo(1),
            depositDetail.assetName,
            _options,
            false
        );

        // Calling Omnichain send function
        globalVariables.send{value: fee.nativeFee}(
            IGlobalVariables.FunctionToDo(1),
            depositDetail.assetName,
            fee,
            _options,
            msg.sender
        ); // @audit-info: check this out! delayed call before updates!

        emit Renewed(msg.sender, index, block.timestamp);
    }
```


```solidity

    /**
     * @dev calculate the options fees per second
     */
    function calculateOptionsFeesPerSec(
        uint128 optionsFees
    ) public pure returns (uint128) {
        // gets the options fees per second by dividing the options fees by number of seconds in 30 days
        return (optionsFees * OPTIONS_FEES_PRECISION) / 30 days;
    }
```


Moreover, the borrowing data **may** be outdated too:
```solidity
    function getOptionFeesToPay(
        ITreasury treasury,
        uint64 index
    ) public view returns (uint256) {
        // Get the deposit details
        ITreasury.GetBorrowingResult memory getBorrowingResult = treasury.getBorrowing(msg.sender, index);
        ITreasury.DepositDetails memory depositDetail = getBorrowingResult.depositDetails;

        // check if borrowerIndex in BorrowerDetails of the msg.sender is greater than or equal to Index
        if (getBorrowingResult.totalIndex >= index) {
            // check the position is not withdrew or liquidated
            if (depositDetail.withdrawed && depositDetail.liquidated) // @audit-info: shouldn't it use the OR || p
                revert IBorrowing.Borrow_AlreadyWithdrewOrLiquidated();

            // check the user is eligible to renew position
```

### PoC

_No response_

### Mitigation

Consider using the corrected `if` conditional expression:
```diff
            // check the user is eligible to renew position
            if (
                block.timestamp <
-               depositDetail.optionsRenewedTimeStamp + 15 days &&
+               depositDetail.optionsRenewedTimeStamp + 15 days ||
                block.timestamp >
                depositDetail.optionsRenewedTimeStamp + 30 days
            ) revert IBorrowing.Borrow_DeadlinePassed();
```