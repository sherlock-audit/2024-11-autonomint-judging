Shallow Coral Kookaburra

High

# `liqAmountToGetFromOtherChain` does not take into account `cdsProfitsForOtherChain` which causes `CDS` depositors to lose profits in `GlobalVariables::liquidationType1`

### Summary

When `GlobalVariables::liquidationType1` occurs, [`cdsProfits`](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/Core_logic/borrowLiquidation.sol#L212) is calculated to compensate `CDS` depositors for providing liquidity. `cdsProfits` is split proportionally in the two chains, which means that the liquidity needed from `CDS` will get reduced proportionally by `cdsProfits`:
[borrowLiquidation.sol#L227-L238](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/Core_logic/borrowLiquidation.sol#L227-L238)```
```solidity
// calculate the amount of usda to get from Other chains
uint256 liqAmountToGetFromOtherChain = BorrowLib.getLiquidationAmountProportions(
        liquidationAmountNeeded,
        cds.getTotalCdsDepositedAmount(),
        omniChainData.totalCdsDepositedAmount - omniChainData.downsideProtected,
        cds.totalAvailableLiquidationAmount(),
        omniChainData.totalAvailableLiquidationAmount
    );
// Calculate CDS profits to give to the users of other chains.
uint128 cdsProfitsForOtherChain = BorrowLib.getCdsProfitsProportions(liquidationAmountNeeded, uint128(liqAmountToGetFromOtherChain), cdsProfits);
// USDa from cds to get from this chain.
uint128 cdsAmountToGetFromThisChain = (liquidationAmountNeeded - uint128(liqAmountToGetFromOtherChain)) - (cdsProfits - cdsProfitsForOtherChain);
```

However, the `liqAmountToGetFromOtherChain` does not get a reduction due to `cdsProfitsForOtherChain` as `cdsAmountToGetFromThisChain` does, making the other chain lose `cdsProfitsForOtherChain` amount of `USDa`.

### Root Cause

In `GlobalVariables::liquidationType1`, the `liqAmountToGetFromOtherChain` is required from the other chains through `GlobalVariables::oftOrCollateralReceiveFromOtherChains`:
[borrowLiquidation.sol#L283-L290](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/Core_logic/borrowLiquidation.sol#L283-L290)
```solidity
globalVariables.oftOrCollateralReceiveFromOtherChains{value: msg.value}(
    IGlobalVariables.FunctionToDo(3),
    IGlobalVariables.USDaOftTransferData(address(treasury),liqAmountToGetFromOtherChain),
    // Since we don't need ETH, we have passed zero params
    IGlobalVariables.CollateralTokenTransferData(address(0),0,0,0),
    IGlobalVariables.CallingFunction.BORROW_LIQ,
    admin
);
```

`GlobalVariables::oftOrCollateralReceiveFromOtherChains` sends a request through LayerZero `_lzSend` function to the other chain to receive `liqAmountToGetFromOtherChain`:
[GlobalVariables.sol#L224-L237](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/Core_logic/GlobalVariables.sol#L224-L237)
[GlobalVariables.sol#L322-L329](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/Core_logic/GlobalVariables.sol#L322-L329)
```solidity
bytes memory _payload = abi.encode(
    functionToDo,
    callingFunction,
    callingFunction == CallingFunction.CDS_WITHDRAW ? oftTransferData.tokensToSend : 0,
    callingFunction == CallingFunction.BORROW_WITHDRAW ? oftTransferData.tokensToSend : 0,
    callingFunction == CallingFunction.BORROW_LIQ ? oftTransferData.tokensToSend : 0,
    CDSInterface.LiquidationInfo(0,0,0,0,IBorrowing.AssetName.DUMMY,0),
    0,
    oftTransferData,
    collateralTokenTransferData,
    omniChainData,
    IBorrowing.AssetName.DUMMY,
    s_collateralData[IBorrowing.AssetName.DUMMY]
);
...
receipt = _lzSend(
    dstEid,
    _payload,
    _options,
    _fee,
    payable(refundAddress)
);
```

The `_payload` is received in the other chain in `GlobalVariables::_lzReceive` and the `liqAmountToGetFromOtherChain` is decoded as `OAppData::liqAmountToRemove`.

Then, that amount is sent to the other chain:
[GlobalVariables.sol#L547-L567](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/Core_logic/GlobalVariables.sol#L547-L567)
```solidity
if (oappData.functionToDo == FunctionToDo.TOKEN_TRANSFER) {
    // getting options since,the src don't know the dst state
    _options = OptionsBuilder.newOptions().addExecutorLzReceiveOption(60000,0);
    // Define SendParam to send OFT
    SendParam memory _sendParam = SendParam(
        dstEid,
        bytes32(uint256(uint160(oappData.oftTransferData.recipient))),
        oappData.oftTransferData.tokensToSend,
        oappData.oftTransferData.tokensToSend,
        _options,
        "0x",
        "0x"
    );
	// Get the fee by calling quote function
    _fee = usda.quoteSend(_sendParam, false);
    // Since we need usda only, we have passed others zero
    treasury.transferFundsToGlobal([0, 0, 0, oappData.oftTransferData.tokensToSend]);
    // Send oft to src chain
    usda.send{value: _fee.nativeFee}(_sendParam, _fee, address(this));
    // If need only collateral or collateral and USDa
} else if ...
```

Finally, the `CDS` is updated to take account for the reduction in deposits:
[GlobalVariables.sol#L629-L634](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/Core_logic/GlobalVariables.sol#L629-L634)
```solidity
// Update the total cds deposited amount with options fees
cds.updateTotalCdsDepositedAmountWithOptionFees(uint128(oappData.optionsFeesToRemove + oappData.cdsAmountToRemove + oappData.liqAmountToRemove));
// Update the total cds deposited amount
cds.updateTotalCdsDepositedAmount(uint128(oappData.cdsAmountToRemove + oappData.liqAmountToRemove));
// Update the total available liquidation amount in CDS
cds.updateTotalAvailableLiquidationAmount(oappData.liqAmountToRemove);
```

Unlike the execution in `BorrowLiquidation::liquidationType1`(source chain), in the destination chain, the total amount (`liqAmountToGetFromOtherChain`) is deducted from the `CDS` deposits. In the origin chain (where liquidation occurred), the amount substracted is the liquidation needed in the origin chain minus the `cdsProfits` proportion for the origin chain:
[borrowLiquidation.sol#L242-L244](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/Core_logic/borrowLiquidation.sol#L242-L244)
```solidity
cds.updateTotalCdsDepositedAmount(cdsAmountToGetFromThisChain);
cds.updateTotalCdsDepositedAmountWithOptionFees(cdsAmountToGetFromThisChain);
cds.updateTotalAvailableLiquidationAmount(cdsAmountToGetFromThisChain);
```

This means that `CDS` in the destination chain lost `cdsProfitsForOtherChain`.

### Internal pre-conditions

None

### External pre-conditions

None

### Attack Path

None

### Impact

- Loss of funds of `CDS` in the destination chain when liquidation type 1 is occurring.
- OmniChainData is broken as it takes into account the total amount of `cdsProfits` from all values, but in reality only `CDS` benefits from the source chain are taken into account.
[borrowLiquidation.sol#L248-L250](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/Core_logic/borrowLiquidation.sol#L248-L250)
```solidity
omniChainData.totalCdsDepositedAmount -= liquidationAmountNeeded - cdsProfits; //! need to revisit this
omniChainData.totalCdsDepositedAmountWithOptionFees -= liquidationAmountNeeded - cdsProfits;
omniChainData.totalAvailableLiquidationAmount -= liquidationAmountNeeded - cdsProfits;
```

### PoC

None

### Mitigation

When sending to the other chain the amount of `USDa` to get, substract the `cdsProfitsForOtherChain` amount:
```diff
globalVariables.oftOrCollateralReceiveFromOtherChains{value: msg.value}(
    IGlobalVariables.FunctionToDo(3),
-   IGlobalVariables.USDaOftTransferData(address(treasury),liqAmountToGetFromOtherChain),
+   IGlobalVariables.USDaOftTransferData(address(treasury),(liqAmountToGetFromOtherChain - cdsProfitsForOtherChain)),
    // Since we don't need ETH, we have passed zero params
    IGlobalVariables.CollateralTokenTransferData(address(0),0,0,0),
    IGlobalVariables.CallingFunction.BORROW_LIQ,
    admin
);
```