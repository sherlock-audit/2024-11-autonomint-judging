Bumpy Lava Seal

High

# `CDSLib::withdrawUser` function reverts when exchangeRate is decreased

### Summary

In `CDSLib::withdrawUser` [function](https://github.com/sherlock-audit/2024-11-autonomint/blob/main/Blockchain/Blockchian/contracts/lib/CDSLib.sol#L668C1-L669C99), while calculating yields for weETH/rsETH accrued during the period from liquidation till withdrawal, the converted amount of weETHAmount/rsETHAmount is subtracted from the original deposited weETHAmount/rsETHAmount.

The `weETH/rsETH` amount was first [converted](https://github.com/sherlock-audit/2024-11-autonomint/blob/main/Blockchain/Blockchian/contracts/Core_logic/borrowLiquidation.sol#L225) in its `ETHValue` at the time of liquidation using the exchangeRate at that time (`T0`). 

```solidity
        liquidationInfo = CDSInterface.LiquidationInfo(
            liquidationAmountNeeded,
            cdsProfits,
            depositDetail.depositedAmount,
            omniChainData.totalAvailableLiquidationAmount,
            depositDetail.assetName,
@>          ((depositDetail.depositedAmount * exchangeRate) / 1 ether)
        );
```

Now, at the time (`T1`) of withdrawal, the converted `weETHAmountInETHValue` is reverted to its `weETHAmount/rsETHAmount` using the `exchangeRate` at `T1` and subtracted from the original/actual deposited `weETHAmount/rsETHAmount`.


```solidity
                interfaces.treasury.updateYieldsFromLiquidatedLrts(
@>                  weETHAmount - ((weETHAmountInETHValue * 1 ether) /  params.weETH_ExchangeRate) + 
@>                  rsETHAmount - ((rsETHAmountInETHValue * 1 ether) /  params.rsETH_ExchangeRate)
                );
```

Herein lies the problem, if the `exchangeRate` at `T1` is reduced (even by 0.02%) as compared to the `exchangeRate` at `T0`, the withdrawal transaction will revert.

### Root Cause

The root cause of the bug is in [these](https://github.com/sherlock-audit/2024-11-autonomint/blob/main/Blockchain/Blockchian/contracts/lib/CDSLib.sol#L668C1-L669C99) lines because if `exchangeRate` at the time of withdrawal is less than the `exchangeRate` at the time of liquidation, then computed amount will always be lesser than the original weETH/rsETH amount and transaction will revert due to error `[FAIL: panic: arithmetic underflow or overflow (0x11)]`

### Internal pre-conditions

1. Borrower has deposited weETH or rsETH as collateral
2. CDS depositor has opted for liquidation
3. Admin has opted LiquidationType1

### External pre-conditions

1. ExchangeRateT0 > ExchangeRateT1

This could happen as we can see in the below chart taken from [coingecko](https://www.coingecko.com/en/coins/wrapped-eeth/eth)
<img width="851" alt="Screenshot 2024-12-27 at 12 24 53â€¯PM" src="https://sherlock-files.ams3.digitaloceanspaces.com/gh-images/ba3cd718-71cc-41fd-b3cb-6fdccaa889cd" />


### Attack Path

_No response_

### Impact

In certain conditions, CDS depositor will not be able to withdraw his funds.

### PoC

```solidity
        uint256 weETHAmount = 2000e18; // amount deposited by borrower as collateral
        uint256 userShare = 3; // CDS depositor's share in liquidation amount 30%
        uint256 weETHAmountUserShare = weETHAmount * userShare / 10; // user's share of deposited collateral
        /////// weETHAmountUserShare = 600000000000000000000

        uint256 ethPrice1 = 3376.96e6; // ETH price at T0
        uint256 weEthPrice1 = 3558.37e6; // weETH price at T0
        uint256 exchangeRate1 = (weEthPrice1 * 1e18 / ethPrice1); // exchangeRate at T0
        /////// exchangeRate1 = 1053719913768596607
        
        uint256 weETHAmountInEthValue1 = weETHAmountUserShare * exchangeRate1 / 1e18; // ETH value of collateral at T0
        /////// weETHAmountInEthValue1 = 632231948261157964200
        
        uint256 ethPrice2 = 3376.95e6; // ETH price at T1
        uint256 weEthPrice2 = 3557.50e6; // weETH price at T1
        uint256 exchangeRate2 = (weEthPrice2 * 1e18 / ethPrice2); // exchangeRate at T1
        /////// exchangeRate2 = 1053465405173307274 //@> a fall of 0.024%

        uint256 weETHAmountRevertedOnNewRate = weETHAmountInEthValue1 * 1e18 / exchangeRate2; // amounted computed with yield at T1
        /////// weETHAmountRevertedOnNewRate = 600144955075338689485

        //@> revert condition = 600e18 - 600.14e18
```

### Mitigation

Modify [this](https://github.com/sherlock-audit/2024-11-autonomint/blob/main/Blockchain/Blockchian/contracts/lib/CDSLib.sol#L667C1-L670C19) code as below, basically wrapping existing code in an `if conditional`

```diff
++              if((weETHAmount >= ((weETHAmountInETHValue * 1 ether) /  params.weETH_ExchangeRate)) && (rsETHAmount >= ((rsETHAmountInETHValue * 1 ether) /  params.rsETH_ExchangeRate))) {
                    interfaces.treasury.updateYieldsFromLiquidatedLrts(
                        weETHAmount - ((weETHAmountInETHValue * 1 ether) /  params.weETH_ExchangeRate) + 
                        rsETHAmount - ((rsETHAmountInETHValue * 1 ether) /  params.rsETH_ExchangeRate)
                    );
                

                    // Calculate the weeth and rseth amount without yields
                    weETHAmount = weETHAmount - (weETHAmount - ((weETHAmountInETHValue * 1 ether) / params.weETH_ExchangeRate));
                    
                    rsETHAmount = rsETHAmount - (rsETHAmount - ((rsETHAmountInETHValue * 1 ether) / params.rsETH_ExchangeRate));
++              }
```