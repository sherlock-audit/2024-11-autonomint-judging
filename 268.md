Shallow Coral Kookaburra

High

# In `Borrowing::depositTokens`, `ethVolatility` is introduced by user and hasn't sanity checks leading to incorrect option fees pricing

### Summary

When a user deposits collateral to borrow `USDa` though the `Borrowing::depositTokens` function, the protocol, to create downside protection for that debt position, has a Collateral Options mechanism. The borrower has to pay an upfront fee to create this option, which goes to the `CDS` depositors to compensate for providing liquidity for the option. One of the parameters that is used to calculate the option fee price is `ethVolatility`, which is a parameter value controlled by the user that allows the user to pay less than expected.

### Root Cause

In the `Borrowing::depositTokens` function, the parameter received has the following [structure](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/interface/IBorrowing.sol#L79-L85):
```solidity
   struct BorrowDepositParams {
        IOptions.StrikePrice strikePercent;
        uint64 strikePrice;
        uint256 volatility;
        AssetName assetName;
        uint256 depositingAmount;
    }
```

The values `strikePercent`, `strikePrice`, and `volatility`, are used to calculate the option price fee the borrower will have to pay to open the position for that particular amount of collateral that is depositing (or for the amount that is borrowing, same thing). 

The input has zero control or sanitization, and the [calculation of the option fee](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/Core_logic/Options.sol#L104-L150) is the following (simplified to keep the report as concise as possible): 
```solidity
    function calculateOptionPrice(
        uint128 _ethPrice,
        uint256 _ethVolatility,
        uint256 _amount,
        StrikePrice _strikePrice
    ) public view returns (uint256) {
        uint256 a = _ethVolatility;
        uint256 ethPrice = _ethPrice; /*getLatestPrice();*/
        ...
        // Calculate the current ETH vault value
        uint256 E = omniChainData.totalVolumeOfBorrowersAmountinUSD + (_amount * _ethPrice);
        ...
        // Calculate b, cdsvault to eth vault ratio
        uint256 b = (cdsVault * 1e2 * OPTION_PRICE_PRECISION) / E;
        uint256 baseOptionPrice = ((sqrt(10 * a * ethPrice)) * PRECISION) / OPTION_PRICE_PRECISION + ((3 * PRECISION * OPTION_PRICE_PRECISION) / b); 
        ...
        uint256 optionPrice;
        // Calculate option fees based on strike price chose by user
        if (_strikePrice == StrikePrice.FIVE) {
            // constant has extra 1e3 and volatility have 1e8
            optionPrice = baseOptionPrice + (400 * OPTION_PRICE_PRECISION * baseOptionPrice) / (3 * a);
        } else if (_strikePrice == StrikePrice.TEN) {
            optionPrice = baseOptionPrice + (100 * OPTION_PRICE_PRECISION * baseOptionPrice) / (3 * a);
        } else if (_strikePrice == StrikePrice.FIFTEEN) {
            optionPrice = baseOptionPrice + (50 * OPTION_PRICE_PRECISION * baseOptionPrice) / (3 * a);
        } else if (_strikePrice == StrikePrice.TWENTY) {
            optionPrice = baseOptionPrice + (10 * OPTION_PRICE_PRECISION * baseOptionPrice) / (3 * a);
        } else if (_strikePrice == StrikePrice.TWENTY_FIVE) {
            optionPrice = baseOptionPrice + (5 * OPTION_PRICE_PRECISION * baseOptionPrice) / (3 * a);
        } else {
            revert("Incorrect Strike Price");
        }
        return ((optionPrice * _amount) / PRECISION) / USDA_PRECISION;
    }
```

As you can see, `ethVolatility`, renamed as `a` in the formulas, is a key value to calculate the option fee, and is left its correct use to the user which is an untrusted party. 


### Internal pre-conditions

None

### External pre-conditions

None

### Attack Path

- Borrower deposits collateral to get `USDa` in `Borrowing` with a malicious `ethVolatility` value.

### Impact

Borrower gets a downside protection paying substantially less fees than expected. 
`CDS` depositors are not compensated correctly for providing liquidity.

### PoC

Add the following PoC to `test/foundry/Borrowing.t.sol`:

```solidity
function test_PoC_ethVolatilityManipulable() public view {
        uint128 ethPrice = 1500e2;
        uint256 ethVolatilityNormal = 50622665;
        uint256 ethVolatilityForged = 10000000;
        uint256 amount = 1 ether;
        IOptions.StrikePrice strikePrice = IOptions.StrikePrice.FIVE;

        uint256 optionFeeNormal =
            contractsA.option.calculateOptionPrice(ethPrice, ethVolatilityNormal, amount, strikePrice);

        uint256 optionFeeForged =
            contractsA.option.calculateOptionPrice(ethPrice, ethVolatilityForged, amount, strikePrice);

        assert(optionFeeNormal > optionFeeForged);

    }
```

To run the test:
1) `anvil --fork-url https://mainnet.mode.network`
2) `forge test --fork-url http://127.0.0.1:8545 --mt test_PoC_ethVolatilityManipulable`

### Mitigation

Change the way the function receives the input value. One option would be to consume the `ethVolatility` directly on-chain from a 3rd party contract, or to make one yourself and keep it updated. 
The 3rd party contract is the best option since you reduce centralization and manipulation from your system.