Shallow Coral Kookaburra

High

# Liquidation type 2 does not update the deposit as liquidated

### Summary

When a borrower position is liquidated with liquidation type 2, the `BorrowLiquidation::liquidationType2` function does not update the borrower position information nor put it in “liquidated” status. As position is not updated, the owner of the position could wait until the `borrowingHealth > 0.8` and withdraw the funds.

### Root Cause

Unlike `BorrowLiquidation::liquidationType1`, `BorrowLiquidation::liquidationType2` does not update the borrower position as “liquidated”:
[borrowLiquidation.sol#L324-L366](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/Core_logic/borrowLiquidation.sol#L324-L366)
```solidity
    function liquidationType2(
        address user,
        uint64 index,
        uint64 currentEthPrice
    ) internal {
        // Get the borrower and deposit details
        ITreasury.GetBorrowingResult memory getBorrowingResult = treasury.getBorrowing(user, index);
        ITreasury.DepositDetails memory depositDetail = getBorrowingResult.depositDetails;
        require(!depositDetail.liquidated, "Already Liquidated");
        
        // Check whether the position is eligible for liquidation
        uint128 ratio = BorrowLib.calculateEthPriceRatio(depositDetail.ethPriceAtDeposit, currentEthPrice);
        require(ratio <= 8000, "You cannot liquidate, ratio is greater than 0.8");
        
        uint256 amount = BorrowLib.calculateHalfValue(depositDetail.depositedAmountInETH);
        
        // Convert the ETH into WETH
        weth.deposit{value: amount}();
        // Approve it, to mint sETH
        bool approved = weth.approve(address(wrapper), amount);
        
        if (!approved) revert BorrowLiq_ApproveFailed();
        
        // Mint sETH
        wrapper.mint(amount);
        // Exchange sETH with sUSD
        synthetix.exchange(
            0x7345544800000000000000000000000000000000000000000000000000000000,
            amount,
            0x7355534400000000000000000000000000000000000000000000000000000000
        );
        
        // Calculate the margin
        int256 margin = int256((amount * currentEthPrice) / 100);
        // Transfer the margin to synthetix
        synthetixPerpsV2.transferMargin(margin);
        
        // Submit an offchain delayed order in synthetix for short position with 1X leverage
        synthetixPerpsV2.submitOffchainDelayedOrder(
            -int((uint(margin * 1 ether * 1e16) / currentEthPrice)),
            currentEthPrice * 1e16
        );
    }
```

This incorrect behavior, allow the owner of the position to call `Borrowing::withDraw` when the `borrowingHealth` is back to the "safe zone": `> 0.8`.

### Internal pre-conditions

None

### External pre-conditions

None

### Attack Path

None

### Impact

A liquidated position could be withdrawn anyway, as if it had not been liquidated.

### PoC

None

### Mitigation

Update `depositDetail` information properly in `Treasury` during liquidation type 2. 