Shallow Coral Kookaburra

Medium

# `usdtAmountDepositedTillNow` is not updated when `CDS::redeemUSDT` is called making the minimum `USDT` amount deposited not enforced anymore

### Summary

One of the two [**Peg Stabilization Modules**](https://docs.autonomint.com/autonomint/autonomint-1/autonomint/stablecoin-peg-stability#peg-stabilization-modules) is [**Utilizing USDT deposited in dCDS for immediate re-deemability**](https://docs.autonomint.com/autonomint/autonomint-1/autonomint/stablecoin-peg-stability#utilizing-usdt-deposited-in-dcds-for-immediate-re-deemability)which is implemented by the `CDS::redeemUSDT` function. To make sure that there will be a sufficient amount of `USDT` to redeem in case of a depeg, the protocol enforces `CDS` depositors to at least deposit `usdtLimit`. In the beginning of the `CDS` creation, depositors won't be able to deposit `USDa` until the `usdtLimit` amount of deposited `USDT` is reached. The issue occurs when through `CDS::redeemUSDT` `USDT` is redeemed because the amount redeemed is not deducted from `usdtAmountDepositedTillNow`.  `usdtAmountDepositedTillNow` is key to enforce users maintain a minimum amount of `USDT` deposited in `CDS` when `CDS::deposit` is called.
However, as the minimum amount deposited decreases with redemptions, the Peg Stabilization Module will lose its functionality.

### Root Cause

To understand the relevance of `usdtAmountDepositedTillNow` in `CDS`, the `CDS::deposit` function needs to be reviewed:
[CDSLib.sol#L437-L458](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/lib/CDSLib.sol#L437-L458)
```solidity
        // Check whether the usdt limit is reached or not
        if (omniChainData.usdtAmountDepositedTillNow < params.usdtLimit) {
            // If the usdtAmountDepositedTillNow and current depositing usdt amount is lesser or
            // equal to usdtLimit
            if (
                (omniChainData.usdtAmountDepositedTillNow +
                    params.usdtAmount) <= params.usdtLimit
            ) {
                // Check the totalDepositingAmount is usdt amount
                if (params.usdtAmount != totalDepositingAmount) revert CDSInterface.CDS_NeedsUSDTOnly();
            } else {
                revert CDSInterface.CDS_SurplusUSDT();
            }
        } else {
            // usda amount must be 80% of totalDepositingAmount
            if (
                params.usdaAmount <
                (params.usdaLimit * totalDepositingAmount) / 100
            ) revert CDSInterface.CDS_RequiredUSDaNotMet();
            // Check the user has enough usda
            if (interfaces.usda.balanceOf(msg.sender) < params.usdaAmount) revert CDSInterface.CDS_Insufficient_USDa_Balance(); // check if user has sufficient USDa token
        }
```

In the previous piece of code from `CDSLib::deposit` it can be seen that the protocol enforces `CDS` users to first deposit at least `usdtLimit` (value set by admin in `CDS:setUsdtLimit`).  Then, in every `USDT` deposit, the `usdtAmountDepositedTillNow` is updated:
[CDSLib.sol#L524-L525](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/lib/CDSLib.sol#L524-L525)
```solidity
// updating global data
omniChainData.usdtAmountDepositedTillNow += params.usdtAmount;
```

However, the `CDS::redeemUSDT` function does not decrement `usdtAmountDepositedTillNow`, which puts `CDS` in a position where it will not force `USDT` to be deposited, so the Peg Stability Module continues to function:
[CDSLib.sol#L382-L418](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/lib/CDSLib.sol#L382-L418)
```solidity
    function redeemUSDT(
        CDSInterface.Interfaces memory interfaces,
        uint256 burnedUSDaInRedeem,
        uint128 usdaAmount,
        uint64 usdaPrice,
        uint64 usdtPrice
    ) external returns (uint256) {
        // CHeck the usdaAmount is non zero
        if (usdaAmount == 0) revert CDSInterface.CDS_NeedsMoreThanZero();
        // Check the user has enough usda balance
        if (interfaces.usda.balanceOf(msg.sender) < usdaAmount)
            revert CDSInterface.CDS_Insufficient_USDa_Balance();
        // Increment burnedUSDaInRedeem
        burnedUSDaInRedeem += usdaAmount;
        // GET the omnichain data
        IGlobalVariables.OmniChainData memory omniChainData = interfaces.globalVariables.getOmniChainData();
        // Increment burnedUSDaInRedeem
        omniChainData.burnedUSDaInRedeem += usdaAmount;
        // burn usda
        bool transfer = interfaces.usda.burnFromUser(msg.sender, usdaAmount);
        if (!transfer) revert CDSInterface.CDS_TransferFailed(IBorrowing.AssetName.USDa);
        // calculate the USDT USDa ratio
        uint128 usdtAmount = ((usdaPrice * usdaAmount) / usdtPrice);
        
        interfaces.treasury.approveTokens(IBorrowing.AssetName.TUSDT, address(interfaces.cds), usdtAmount);
        // Transfer usdt to user
        bool success = interfaces.usdt.transferFrom(
            address(interfaces.treasury),
            msg.sender,
            usdtAmount
        );
        if (!success) revert CDSInterface.CDS_TransferFailed(IBorrowing.AssetName.TUSDT);
        
        interfaces.globalVariables.setOmniChainData(omniChainData);
        
        return burnedUSDaInRedeem;
    }
```

### Internal pre-conditions

None

### External pre-conditions

None

### Attack Path

None

### Impact

The "Utilizing USDT deposited in dCDS for immediate re-deemability" Peg Stabilization Module functioning will not be always operative. 

### PoC

None

### Mitigation

Decrease `usdtAmountDepositedTillNow` when `CDS::redeemUSDT` is executed:
In `CDSLib::redeemUSDT`, before the `omniChainData` update, add:
```diff
+ usdtAmountDepositedTillNow -= usdtAmount;
+ omniChainData.usdtAmountDepositedTillNow -= usdtAmount;
```