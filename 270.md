Shallow Coral Kookaburra

High

# In `Borrowing::depositTokens`, `strikePrice` is introduced by the user but it should be calculated based on `strikePercent` to avoid manipulation

### Summary

When the `Borrowing::depositTokens` function is called, it receives one parameter `IBorrowing::BorrowDepositParams` which contains `IOptions.StrikePrice strikePercent` and `uint64 strikePrice`. Those two values should be consistent with each other, if `strikePercent` is `5%`, then `strikePrice` should be `ethPrice * 1.05 `. However, that is not enforced nor controlled and lets the user withdraw more collateral than expected when the `borrowingHealth` is greater than `1`.

### Root Cause

The `Borrowing::depositTokens` function receives as the only parameter the following `struct`:
[IBorrowing.sol#L79-L85](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/interface/IBorrowing.sol#L79-L85)
```solidity
struct BorrowDepositParams {
    IOptions.StrikePrice strikePercent;
    uint64 strikePrice;
    uint256 volatility;
    AssetName assetName;
    uint256 depositingAmount;
}
```

Where `strikePercent` is used in `Options::calculateOptionPrice` to calculate option fees, and `strikePrice` is used in `Options::calculateStrikePriceGains` when the user calls `Borrowing:withDraw` to know how much collateral to return.

The `strikePrice` value is directly used to store the `strikePrice * depositingAmount` in the `depositDetail` struct (this value is never previously modified):
[BorrowLib.sol#L754](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/lib/BorrowLib.sol#L754)
```solidity
depositDetail.strikePrice = params.strikePrice * uint128(params.depositingAmount);
```

 Those two values should be consistent with each other, if `strikePercent` is `5%`, then `strikePrice` should be `ethPrice * 1.05`. So even if `strikePrice` were a value introduced by a honest user, we cannot be sure that the value is correct because `ethPrice` is got during the transaction.

As `strikePrice` can be set as any value, a malicious user could set this to the lowest amount possible, so when its position has a `borrowingHealth` grater than `1`, it will receive more funds than expected when `Borrowing::withDraw` is called.

Note that the excess in funds that the malicious user could get is not an amount that could later withdraw anyways. The debt position is cleared and that withdrawn excess is a direct theft to the protocol.

The calculation of the `collateralToReturn` when withdrawing is the following:
(deleted irrelevant parts such as the cases in which `borrowingHealth <= 1`)
[BorrowLib.sol#L472-L516](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/lib/BorrowLib.sol#L472-L516)
```solidity
    function calculateCollateralToReturn(
        IBorrowing.CalculateCollateralToReturn_Param memory params
    ) public view returns (IBorrowing.CalculateCollateralToReturn_Result memory) {
        uint128 collateralToReturn;
        //Calculate current depositedAmount value
        uint128 depositedAmountvalue = (params.depositDetail.depositedAmountInETH * params.depositDetail.ethPriceAtDeposit) / params.ethPrice;
        uint128 collateralRemainingInWithdraw;
        // If the health is greater than 1
        if (params.borrowingHealth > 10000) { // @audit HERE
            // If the ethPrice is higher than deposit ethPrice,call withdrawOption in options contract
            collateralToReturn = (depositedAmountvalue + params.options.calculateStrikePriceGains(
                        params.depositDetail.depositedAmountInETH,
                        params.depositDetail.strikePrice,
                        params.ethPrice
                    )
                );
            // increment the difference between collatearla to  return and deposited amount in collateralRemainingInWithdraw
            collateralRemainingInWithdraw = params.depositDetail.depositedAmountInETH - collateralToReturn;
            params.collateralRemainingInWithdraw += collateralRemainingInWithdraw;
            params.omniChainData.collateralRemainingInWithdraw += collateralRemainingInWithdraw;
            // increment the difference between collatearl to return and deposited amount
            // in collateralValueRemainingInWithdraw in usd
            params.collateralValueRemainingInWithdraw += (collateralRemainingInWithdraw *params.ethPrice);
            params.omniChainData.collateralValueRemainingInWithdraw += (collateralRemainingInWithdraw *params.ethPrice);
            // If the health is one collateralToReturn is depositedAmountvalue itself
        } else if (params.borrowingHealth == 10000) {
            collateralToReturn = depositedAmountvalue;
            // If the health is between 0.8 and 1 collateralToReturn is depositedAmountInETH itself
        }
        
        ...
        
        // Calculate the 50% of colllateral to return
        collateralToReturn = calculateHalfValue(params.depositDetail.depositedAmountInETH) - collateralRemainingInWithdraw;
        return IBorrowing.CalculateCollateralToReturn_Result(
                collateralToReturn,
                params.collateralRemainingInWithdraw,
                params.collateralValueRemainingInWithdraw,
                params.omniChainData
            );
    }
```

If `collateralToReturn` is bigger, the user will receive more fund when withdrawing, then the call to the `Options::calculateStrikePriceGains` which receives `strikePrice` as parameter is the following: 
(remember that now `strikePrice = actualEthStrikePrice * depositedAmount`, names of variables are not the used in code just a representation)

[Options.sol#L77-L95](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/Core_logic/Options.sol#L77-L95)
```solidity
    function calculateStrikePriceGains(
        uint128 depositedAmount,
        uint128 strikePrice,
        uint64 ethPrice
    ) external view onlyBorrowingContract returns (uint128) {
        // Check the input params are non zero
        require(depositedAmount != 0 && strikePrice != 0 && ethPrice != 0,"Zero inputs in options");
        uint64 currentEthPrice = ethPrice;
        // Calculate current deposited ETH value in USD
        uint128 currentEthValue = depositedAmount * currentEthPrice;
        uint128 ethToReturn;
        // If the current value is greater, then user will get eth
        if (currentEthValue > strikePrice) {
            ethToReturn = (currentEthValue - strikePrice) / currentEthPrice;
        } else {
            ethToReturn = 0;
        }
        return ethToReturn;
    }
```

As we can see, the lowest value the user can input for `strikePrice` in `Borrowing::depositTokens` is `1` (because zero will revert here). Therefore, `stikePrice` is now the `depositingAmount`. The attacker needs `ethToReturn` to be as big as possible so `collateralToReturn` is the biggest possible and he receives more funds.

Finally, `Treasury` will be called to send user those previously calculated funds in `calculateCollateralToReturn`:

[BorrowLib.sol#L941-L947](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/lib/BorrowLib.sol#L941-L947)
```solidity
bool sent = interfaces.treasury.withdraw(
    msg.sender, params.toAddress, result.collateralToReturn, params.exchangeRate, params.index);
```

[Treasury.sol#L253-L264](https://github.com/sherlock-audit/2024-11-autonomint/blob/0d324e04d4c0ca306e1ae4d4c65f0cb9d681751b/Blockchain/Blockchian/contracts/Core_logic/Treasury.sol#L253-L264)
```solidity
        if (depositDetails.assetName == IBorrowing.AssetName.ETH) {
            // Send the ETH to Borrower
            (bool sent, ) = payable(toAddress).call{value: amount}("");
            require(sent, "Failed to send Collateral");
        } else {
            uint256 upside = ((depositDetails.depositedAmountInETH * 50) / 100) - amount;
            uint256 upsideToDeduct = (upside * 1 ether) / exchangeRate;
            // Transfer collateral to user
            bool sent = IERC20(borrow.assetAddress(depositDetails.assetName)).transfer(toAddress, depositDetails.depositedAmount - upsideToDeduct);
            // check the transfer is successfull or not
            require(sent, "Failed to send Collateral");
        }
```

### Internal pre-conditions

- The user debt position has to have a `borrowingHealth > 1`

### External pre-conditions

None

### Attack Path

- Attacker calls `Borrowing::depositTokens` with `depositParams.strikePrice` set to `1` (or any other value that will result in a smaller `strikePrice` than expected).
- Attacker waits until `borrowingHealth > 1`
- Attacker calls `Borrowing::withDraw` and gets an excess of funds.

### Impact

Incorrect calculation in withdrawal of funds. A malicious user receives more funds than expected. 
The amount in excess that receives is: 
- In case ETH is the deposited asset, when `params.strikePrice == 1`, then `strikePrice` stored is equal to `depositedAmount`. So the excess the user will get is the `ethReturn` amount with the malicious `strikePrice` minus the `ethReturn` that he will actually get if the value were not malicious:
	 `excess = (((depositedAmount * currentEthPrice) - depositedAmount) / currentEthPrice) - (((depositedAmount * currentEthPrice) - strikePriceCorrect) / currentEthPrice)` 
	Note: `strikePriceCorrect` is `ethPriceAtDeposit * params.strikePrice`. Difference is huge.

- In case `weETH` or `rsETH` are the deposited assets, the calculation is similar when the deposit asset is ETH, but an additional calculation is performed:
	- `amountMalicious = ((depositedAmount * currentEthPrice) - depositedAmount) / currentEthPrice)`
	- `amountCorrect = ((depositedAmount * currentEthPrice) - strikePriceCorrect) / currentEthPrice)`
	- `upsideMalicious = ((depositDetails.depositedAmountInETH * 50) / 100) - amountMalicious`
	- `upsideToDeductMalicious = (upsideMalicious * 1 ether) / exchangeRate`
	- `upsideCorrect = ((depositDetails.depositedAmountInETH * 50) / 100) - amountCorrect`
	- `upsideToDeductCorrect = (upsideCorrect * 1 ether) / exchangeRate`
	- `excess = upsideToDeductCorrect - upsideToDeductMalicious`
	Note: `upsideToDeductMalicious` will always be smaller because `amountMalicious` will be bigger.


### PoC

None

### Mitigation

Do not allow user to pass the `strikePrice` parameter. Instead, calculate it based on `strikePercent` (which is sanitized), and the `ethPrice` calculated in the transaction.