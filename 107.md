Low Tangerine Cod

High

# lastEthprice is not updated on cds's withdraw

### Summary

lastEthprice in borrower contract is not updated on cds's deposit which leads to incorrectly tracked `cdsPoolValue` which means cds holders might be stuck.

### Root Cause
When call happens: `cds.withdraw`-> `borrowing.calculateRatio` `lastEthprice` not updated.

net P/L of CDS Pool will be always updating as eth price is not tracked properly. E.x. two cds withdraw did call at the same time, this means profits will be doubled or losses will be doubled for the pool and not only once, until someone call withdraw on borrowing contract because thats the only place where lastEthprice updated.
```solidity
    function calculateRatio(
        uint256 amount,
        uint128 currentEthPrice,
        uint128 lastEthprice,
        uint256 noOfDeposits,
        uint256 totalCollateralInETH,
        uint256 latestTotalCDSPool,
        IGlobalVariables.OmniChainData memory previousData
    ) public pure returns (uint64, IGlobalVariables.OmniChainData memory) {
        uint256 netPLCdsPool;

        // Calculate net P/L of CDS Pool
        // if the current eth price is high
        if (currentEthPrice > lastEthprice) {
            // profit, multiply the price difference with total collateral
->            netPLCdsPool = (((currentEthPrice - lastEthprice) * totalCollateralInETH) / USDA_PRECISION) / 100;
        } else {
            // loss, multiply the price difference with total collateral
->            netPLCdsPool = (((lastEthprice - currentEthPrice) * totalCollateralInETH) / USDA_PRECISION) / 100;
        }

        uint256 currentVaultValue;
        uint256 currentCDSPoolValue;

        // Check it is the first deposit
        if (noOfDeposits == 0) {
            // Calculate the ethVault value
            previousData.vaultValue = amount * currentEthPrice;
            // Set the currentEthVaultValue to lastEthVaultValue for next deposit
            currentVaultValue = previousData.vaultValue;

            // Get the total amount in CDS
            // lastTotalCDSPool = cds.totalCdsDepositedAmount();
            previousData.totalCDSPool = latestTotalCDSPool;

            // BAsed on the eth prices, add or sub, profit and loss respectively
            if (currentEthPrice >= lastEthprice) {
                currentCDSPoolValue = previousData.totalCDSPool + netPLCdsPool;
            } else {
                currentCDSPoolValue = previousData.totalCDSPool - netPLCdsPool;
            }

            // Set the currentCDSPoolValue to lastCDSPoolValue for next deposit
            previousData.cdsPoolValue = currentCDSPoolValue;
            currentCDSPoolValue = currentCDSPoolValue * USDA_PRECISION;
        } else {
            // find current vault value by adding current depositing amount
            currentVaultValue = previousData.vaultValue + (amount * currentEthPrice);
            previousData.vaultValue = currentVaultValue;

            // BAsed on the eth prices, add or sub, profit and loss respectively
            if (currentEthPrice >= lastEthprice) {
                previousData.cdsPoolValue += netPLCdsPool;
            } else {
                previousData.cdsPoolValue -= netPLCdsPool;
            }
            previousData.totalCDSPool = latestTotalCDSPool;
            currentCDSPoolValue = previousData.cdsPoolValue * USDA_PRECISION;
        }

        // Calculate ratio by dividing currentEthVaultValue by currentCDSPoolValue,
        // since it may return in decimals we multiply it by 1e6
        uint64 ratio = uint64((currentCDSPoolValue * CUMULATIVE_PRECISION) / currentVaultValue);
        return (ratio, previousData);
    }
```
[Blockchian/contracts/lib/BorrowLib.sol#L171](https://github.com/sherlock-audit/2024-11-autonomint/blob/main/Blockchain/Blockchian/contracts/lib/BorrowLib.sol#L171)

Which means ratio will not be tracked properly and some cds holders will not be able to withdraw due to the fact they will be forced to protect against positions that do not exist.

```solidity
        // Check whether after withdraw cds have enough funds to protect borrower's collateral
        if (withdrawResult.omniChainData.totalVolumeOfBorrowersAmountinWei != 0) {
            if (borrowing.calculateRatio(0, ethPrice) < (2 * CDSLib.RATIO_PRECISION)) revert CDS_NotEnoughFundInCDS();
        }
```
### Internal pre-conditions

none

### External pre-conditions

### Attack Path

always happening

### Impact

1. Cds holder will be forced to protect against positions that do not exist. Their deposits will be stuck
2. borrowers will not be able to deposits when they should

### PoC

_No response_

### Mitigation

```diff
    function calculateRatio(
        uint256 amount,
        uint128 currentEthPrice
    ) public onlyCoreContracts returns (uint64) {
        return _calculateRatio(amount, currentEthPrice);
    }

    /**
     * @dev calculate the ratio of CDS Pool/Eth Vault
     * @param amount amount to be depositing
     * @param currentEthPrice current eth price in usd
     */
    function _calculateRatio(
        uint256 amount,
        uint128 currentEthPrice
    ) private returns (uint64 ratio) {
        if (currentEthPrice == 0) revert Borrow_GettingETHPriceFailed();

        // Get the omnichain data
        IGlobalVariables.OmniChainData memory omniChainData = globalVariables.getOmniChainData();
        // Get the return values from calculateRatio in library to store
        (ratio, omniChainData) = BorrowLib.calculateRatio(
            amount,
            currentEthPrice,
            lastEthprice,
            omniChainData.totalNoOfDepositIndices,
            omniChainData.totalVolumeOfBorrowersAmountinWei,
            omniChainData.totalCdsDepositedAmount -
                omniChainData.downsideProtected,
            omniChainData // using global data instead of individual chain data
        );
+        lastEthprice = uint128(currentEthPrice);

        // updating global data
        globalVariables.setOmniChainData(omniChainData);
    }


```