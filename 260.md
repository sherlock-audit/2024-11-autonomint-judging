Eager Syrup Starfish

Medium

# User can suffer from wrong `ethAmount` calculated from `CDS.sol#calculateLiquidatedETHTogiveToUser()`.

### Summary

Wrong calculation of `ethAmount` in `CDS.sol#calculateLiquidatedETHTogiveToUser()` will cause suffering of user.

https://github.com/sherlock-audit/2024-11-autonomint/blob/main/Blockchain/Blockchian/contracts/Core_logic/CDS.sol#L766

### Root Cause

- In `CDS.sol:766`, there is wrong calculation `ethAmount` not considering loop through liquidation indexes.
```solidity
    for (uint128 i = (liquidationIndexAtDeposit + 1); i <= currentLiquidations; i++) {
        uint128 liquidationAmount = cdsDepositData.liquidationAmount;
        if (liquidationAmount > 0) {
            CDSInterface.LiquidationInfo memory liquidationData = omniChainCDSLiqIndexToInfo[i];

            uint128 share = (liquidationAmount * 1e10) / uint128(liquidationData.availableLiquidationAmount);

            profit += (liquidationData.profits * share) / 1e10;
            cdsDepositData.liquidationAmount -= ((liquidationData.liquidationAmount * share) / 1e10);
766@>       ethAmount = (liquidationData.collateralAmount * share) / 1e10;
        }
    }
```
As you can see above, ethAmount is calculated only from last liquidation not considering all liquidation indexes.   
- In `CDSLib.sol#withdrawUser():639~663`, `ethAmount` is calculated as following.
```solidity
    for (uint128 i = (liquidationIndexAtDeposit + 1); i <= params.omniChainData.noOfLiquidations; i++) {
        uint128 liquidationAmount = params.cdsDepositDetails.liquidationAmount;
        // If the user available liquidation is non zero
        if (liquidationAmount > 0) {
            CDSInterface.LiquidationInfo memory liquidationData = omniChainCDSLiqIndexToInfo[i];
            // Calculate the share by taking ratio between
            // User's available liquidation amount and total available liquidation amount
            uint128 share = (liquidationAmount * 1e10) / uint128(liquidationData.availableLiquidationAmount);
            // Update users available liquidation amount
            params.cdsDepositDetails.liquidationAmount -= getUserShare(liquidationData.liquidationAmount, share);
            // Based on the collateral type calculate the liquidated collateral to give to user
            if (liquidationData.assetName == IBorrowing.AssetName.ETH) {
                // increment eth amount
@>              params.ethAmount += getUserShare(liquidationData.collateralAmount, share);
            } else if (liquidationData.assetName == IBorrowing.AssetName.WeETH) {
                // increment weeth amount and weth amount value
@>              weETHAmount += getUserShare(liquidationData.collateralAmount, share);
@>              weETHAmountInETHValue += getUserShare(liquidationData.collateralAmountInETHValue, share);
            } else if (liquidationData.assetName == IBorrowing.AssetName.WrsETH) {
                // increment rseth amount and rseth amount value
@>              rsETHAmount += getUserShare(liquidationData.collateralAmount, share);
@>              rsETHAmountInETHValue += getUserShare(liquidationData.collateralAmountInETHValue, share);
            }
        }
    }
```

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

_No response_

### Impact

User suffers from wrong `ethAmount` calculated from `CDS.sol#calculateLiquidatedETHTogiveToUser()`.


### PoC

_No response_

### Mitigation

`CDS.sol#calculateLiquidatedETHTogiveToUser()` function has to be modified as follows.
```solidity
    function calculateLiquidatedETHTogiveToUser(
        address user,
        uint64 index
    ) external view returns (uint256, uint256, uint128, uint256) {
        ...

        uint256 returnAmount = (cdsDepositData.normalizedAmount * omniChainData.lastCumulativeRate) / CDSLib.PRECISION;
        if (cdsDepositData.optedLiquidation) {
            returnAmount -= cdsDepositData.liquidationAmount;
            uint128 currentLiquidations = omniChainData.noOfLiquidations;
            uint128 liquidationIndexAtDeposit = cdsDepositData.liquidationindex;
            if (currentLiquidations >= liquidationIndexAtDeposit) {
                // Loop through the liquidations that were done after user enters
                for (uint128 i = (liquidationIndexAtDeposit + 1); i <= currentLiquidations; i++) {
                    uint128 liquidationAmount = cdsDepositData.liquidationAmount;
                    if (liquidationAmount > 0) {
                        CDSInterface.LiquidationInfo memory liquidationData = omniChainCDSLiqIndexToInfo[i];

                        uint128 share = (liquidationAmount * 1e10) / uint128(liquidationData.availableLiquidationAmount);

                        profit += (liquidationData.profits * share) / 1e10;
                        cdsDepositData.liquidationAmount -= ((liquidationData.liquidationAmount * share) / 1e10);
--                      ethAmount = (liquidationData.collateralAmount * share) / 1e10;
++                      ethAmount += (liquidationData.collateralAmount * share) / 1e10;
                    }
                }
            }
            returnAmount += cdsDepositData.liquidationAmount;
        } else {
            ethAmount = 0;
            profit = 0;
        }
        return (returnAmount, priceChangePL, profit, ethAmount);
    }
```